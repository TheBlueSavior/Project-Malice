Class PM_Fusionite : PMUnaffMonsterBase
{
	int user_numbullets;
	Default
	{
		PMMonsterBase.CodexLore 
		"Fusionites were a race of psychic archdemons that ruled alongside Heresy Lords, now reduced to nothing more than vestigial bodies operating on a metal chariot.
		 Their tendrils are deeply rooted inside these personal chariots, essentially acting as their main power source to launch bolts of plasma aswell as their only mode of transportation.
		 Killing them causes their bodies to overload the metal chariot with too much energy, causing all of its internal systems to erupt in a violent explosion.

		 Has a variety of ways it can spew plasma at the player before it needs to reload, but keep your distance when it dies.";
		Health 500;
		Height 64;
		Mass 600;
		PainChance 100;
		Speed 7;
		Radius 48;
		Scale 1.1;
		MaxStepHeight 24;
		MaxDropOffHeight 24;
		Tag "Fusionite";
		Obituary "%o was liquefied by a Fusionite.";
		Species "Arachnotron";
		+DONTHARMSPECIES
		SEESOUND "Fusionite/Sight";
		PAINSOUND "Fusionite/Pain";
		DEATHSOUND "Fusionite/Death";
		ACTIVESOUND "Fusionite/Active";
		Bloodtype "PM_NormalCyberBloodBase";
		TeleFogSourceType "PM_SmallDemonicTeleportFog";
		TeleFogDestType "PM_DemonicTeleportFog";
	}

	States
	{
		Spawn:
			FSPI A 0 NoDelay { user_numbullets = random(5,75); }
		Idle:
			FSPI A 10 PM_Look();
			Loop;
		Look:
			FSPI A 2 A_StartSound("GenericArachWalk",12,CHANF_OVERLAP);
			FSPI ABBCC 2 PM_LookingForPlayer();
			TNT1 A 0 A_JumpIfTargetinLOS("Spotted",270,JLOSF_DEADNOJUMP);
			FSPI D 2 A_StartSound("GenericArachWalk",12);
			FSPI DEEFF 2 PM_LookingForPlayer();
			Loop;
		Spotted:
			TNT1 A 0 PM_AlertSound();
		See:
			FSPI A 2 A_StartSound("GenericArachWalk",12);
			FSPI ABBCC 2 A_Chase();
			FSPI D 2 A_StartSound("GenericArachWalk",12,CHANF_OVERLAP);
			FSPI DEEFF 2 A_Chase();
			Loop;
		Missile:
			TNT1 A 0 A_Jump(100,"Shotgun","Rapid");
			FSPI G 15
			{
			A_StartSound("Fusionite/Charge");
			A_FaceTarget();
			}
			FSPI G 3 A_FaceTarget(8,8);
			TNT1 A 0 A_StartSound("GenericPlasmaFire");
			FSPI H 3 Light("GreenBall3")
			{
				if(user_numbullets < 0)
				{
					return ResolveState("NoAmmo");
				}
				for(int i=3;i>0;i--)
				A_SpawnProjectile("PM_GreenFusioniteFireFlare",22,0,0,CMF_OFFSETPITCH|CMF_AIMDIRECTION,pitch*1);
				A_SpawnProjectile("PM_FusionShot",22,0,0,CMF_OFFSETPITCH|CMF_AIMDIRECTION,pitch*1);
				user_numbullets--;
				return ResolveState(null);
			}
			FSPI G 1 A_CheckSight("See"); //A_SpidRefire
			TNT1 A 0 A_Jump(10,"See");
			Goto Missile+2;
		Shotgun:
			FSPI A 9 A_FaceTarget();
			TNT1 A 0 A_StartSound("Fusionite/ScatterCharge",13);
			FSPI GGGGGG 5 Light("GreenBall2") A_FaceTarget();
			TNT1 A 0 A_StartSound("HellSoldier/ChargeReady2",14);
			FSPI GGGGG 5 Light("GreenBall2") A_FaceTarget();
			TNT1 A 0 
			{
				user_numbullets = (user_numbullets - 7);
			}
			TNT1 A 0 A_JumpIf(user_numbullets < 0, "ShotgunNoAmmo");
			TNT1 A 0 A_StartSound("Fusionite/Scattershot",15);
			FSPI H 3 Light("GreenBall3")
			{
				A_FaceTarget();
				A_SpawnProjectile("PM_GreenFusioniteFlare",22,0);
				for(int i=2;i>0;i--)
				A_SpawnProjectile("PM_GreenFusioniteFireFlare",22,0);
				for(int i=5;i>0;i--)
				A_SpawnProjectile("PM_ScatterFusionShot",22,0,frandom(-12,12),CMF_OFFSETPITCH,frandom(-3,3));
			}
			FSPI G 3 A_FaceTarget();
			TNT1 A 0 A_StartSound("GenericSteam");
			FSPI GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1;
			Goto See;
		ShotgunNoAmmo:
			TNT1 A 0 A_StartSound("Fusionite/ScattershotDry",15);
			FSPI H 3 Light("GreenBall3")
			{
				A_FaceTarget();
				A_SpawnProjectile("PM_GreenFusioniteFireFlare",22,0);
                for(int i=7;i>0;i--)
				A_SpawnItemEx("PM_VentSmoke",10,0,32,random(5,-5),random(5,-5),random(1,10),random(-10,10),0,0,0);
			}
			FSPI G 3 A_FaceTarget();
			Goto NoAmmo;
		Rapid:
			FSPI A 8 
			{
				A_FaceTarget();
				A_SetAngle(angle+random(-40,40));
				A_SetPitch(pitch+random(-15,50));
				A_StartSound("Fusionite/SmallCharge");
			}
			FSPI GGG 5 A_FaceTarget();
			FSPI G 1 A_FaceTarget(3,3);
			FSPI H 1 Light("GreenBall4")
			{
				if(user_numbullets < 0)
				{
					return ResolveState("NoAmmo");
				}
				A_SpawnProjectile("PM_GreenFusioniteFireFlare",22,0);
				A_SpawnProjectile("PM_SmallFusionShot",22,0,frandom(-4,4),CMF_OFFSETPITCH|CMF_AIMDIRECTION,pitch+frandom(-3,3));
				user_numbullets--;
				return ResolveState(null);
			}
			FSPI G 1 A_CheckSight("See");
			Goto Rapid+4;
		NoAmmo:
			TNT1 A 0 A_StartSound("PM/ArachnoCooldown");
			TNT1 A 0 {bNOPAIN = True;}
			TNT1 AAAAAAAAAAAA 0 A_SpawnParticle("ffaa21",SPF_FULLBRIGHT|SPF_RELATIVE,105,random(8,20),0,random(5,10),random(-3,3),32,random(3,8),random(-2,2),random(-2,2),0,0,0,1,-1,-0.4);
			FSPI GGGGGGGGGGGGGGGGGGGG 1 Light("Avatarfireball4")
			{
			A_SpawnParticleEx
			(
			"", TexMan.CheckForTexture("LENYA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT,
			lifetime: 3, size: 100.0, xoff: 10, zoff: 30, startalphaf: 0.9
			);
			A_SpawnParticleEx
			( "ffbc6b", TexMan.CheckForTexture("SPKOA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		    lifetime: random(1,70), size: frandom(0.4,10), xoff: random(3,5), yoff: random(-5,5), zoff: 30,
			velx: frandom(1,4), vely: frandom(-0.6,0.6), velz: frandom(-0.6,0.6), startalphaf: 1.0,
			fadestepf: -0.5, sizestep: -0.2, startroll: random(-180,180) );
			A_SpawnParticleEx
			( "8a8a8a", TexMan.CheckForTexture("GNSMK0"),
			style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,100),
			size: frandom(30,70), xoff: random(3,5), yoff: random(-5,5), zoff: 30,
			velx: frandom(15,20), vely: frandom(-0.8,0.8), velz: frandom(-0.8,0.8), startalphaf: 0.3, fadestepf: -0.5,
			sizestep: 0.2, startroll: random(-180,180) );
			}
			TNT1 A 0 A_StartSound("PM/BigVentLoop",CHAN_7,CHANF_LOOP);
			FSPI GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 Light("Avatarfireball4")
			{
			A_SpawnParticleEx
			( "", TexMan.CheckForTexture("LENYA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT,
			lifetime: 2, size: 80.0, xoff: 10, zoff: 30, startalphaf: 0.7 );
			A_SpawnParticleEx
			( "8a8a8a", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,
			lifetime: random(20,100), size: frandom(30,70), xoff: random(16,20), yoff: random(-5,5), zoff: 30,
			velx: frandom(-0.8,4), vely: frandom(-0.8,0.8), velz: frandom(-0.8,0.8), startalphaf: 0.3,
			fadestepf: -0.5, sizestep: 0.2, startroll: random(-180,180) );
			A_SpawnParticleEx
			( "C8C8C8", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,
			lifetime: random(50,100), size: 3, xoff: 10, yoff: random(-5,5), zoff: 30, vely: frandom(12,15), velz: frandom(-0.2,0.3),
			accely: -0.4, startalphaf: 0.5, fadestepf: -1, sizestep: 3, startroll: random(-180,180)
			);
			A_SpawnParticleEx
			( "C8C8C8", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,
			lifetime: random(50,100), size: 3, xoff: 10, yoff: random(-5,5), zoff: 30, vely: frandom(-12,-15), velz: frandom(-0.2,0.3),
			accely: 0.4, startalphaf: 0.5, fadestepf: -1, sizestep: 3, startroll: random(-180,180) );
			}
			TNT1 A 0 A_StopSound(CHAN_7);
			TNT1 A 0 A_StartSound("PM/BigVentEnd",CHAN_7);
		ZReload:
			TNT1 A 0 A_StartSound("PM/ArachnoReloadStart",CHAN_5);
			FSPI A 20 A_FaceTarget();
			FSPI A 29 
			{
			A_StartSound("PM/ArachnoReload",CHAN_6);
			A_SpawnItemEx("PM_EmptyHeavyMag",-5,10,20,frandom(-1,1),frandom(5,8),random(5,8));
			A_SpawnItemEx("PM_EmptyHeavyMag",-8,10,20,frandom(-1,1),frandom(5,8),random(5,8));
			A_FaceTarget(); 
            }
			FSPI A 29 
			{
			A_StartSound("PM/ArachnoReload",CHAN_6);
			A_SpawnItemEx("PM_EmptyHeavyMag",-5,-10,20,frandom(-1,1),frandom(-5,-8),random(5,8));
			A_SpawnItemEx("PM_EmptyHeavyMag",-8,-10,20,frandom(-1,1),frandom(-5,-8),random(5,8));
			A_FaceTarget(); 
            }
			TNT1 A 0 { bNOPAIN = false; user_numbullets = random(55,75); }
			TNT1 A 0 A_StartSound("PM/ArachnoReloadEnd",CHAN_5);
			FSPI GGG 5 A_FaceTarget();
			TNT1 A 0 A_Jump(56,"Missile");
			Goto See;
		Pain:
			TNT1 A 0;
			TNT1 A 0 A_StopSound(5);
			FSPI I 12 A_Pain();
			Goto See;
		Death:
			TNT1 A 0;
			FSPI J 15 PM_Scream();
			TNT1 A 0 A_SpawnItemEx("PM_OFireBallExplode",random(-10,10),0,random(-10,10));
			FSPI K 4 Light("AvatarFireball4") A_SpawnItemEx("PM_OrangeEmbersSpawn",0,0,30);
			TNT1 A 0 A_StartSound("ArachChassisExplode",CHAN_5);
			TNT1 A 0 A_SpawnItemEx("PM_OrangeEmbersSpawn",0,0,25);
			TNT1 A 0 A_SpawnItemEx("PM_OFireBallExplode",random(-10,10),0,30);
			TNT1 A 0 A_Explode(25,130);
			TNT1 AAAAA 0 A_SpawnItemEx("PM_ArachnotronGenericExplosionFlames",0,0,35,random(-7,7),random(-7,7),random(0,7));
			TNT1 A 0 A_NoBlocking();
			TNT1 AAAAAA 0 A_SpawnItemEx("PM_NormalMeatChunkLauncher",0,0,random(10,30),0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("PM_NormalBloodyCloudBig",0,0,20,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 AAAAA 0 A_SpawnItemEx("PM_NormalBigMeatChunkLauncher",0,0,random(10,30),0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 FFFFFFFFFFFFFFFFFFFF 0 A_SpawnItemEX("PM_ArachExpGoreSplash", 0, 0, 32, random (4, 10), random(4,10), random (4, 15), random(0, 360));
			TNT1 A 0 A_SpawnItemEx("PM_ArachExpMist",0,0,20);
			TNT1 A 0 A_StartSound("ArachExpGore",CHAN_6);
			FSPI L 6 Light("TyrantMExp");
			FSPI MN 6 Light("TyrantMExp") A_SpawnItemEX("PM_ArachExpGoreSplash", 0, 0, 15, random (-3, 3), random(-3,3), random (4, 15));
			TNT1 A 0 A_SpawnItemEx("PM_OFireBallExplode",random(-10,10),0,random(-10,10));
			TNT1 A 0 A_StartSound("ArachCrash",CHAN_7);
			FSPI O 6 Light("AvatarFireball4");
			FSPI PPPPPPP 3 A_SpawnItemEX("PM_ArachExpGoreSplash", 0, 0, 15, random (-3, 3), random(-3,3), random (4, 15));
			FSPI P 1 A_KillMaster();
			FSPI P -1;
			Stop;
		Raise:
			FSPI ONMLKJ 6;
			Goto Look;
	}
}

Class PM_FusionShot : PMProjectileBase
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 24;
		FastSpeed 26;
		DamageFunction (random(15,25));
		Scale 1.35;
		RenderStyle "Add";
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD
		SeeSound "GenericPlasmaFire";
		DeathSound "GenericPlasmaExp";
		Decal "ArachnotronScorch";
		Projectile;
        PMProjectileBase.WhizSound "PM/PlasmaWhiz";
        PMProjectileBase.WhizRangeCheck 250;
	}

	States
	{
		Spawn:
			APPL AAABBB 1 Bright Light("GreenBall4")
			{
			  PM_CheckWhiz();
			  A_SetRoll(random(0,259),SPF_INTERPOLATE);
			  A_SpawnParticleEx("",TexMan.CheckForTexture("DTPRA0"),style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: 4,size: 40.0,xoff: random(-8,-12),startalphaf: 0.9,fadestepf: -1,sizestep: -6);
			  A_SpawnParticleEx("",TexMan.CheckForTexture("SPKOG0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,30),size: frandom(0.2,10),xoff: random(-10,5),yoff: random(-5,5),zoff: random(-5,5),velx: frandom(-0.6,0.6),vely: frandom(-0.6,0.6),velz: frandom(-0.6,0.6),startalphaf: 1.0,fadestepf: -0.5,sizestep: -0.5,startroll: random(-180,180));
			  A_SpawnParticleEx("",TexMan.CheckForTexture("PBALC0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 1,size: 95.0,xoff: -10,startalphaf: 0.15);
			}
			Loop;
		Death:
			TNT1 A 0;
			TNT1 A 0 A_SetTranslation("PM_GreenFusionTint");
			TNT1 A 0 A_SetScale(2.5);
			TNT1 A 0 A_SpawnItem("PM_GreenFusioniteFlare");
			FS03 HIJ 6 Bright Light("GreenBall4");
			Stop;
	}
}

Class PM_ScatterFusionShot : PM_FusionShot
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 19;
		FastSpeed 28;
		DamageFunction (22);
		Scale 0.15;
		RenderStyle "Add";
		+SEEKERMISSILE
		+FORCEXYBILLBOARD
		SeeSound "Fusionite/Scattershot"; 
		DeathSound "HellKnight/BallImpact";
		Decal "ArachnotronScorch";
	}

	States
	{
		Spawn:
			DTPR A 1 Bright Light("GreenBall3")
			{
			  PM_CheckWhiz();
			  A_SpawnItemEx("PM_GreenFlameTrails",0,0,0,0,0,0,0,128,0);
			  A_SpawnParticleEx("",TexMan.CheckForTexture("DTPRA0"),style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: 6,size: 50.0,xoff: -5,startalphaf: 0.2,fadestepf: -1,sizestep: -1);
			  A_SpawnParticleEx("218900",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_ROLL,lifetime: random(60,120),size: 85.0,xoff: -5,startalphaf: 0.45,fadestepf: -1,sizestep: 0.15,startroll: random(-180,180));
			  A_SpawnParticleEx("",TexMan.CheckForTexture("SPKOG0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,120),size: frandom(0.2,20),xoff: random(-10,5),yoff: random(-5,5),zoff: random(-5,5),velx: frandom(-0.6,0.6),vely: frandom(-0.6,0.6),velz: frandom(-0.6,0.6),startalphaf: 1.0,fadestepf: -0.5,sizestep: -0.5,startroll: random(-180,180));
			}
			Loop;
		Death:
			TNT1 A 0;
			TNT1 A 0 A_SetTranslation("PM_GreenFusionTint");
			TNT1 A 0 A_RadiusThrust(6000,90);
			TNT1 A 0 A_SetScale(2.5);
			TNT1 A 0 A_SpawnItemEx("PM_GFireBallExplode",0,0,0,random(-4,4),random(-4,4),random(-4,4),0,SXF_NOCHECKPOSITION);
			TNT1 AAA 0 A_SpawnItemEx("PM_GreenFireballExpFlames",0,0,0,random(-4,4),random(-4,4),random(-4,4),0,SXF_NOCHECKPOSITION);
			TNT1 AAA 0 A_SpawnProjectile("PM_GreenShock",2,0,random(0,360),CMF_OFFSETPITCH,random(0,360));
			TNT1 A 0 A_SpawnItem("PM_GreenFusioniteFlare");
			FS03 HIJ 8 Bright Light("GreenBall3");
			Stop;
  }
}

Class PM_SmallFusionShot : PM_FusionShot
{
	Default
	{
		Translation "0:255=%[0,0,0]:[0.8,2.0,0.28]";
		Radius 6;
		Height 8;
		Speed 27;
		FastSpeed 26;
		DamageFunction (12);
		Scale 0.6;
		SeeSound "Fusionite/SmallShot"; 
		DeathSound "GenericPlasmaExp";
		Decal "ArachnotronScorch";
		PROJECTILE;
		+FORCEXYBILLBOARD
	}

	States
	{
		Spawn:
			FS03 AB 1 Bright Light("GreenBall5") { PM_CheckWhiz(); A_SpawnParticle("80FF50",SPF_FULLBRIGHT|SPF_RELATIVE,14,16,0,random(-22,-5),random(-5,5),random(-5,5),0,0,0,0,0,0,1,-1,-2); }
			Loop;
		Death:
			TNT1 A 0;
			TNT1 A 0 A_SetScale(1.2);
			FS03 HIJ 3 Bright Light("GreenBall5");
			Stop;
	}
}