Class PM_Tainted : PMGluttonyMonsterBase
{
    Default
	{
	PMMonsterBase.CodexLore
	"Shapeless blobs made out of blood taking humanoid form, these golems like to hide in plain sight as small puddles of blood that creep around the player, soon rising to attack.
	 They are known to absorb anything living, turning skin and bone into viscous blood and takes advantage of this attribute to tear chunks off of itself to throw at you.
     
     Can reform into blood to 'burrow' past you when hurt or dodging. High damage weapons are recommended.";
    Health 80;
    Radius 20;
    Height 56;
    Mass 250;
    Speed 8;
	Painchance 204;
    Monster;
    +FLOORCLIP
    -QUICKTORETALIATE
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MISSILEMORE
	Bloodtype "PM_NormalBloodBase";
	Species "Imp";
	Tag "Tainted";
    SeeSound "Tainted/Sight";
    PainSound "Tainted/Pain";
    DeathSound "Tainted/Death";
    ActiveSound "Tainted/Active";
    HitObituary "%o was overwhelmed by a Tainted!";
    Obituary "A Tainted's remains ate through %o's flesh!";
	}
    States
    {
    Spawn:
        BZRD A 0 NoDelay 
		{
		  bSolid = false;
		  bShootable = false;
		}
    Idle:
        BZRD N 4 PM_Look();
        Loop;
	Look:
        TNT1 A 0 A_Jump(15,"Looking");
		BZRD NN 3 PM_LookingForPlayer();
		TNT1 A 0 A_Recoil(-0.001);
		BZRD NN 3 PM_LookingForPlayer();
		TNT1 A 0 A_Recoil(-0.001);
		TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
        Loop;
	Looking:
		BZRD NOPQ 3 A_FaceTarget();
	LookingLoop:
		TNT1 A 0 { bNoPain = false; }
		TNT1 A 0 A_StartSound("Tainted/Step",4);
        BZRD AABB 3 PM_LookingForPlayer();
		TNT1 A 0 A_JumpIfTargetinLOS("Roar",150,JLOSF_DEADNOJUMP);
		TNT1 A 0 A_StartSound("Tainted/Step",4);
        BZRD CCDD 3 PM_LookingForPlayer();
		TNT1 A 0 A_JumpIfTargetinLOS("Roar",150,JLOSF_DEADNOJUMP);
        Loop;
    Spotted:
	    BZRD NNNN 3 A_Chase(null,null,CHF_NOPLAYACTIVE);
		BZRD NOPQ 3 A_FaceTarget();
        TNT1 A 0
		{
		  bSolid = true;
		  bShootable = true;
		}
        BZRD RST 3 A_FaceTarget();
	Spotted2:
	    TNT1 A 0 A_Jump(256,"Roar","Roar","SpotMissile");
	Roar:
	    TNT1 A 0 PM_AlertSound();
		Goto See;
	SpotMissile:
        TNT1 A 0 PM_AlertSound();
	    BZRD EE 5 A_FaceTarget();
		Goto Missile;
	See:
		TNT1 A 0 { bNoPain = false; }
		TNT1 A 0 A_StartSound("Tainted/Step",4);
        BZRD AABB 3 A_Chase();
		TNT1 A 0 A_StartSound("Tainted/Step",4);
        BZRD CCDD 3 A_Chase();
		TNT1 A 0 A_Jump(15,"Teleport2");
        Loop;
    Melee:
		BZRD EF 8 A_FaceTarget();
		BZRD G 5 A_CustomMeleeAttack(5);
    Missile:
		TNT1 A 0 A_Jump(80,"BounceCheck");
        BZRD EF 8 A_FaceTarget();
        BZRD G 4 A_CustomComboAttack("PM_TaintedBall", 36, 3 * random(4, 7), "imp/melee");
		BZRD FEAAAAA 3 A_FaceTarget();
		TNT1 A 0 A_Jump(100,"Teleport");
        Goto See;
	BounceCheck:
		TNT1 A 0 A_JumpIfCloser(600,"Missile2");
		Goto Missile+1;
	Missile2:
		TNT1 A 0 A_StartSound("TaintedProjFire",CHAN_7);
		BZRD AAAAAA 1 
		{
		A_FaceTarget();
		A_SpawnProjectile("HungerBloodSpray",42,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20));
		}
		BZRD HHH 3 A_FaceTarget();
		BZRD AAAAAA 4
		{
		A_FaceTarget();
		A_SpawnProjectile("HungerBloodSpray",42,0,random(-20,20),CMF_AIMDIRECTION,random(-10,20));
		A_SpawnProjectile("PM_TaintedBounceShot",42,0,random(-15,15),CMF_AIMDIRECTION,random(-3,30));
		}
		BZRD AAAAA 1
		{
		A_FaceTarget();
		A_SpawnProjectile("HungerBloodSpray",42,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20));
		}
		BZRD AA 2 A_FaceTarget();
		Goto See;
	Teleport:
		BPWA A 0 { bShootable = false; }
		TNT1 A 0 A_StartSound("Tainted/Slide");
		BZRD TSRQPON 2
		{
		A_SpawnProjectile("RedGroundOoze",1,random(-6,6),90);
		A_SpawnProjectile("RedGroundStain",1,random(-8,8),90);
		A_SpawnProjectile("RedGroundOoze",random(0,10),random(-6,6),90);
		}
		BZRD NNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNN 1 
		{
		A_Wander();
		A_SpawnItemEx("RedGroundOoze",random(-3,3),random(-3,3),1,random(0,4),random(-2,2),0);
		A_SpawnItemEx("RedGroundStain",random(-3,3),random(-3,3),1,random(0,4),random(-1,1),0);
		}
		TNT1 A 0 { bShootable = true; }
		BZRD NOPQRST 3
		{
		bNoPain = true;
		A_FaceTarget();
		}
		TNT1 A 0 { bNoPain = false; }
		Goto See;
	Teleport2:
		BPWA A 0 { bShootable = false; }
		TNT1 A 0 A_StartSound("Tainted/Slide");
		BZRD TSRQPON 2
		{
		A_SpawnProjectile("RedGroundOoze",random(0,10),random(-6,6),90);
		}
		BZRD NNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNN 1 
		{
		A_Chase(null,null);
		A_SpawnItemEx("RedGroundOoze",random(-3,3),random(-3,3),1,random(0,4),random(-2,2),0);
		A_SpawnItemEx("RedGroundStain",random(-3,3),random(-3,3),1,random(0,4),random(-1,1),0);
		}
		TNT1 A 0 { bShootable = true; }
		BZRD NOPQRST 3 A_FaceTarget();
		Goto See;
    Pain:
        BZRD H 2;
        BZRD H 6 A_Pain;
		TNT1 A 0 A_Jump(100,"Teleport");
        Goto See;
    Death:
        BZRD I 12;
        BZRD J 3;
		BZRD J 6 PM_Scream;
        BZRD K 8;
        BZRD L 6 A_NoBlocking;
        BZRD L 0;
        BZRD M 2;
		TNT1 A 0 A_StartSound("GBloodSplashBig",CHAN_6);
		TNT1 A 0 A_SpawnItemEx("PM_TumorBloodMist",0,0,3,0,0,0,0,SXF_NOCHECKPOSITION);
		TNT1 AAAAAAA 0 A_SpawnItemEx("RedGroundOoze",random(-7,7),random(-7,7),1,random(-5,5),random(-5,5),random(-0,3),0,SXF_NOCHECKPOSITION);
		TNT1 AAAAAAAA 0 A_SpawnItemEX("PM_TaintedDeathSplash", 0, 0, 0, random (1, 3), random(1,3), random (1, 6), random(0, 360),SXF_NOCHECKPOSITION);
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEX("PM_TumorBurstBlood", 0, 0, 0, random (-15, 15), random(-15,15), random (1, 7),0,SXF_NOCHECKPOSITION);
		BZRD NNNNNNNNNNNNNN 4 A_SpawnItemEx("PM_TumorBurstBlood",random(-40,40),random(-40,40),0,random(-4,4),random(-4,4),random(0,5),0,SXF_NOCHECKPOSITION);
		BZRD N -1;
        Stop;
	XDeath:
	    TNT1 A 0 A_StopSound(555);
		BZRD N 5 PM_Gib();
		BZRD N 5 A_XScream;
		BZRD N 1 A_NoBlocking;
		BZRD N -1;
        Stop;
    }
}

Class PM_TaintedBounceShot : Actor
{
  Default
  {
  Radius 3;
  Height 3;
  Speed 15;
  DamageFunction (random(8,8));
  Projectile;
  RenderStyle "Translucent";
  Alpha 0.90;
  Scale 1.25;
  +ThruGhost
  -NOGRAVITY
  BounceType "Hexen";
   BounceFactor 0.9;
   SeeSound "TaintedBloodFire";
  DeathSound "TaintedBloodExp";
  Decal "Bloodsplat";
  Gravity 0.67;
  BounceCount 5;
  BounceSound "Tainted/Step";
  }
  States
  {
  Spawn:
    FBL1 A 2 A_CStaffMissileSlither;
    FBL1 A 0 A_SpawnItemEx("MaliceBloodSplash",0,0,0,0,0,0,128);
    FBL1 B 2 A_CStaffMissileSlither;
    FBL1 B 0 A_SpawnItemEx("MaliceBloodSplash",0,0,0,0,0,0,128);
    Loop;
  Bounce:
	  TNT1 A 0 A_SpawnProjectile("RedGroundOoze",0,random(-6,6),random(-90,90));
	  TNT1 A 0 A_SpawnProjectile("RedGroundOoze",0,random(-6,6),random(-90,90));
	  Goto Spawn;
  Death:
	 TNT1 A 0 A_SpawnProjectile("RedGroundOoze",1,random(-6,6),random(-90,90));
		TNT1 A 0 A_SpawnProjectile("RedGroundOoze",1,random(-6,6),random(-90,90));
		TNT1 A 0 A_SpawnProjectile("RedGroundOoze",1,random(-6,6),random(-90,90));
		TNT1 A 0 A_SpawnProjectile("RedGroundOoze",1,random(-6,6),random(-90,90));
		TNT1 A 0 A_SpawnProjectile("RedGroundOoze",1,random(-6,6),random(-90,90));
		TNT1 A 0 A_SpawnProjectile("RedGroundStain",1,random(-6,6),random(-90,90));
    FBL1 CDE 3 Bright;
    Stop;
  }
}

Class RedGroundOoze : Actor
{
    Default
	{
	Projectile;
	-NOGRAVITY
	Gravity 0.5;
	+NOCLIP
	+DONTSPLASH
	Renderstyle "Translucent";
	Damage 0;
	Alpha 0.8;
	Scale 0.15;
	}
	States
	{
	Spawn:
		TNT1 A 1;
		TNT1 A 0 A_CheckSight("SightNull");
		TNT1 A 0 ThrustThing(random(-90,90),4);
		TNT1 A 0 ThrustThingZ(0,random(4,20),0,1);
		FBLD ABCDA 3;
		FBLD A 1 A_FadeOut(0.1);
		Wait;
	SightNull:
		TNT1 A 1;
		Stop;
	}
}

Class RedGroundStain : Actor
{
    Default
	{
	Projectile;
	-NOGRAVITY
	+THRUACTORS
	+ROLLSPRITE
	Gravity 0.4;
	+NOCLIP
	+DONTSPLASH
	Renderstyle "Translucent";
	Damage 0;
	Alpha 0.8;
	Scale 0.5;
	}
	States
	{
	Spawn:
		TNT1 A 0 A_SetRoll(random(0,360));
		TNT1 A 1;
		TNT1 A 0 A_CheckSight("SightNull");
		//TNT1 A 0 ThrustThing(0,random(1,4))
		TNT1 A 0 ThrustThingZ(0,random(1,10),0,1);
		FBLD ABCDA 3;
		FBLD A 1 A_FadeOut(0.2);
		Wait;
	SightNull:
		TNT1 A 1;
		Stop;
	}
}

Class PM_TaintedBall : PM_ImpBallBase
{
    Default
	{
	Radius 6;
	Height 8;
	Speed 15;
	FastSpeed 20;
	DamageFunction 14;
	Scale 0.8;
	Projectile;
	+Strifedamage
	+RANDOMIZE
	Alpha 1;
	RenderStyle "Normal";
	Decal "Bloodsplat";
	SeeSound "TaintedBall";
	DeathSound "TaintedBloodExp";
	}
	States
	{
	Spawn:
		BZR2 AABAAA 1
		{
		A_SpawnParticle("3C1414",SPF_RELATIVE,37,7,0,random(-5,5),random(-5,5),random(-5,5),0,0,0,0,0,0,1,-1,0);
	    A_SpawnParticleEx("FF0000",TexMan.CheckForTexture("GNSML0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 40,size:frandom(20,35),xoff: random(-10,1),startalphaf:frandom(0.6,0.85)
		,fadestepf: -1,sizestep:frandom(-1,1),startroll: random(-180,180));
		A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
		if (!random(0, 5))
			{
			A_SpawnItemEx("PM_NormalBloodEx",random(8,-3),random(3,-3),random(3,-3),0,0,0,0,SXF_NOCHECKPOSITION,100);
			}
		}
		Loop;
	Death:
		TNT1 AA 0 A_SpawnItemEx("PM_NormalBloodMist",random(-3,3),random(-3,3),random(-3,3),random(-2,2),random(-2,2),random(-2,2),0,SXF_NOCHECKPOSITION);
		TNT1 AAA 0 A_SpawnItemEx("PM_NormalBloodEX",random(-2,2),random(-2,2),random(-2,2),0,0,0,0,SXF_NOCHECKPOSITION);
		TNT1 AAAAA 0 A_SpawnProjectile("RedGroundOoze",1,random(-6,6),random(-90,90));
		TNT1 A 0 A_SpawnProjectile("RedGroundStain",1,random(-6,6),random(-90,90));
		BZR2 CDE 6;
		Stop;
	}
}


Class Bloodtrail : Actor
{  
   Default
   {
   Radius 0;
   Height 1;
   PROJECTILE;
   RENDERSTYLE "Add";
   ALPHA 0.80;
   +CLIENTSIDEONLY
   }
   States
   {
   Spawn:
      NULL A 1 Bright;
      SSFX ABCDEFG 2;
      Stop;
   }
}

Class IMPMTrail : Actor
{
  Default
  {
  Radius 1;
  Height 1;
  +NoGravity
  +NoClip
   +CLIENTSIDEONLY
  Scale 1.0;
  }
  States
  {
  Spawn:
    BSFX A 0;
    BSFX A 0 A_Jump(192, 2, 3, 4, 5);
    BSFX B 16;
    Stop;
    BSFX C 16;
    Stop;
    BSFX D 16;
    Stop;
    BSFX E 16;
    Stop;
    BSFX F 16;
    Stop;
  }
}

Class PM_BloodTrail : Actor
{
  Default
  {
  Scale 0.80;
  +NOINTERACTION
  +CLIENTSIDEONLY
  +NOBLOCKMAP
  +THRUACTORS
  +DONTSPLASH
  +NOCLIP
  }
States
{
Spawn:
	TNT1 A 2;
	Go:
    BZR2 AB 1
	{
	A_SetScale(Scale.X-0.07);
	A_FadeOut(0.17);
	}
    Loop;
    }
}