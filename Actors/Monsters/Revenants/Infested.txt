Class PM_Infested : PMPestilenceMonsterBase
{
  Default
  {
  //$Category "PM_Monsters/Revenants"
  Health 360;
  Radius 20;
  Height 56;
  Mass 500;
  Speed 12;
  PainChance 20;
  Monster;
  MeleeThreshold 100;
  BloodColor "Dark Red";
  +FLOORCLIP
  +DONTHARMSPECIES
  +MISSILEMORE
  Bloodtype "PM_PestilenceBloodBase";
  Species "Revenant";
  SeeSound "Infested/Sight";
  PainSound "";
  DeathSound "Infested/Death";
  ActiveSound "Infested/Active";
  MeleeSound "skeleton/melee";
  Tag "Infested";
  HitObituary "%o fell victim to an Infested.";
  TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
  }
  States
  {
  Spawn:
		TNT1 A 0 PM_Look();
		ZKEL A 10 A_Wander();
		TNT1 A 0 PM_Look();
		ZKEL B 10 A_wander();
		TNT1 A 0 PM_Look();
		NULL A 0 A_StartSound("skeleton/step",5);
		ZKEL C 10;
		TNT1 A 0 PM_Look();
		ZKEL D 10 A_wander();
		TNT1 A 0 PM_Look();
		ZKEL E 10 A_wander();
		TNT1 A 0 PM_Look();
		NULL A 0 A_StartSound("skeleton/step",5);
		ZKEL F 10;
        Loop;
  Bark:
	TNT1 A 0 A_StartSound("Infested/Idle");
	Goto Look;
  Look:
	TNT1 A 0 A_SetSpeed(7);
	ZKEL AABBCC random(1,8) PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("skeleton/step",5);
    TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
	ZKEL DDEEFF random(1,8) PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("skeleton/step",5);
	TNT1 A 0 A_Jump(30,"Bark");
	Loop;
  Spotted:
	TNT1 A 0 PM_AlertSound();
	ZKEL BBBLLLLLLLLBBBB 2
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_RotflyYoungling",random(-5,5),random(-5,5),random(40,75),frandom(-2.5,2.5),frandom(-2.5,2.5),frandom(0,3.5),random(-180,180),SXF_NOCHECKPOSITION,74);
	}
  See2:
	TNT1 A 0 A_SetSpeed(7);
	ZKEL AABBCC random(1,7) A_Chase("Melee","Missile");
	TNT1 A 0 A_StartSound("skeleton/step",5);
	ZKEL DDEEFF random(1,7) A_Chase("Melee","Missile");
	TNT1 A 0 A_StartSound("skeleton/step",5);
	ZKEL AABBCC random(1,7) A_Chase("Melee","Missile");
	TNT1 A 0 A_StartSound("skeleton/step",5);
	ZKEL DDEEFF random(1,7) A_Chase("Melee","Missile");
	TNT1 A 0 A_StartSound("skeleton/step",5);
  See3:
	TNT1 A 0 A_SetSpeed(10);
	TNT1 A 0 A_SpawnProjectile("MaggotFall", random(35,74), 0, random(-300,300), 0);
    ZKEL AA random(2,4) A_Chase();
	TNT1 A 0 A_Recoil(-3);
	ZKEL BBCC random(1,3) A_Chase();
	TNT1 A 0 A_SpawnProjectile("MaggotSpawn", random(35,74), random(-4,4), 0, 0, 0);
	TNT1 A 0 A_SpawnProjectile("MaggotFall", random(35,74), 0, random(-300,300), 0);
	TNT1 A 0 A_StartSound("skeleton/step",5);
	TNT1 A 0 A_SpawnProjectile("MaggotSpawn", random(35,74), random(-4,4), 0, 0, 0);
	ZKEL DD random(2,4) A_Chase();
	TNT1 A 0 A_Recoil(-3);
	ZKEL EEFF random(1,3) A_Chase();
	TNT1 A 0 A_SpawnProjectile("MaggotFall", random(35,74), 0, random(-300,300), 0);
	TNT1 A 0 A_StartSound("skeleton/step",5);
    Loop;
  Melee:
    ZKEL G 1 A_FaceTarget();
    ZKEL G 6 A_StartSound("Doom4/Other/MeleeSwing",20);
    ZKEL H 0 A_FaceTarget();
   ZKEL H 2 A_Recoil(-8);
    ZKEL H 0 A_FaceTarget();
   ZKEL H 2 A_Recoil(-8);
    ZKEL H 0 A_FaceTarget();
   ZKEL H 2 A_Recoil(-8);
    ZKEL I 6 A_CustomMeleeAttack(30,"Doom4/Other/MeleeHit");
    Goto See2;
  Missile:
	TNT1 A 0;
	TNT1 A 0 A_Jump(100,"RotSpit");
	TNT1 A 0 A_Jump(110,"FlyBurst");
	ZKEL L 0 A_FaceTarget();
	TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,random(45,74));
	ZKEL L 13 A_StartSound("ZFODDGIB");
	ZKEL G 1 A_StartSound("Doom4/Other/MeleeSwing",20);
	ZKEL H 4 A_Facetarget();
	ZKEL IIIIII 0 A_SpawnProjectile("PM_InfestedThrownGiblets",46,0,random(-8,8),CMF_OFFSETPITCH,random(-10,0));
	ZKEL I 10 A_FaceTarget();
	TNT1 A 0 A_Jump(70,"See2");
	Goto See3;
  RotSpit:
	ZKEL BLL 4 A_FaceTarget();
	TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,55);
	ZKEL B 10
	{
	A_FaceTarget();
	A_StartSound("PM/GibSplat",30);
	A_StartSound("Depraved/Spit",35);
	let p = A_SpawnProjectile("PM_InfestedSpit",60,0,random(-6,6),CMF_AIMDIRECTION|CMF_OFFSETPITCH);
	 if (p) p.vel = CalcThrowVector(pos, target.pos, 0.6, frandom(40.0,65.0));
	}
	ZKEL A 10 A_FaceTarget();
	TNT1 A 0 A_Jump(70,"See2");
	Goto See3;
  FlyBurst:
	ZKEL BBL 5;
    ZKEL L 10 A_Pain;
	TNT1 A 0 A_StartSound("ZFODDGIB");
	TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,random(45,74));
	TNT1 AAA 0 A_SpawnItemEx("PM_PestilenceMeatChunkLauncher",0,0,random(45,74),0,0,0,0,0,80);
	TNT1 A 0 A_SpawnItemEx("PM_InfestedRotfly",0,0,random(45,74),0,0,0,random(-180,180),SXF_NOCHECKPOSITION);
	TNT1 AA 0 A_SpawnItemEx("PM_InfestedRotfly",0,0,random(45,74),0,0,0,random(-180,180),SXF_NOCHECKPOSITION,170);
	ZKEL LLLBA 5;
	TNT1 A 0 A_Jump(100,"See2");
    Goto See3;
  Pain:
	TNT1 A 0 A_Jump(100,"Pain2");
	TNT1 A 0 A_StartSound("PM/GibSplat",30);
    ZKEL L 5;
    ZKEL L 5 A_Pain;
	ZKEL LLLLLL 1;
    Goto See3;
  Pain2:
    ZKEL L 5;
	TNT1 A 0 A_StartSound("PM/GibSplat",30);
	TNT1 AAAAA 0 A_SpawnProjectile("MaggotFall", random(35,74), 0, random(-300,300), 0);
    ZKEL B 10 A_Pain;
	TNT1 A 0 A_StartSound("ZFODDGIB");
	ZKEL LLLLLLLLLLLLLLL 1
	{
	A_SpawnItemEx("PM_RotflyMaggot",0,0,random(35,74),random(-3,3),random(-3,3),random(0,5),0,0,220);
	A_SpawnItemEx("PM_PestilenceMeatChunkLauncher",0,0,random(35,74),random(-3,3),random(-3,3),random(0,5),0,0,220);
	}
	TNT1 AAAAA 0 A_SpawnProjectile("MaggotFall", random(35,74), 0, random(-300,300), 0);
	ZKEL A 5;
	TNT1 A 0 A_Jump(100,"See2");
    Goto See3;
  Death:
    TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING);
    ZKEL LM 7 A_SpawnItemEx("PM_LargeWhiteMaggot");
	TNT1 AAAAAAAAAA 0 A_SpawnProjectile("MaggotFall", random(35,74), 0, random(-300,300), 0);
    ZKEL N 7 PM_Scream();
	TNT1 AAAA 0 A_SpawnProjectile("MaggotFall",random(35,74), 0, random(-300,300),70);
    ZKEL O 7 A_NoBlocking;
	TNT1 AAAA 0 A_SpawnProjectile("MaggotFall", random(25,44), 0, random(-300,300),0,75);
    ZKEL P 7 A_SpawnItemEx("PM_LargeWhiteMaggot");
	TNT1 AAAA 0 A_SpawnProjectile("MaggotFall", random(5,24), 0, random(-300,300),80);
	TNT1 A 0 A_StartSound("Corpse/FallMed",CHAN_6);
	ZKEL QQQQQQQQQQQQQQQQ 3
	{
	A_SpawnItemEx("PM_LargeWhiteMaggot",0,0,0,random(-3,3),random(-3,3),random(0,6));
	A_SpawnItemEx("PM_PestilenceBlood",0,0,0,random(-3,3),random(-3,3),random(0,6),0,0,100);
	}
	ZKEL QQQQQQQQQQQQQQQQQQQQQQQQ random(0,10)
	{
	A_StartSound("PM/GibSplat",30);
	A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,0,random(-3,3),random(-3,3),random(0,6),0,0,90);
	A_SpawnItemEx("PM_PestilenceMeatChunkLauncher",0,0,0,random(-3,3),random(-3,3),random(0,6),0,0,100);
	A_SpawnItemEx("PM_RotflyYoungling",0,0,0,frandom(-2.5,2.5),frandom(-2.5,2.5),frandom(0,3.5),random(-180,180),flags:SXF_NOCHECKPOSITION,85);
	A_SpawnItemEx("PM_InfestedRotfly",0,0,0,random(-3,3),random(-3,3),random(0,6),0,0,230);
	A_SpawnItemEx("PM_VenomMaggot",0,0,0,random(-3,3),random(-3,3),random(0,6),0,0,250);
	A_SpawnItemEx("PM_Trite",0,0,0,random(-3,3),random(-3,3),random(0,6),0,0,250);
	}
    ZKEL Q -1;
    Stop;
  Raise:
    ZKEL Q 5;
    ZKEL PONML 5;
    Goto Look;
  }
}

Class PM_InfestedSpit : DoomImpBall
{
	Default
	{
	DamageFunction 20;
	SeeSound "";
	DeathSound "";
	Speed 13;
	FastSpeed 14;
	Scale 0.93;
	Gravity 0.8;
	-NOGRAVITY
	}
	
	States
  	{
	Spawn:
		TNT1 A 1
		{
		A_SpawnItemEx("PM_RotflySmallSplash",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_PestilenceBloodEx",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,SXF_NOCHECKPOSITION,100);
		}
		Loop;
	Death:
		TNT1 A 0 A_StartSound("PM/GibSplat",30);
		TNT1 AAAAAA 0 A_SpawnItemEx("PM_RotflySplash",0,0,0,random(-2,2),random(-2,2),random(0,3));
		TNT1 AA 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
		TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodEX",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
		TNT1 A 0 A_SpawnItemEx("PM_GagRotflySmokeCloud",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,SXF_NOCHECKPOSITION);
		Stop;
	}
}

Class PM_InfestedRotfly : PM_Rotfly
{
  Default
  {
  Scale 0.50;
  Health 5;
  PMMonsterBase.CodexLore "";
  Species "Elemental";
  }
  
  States
  {
  Spawn:
	BFLY A 5 PM_Look;
  Look:
    BFLY ABABABAB 2
	{
	A_Wander();
	PM_LookingForPlayer();
	}
	Loop;
  Spotted:
	TNT1 A 0 PM_AlertSound();
	TNT1 A 0 ThrustThingZ(0,6,0,1);
  Go:
	TNT1 A 0 A_SetSpeed(7);
	BFLY ABABABABABABAB 1 A_Chase;
	TNT1 A 0 A_JumpIfCloser(1000,"StandoffStart");
	Loop;
  StandoffCheck:
	TNT1 A 0 A_Jump(50, "StandoffStart");
	Goto Go;
  StandoffStart:
	TNT1 A 0 A_CheckSight("Go");
	TNT1 A 0 A_SetSpeed(0);
	TNT1 A 0 A_JumpIfCloser(100,"Backup");
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	BFLY ABABAB 2
	{
	A_Chase("Kamikaze","Kamikaze");
	A_FaceTarget();
	}
	TNT1 A 0 A_Jump(130,"RandomDodge");
	Loop;
  Backup:
	TNT1 A 0 A_Recoil(8);
	BFLY ABABABABAB 2;
	Goto StandoffStart;
  RandomDodge:
	TNT1 A 0
	{
		ThrustThing(random(0,360),10);
		ThrustThingZ(0,7,random(0,1),1);
	}
	BFLY ABABAB 2 A_FaceTarget;
	Goto StandoffStart;
  Kamikaze:
    TNT1 A 0
	{
		A_ChangeVelocity(0,0,0,CVF_REPLACE);
		A_StartSound("Rotfly/Attack");
	}
    BFLY ABABABABABABABABABA 1
    {
    A_FaceTarget();
    A_SpawnItemEx("PM_PestilenceBlood1",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION,213);
    }
    TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
    TNT1 A 0 A_SpawnProjectile("PM_InfestedRotflyKamikaze",0,0,0);
	Stop;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_Scream;
	TNT1 A 0 A_StartSound("FlyHit/Hit",0,1);
	TNT1 AAAA 0 A_SpawnItemEx("PM_RotflySplash",0,0,0,random(-3,3),random(-3,3),random(-3,3));
	BFLY C 20 A_NoBlocking;
	TNT1 AA 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodEX",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_GagRotflySmokeCloud",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,SXF_NOCHECKPOSITION);
	Stop;
   }
}

Class PM_InfestedRotflyKamikaze : DoomImpBall
{
  Default
  {
  Speed 18;
  DamageFunction 25;
  Radius 3;
  Height 3;
  Health 5;
  Scale 0.50;
  -THRUSPECIES
  -DONTHARMSPECIES
  -DONTHARMSPECIES
  -THRUACTORS
  +SHOOTABLE
  +SEEKERMISSILE
  Species "Elemental";
  +MISSILE
  RenderStyle "Normal";
  DamageType "Poison";
  Seesound "Rotfly/See";
  DeathSound "Rotfly/Death";
  }
  
  States
  {
  Spawn:
	BFLY AB 2 Bright
	{
	A_SpawnParticleEx
		(
		"bdff00",
		TexMan.CheckForTexture("DTPRA0"),
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,2),
		size: 90,
		zoff: 6,
		startalphaf: frandom(0.2,0.3),
		fadestepf: -1,
		sizestep: -30,
		startroll: random(-180,180),
		rollvel: frandom(-10,10),
		rollacc: frandom(-1,1)
		);
	A_SpawnItemEx("PM_RotflySmallSplash",random(-5,5),random(-10,10),random(1,8),0,0,0,0,SXF_NOCHECKPOSITION);
	A_SeekerMissile(4,4,SMF_PRECISE);
	}
	Loop;
  Death:
	TNT1 AA 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodEX",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_GagRotflySmokeCloud",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_NoBlocking;
	Stop;
  }
}

Class PM_InfestedThrownMaggots : Actor
{
  Default
  {
  Damage 1;
  Height 3;
  Radius 3;
  Speed 30;
  Mass 1;
  Scale 2.5;
  Gravity 1.0;
  +ROLLSPRITE
  +FORCEXYBILLBOARD
  PROJECTILE;
  Gravity 1.0;
  +LOWGRAVITY
  }
  States
  {
  Spawn:
	TNT1 A 0 NODELAY
	{
		A_SetGravity(frandom(0,1.5));
	}
	MRMG J 1;
	Go:
	TNT1 A 0;
	TNT1 A 0 A_SetRoll(random(0,360));
	MRMG J 2;
	MRMG IHGFEFGFEFGHIJ 3;
	Loop;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_SetRoll(0);
	TNT1 A 0 { bNOGRAVITY = false; }
    MRMG EFGHIJKLKJIHGFEFGHIJKLKJIHGF 4;
    Stop;
  }
}

Class PM_InfestedThrownGiblets : Actor
{
  int timer;
  Default
  {
  Radius 3;
  Height 3;
  Speed 26;
  DamageFunction 12;
  Alpha 1;
  +ThruGhost
  -NOGRAVITY
  -NOLIFTDROP
  +BOUNCEONWALLS
  +BOUNCEONFLOORS
  +ROLLSPRITE
  +NOTELEPORT
  +DROPOFF
  +NOBLOCKMAP
  +ACTIVATEIMPACT
  +ACTIVATEMCROSS
  +MISSILE
  Scale 0.4;
  Gravity 0.35;
  BounceFactor 0.5;
  DeathSound "";
  }
  States
  {
	Spawn:
		TNT1 A 0 NoDelay { timer = random(50,70); }
	Go:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("PM/GibSplat",30);
		TNT1 A 0 A_SetScale(frandom(0.2,1.1));
		TNT1 A 0 A_SetRoll(random(0,180));
		TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodMulti");
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20);
		MEAT ABCDEFGHIJLKMNOPQRST 0 A_JumpIf(1 == 1,"Play");
	Play:
		"####" "#" 2 A_SpawnItemEx("PM_PestilenceBloodEx",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,SXF_NOCHECKPOSITION,100);
		"####" "#" 0 A_CheckFloor("Death");
		"####" "#" 0
		{
		A_SetRoll(roll+15);
		timer--;
		}
		"####" "#" 0
			{
			if(timer < 0)
			SetStateLabel("Death");
			}
		Loop;
	Death:
		"####" "#" 0 { bMISSILE = false; }
		"####" "#" 0
		{
		A_SetRoll(0);
		}
		"####" "#" 0 A_StartSound("PM/GibLand");
		"####" "#" 0 A_SpawnItemEx("PM_PestilenceBloodEx",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,SXF_NOCHECKPOSITION);
		"####" "#" 0 A_QueueCorpse;
		"####" "#" 100;
		"####" "#" 1 A_FadeOut;
		Wait;
	}
}

Class MaliceGoreFliesWSound : Actor
{
   Default
   {
   +NOGRAVITY
   +NOTONAUTOMAP
   -SHOOTABLE
   +NOBLOOD
   radius 1;
   height 1;
   }
States
	{
	Spawn:
	TNT1 A 0;
	TNT1 A 0 A_StartSound("FLEAS", 1, 1, 1);
	TNT1 A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Spawn4", "Spawn5", "Spawn6", "Spawn7", "Spawn8");
	Spawn1:
		TNT1 A 0;
		TNT1 A 0 A_SetScale(1.3, 2.0);
		Goto Live;
	Live:
		0CFL ABCDEFGHIJKLMNOPQRSTUWVXYZ 1;
		Loop;		
	Death:
	TNT1 A 0;
	TNT1 A 0 A_StopSound(1);
	TNT1 A 0 A_NoBlocking;
	Stop;
}
}

Class MaliceMaggotSpawner2 : Actor
{
  Default
  {
  +CLIENTSIDEONLY
  +NOGRAVITY
  +NOTONAUTOMAP
  -SHOOTABLE
  +NOBLOOD
  radius 1;
  height 1;
  }
States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 20;
		TNT1 A 0 A_Jump(128, "Live");
		TNT1 A 20;
		TNT1 A 0 A_SetScale(-1.0, 1.0);
	Live:
		GRMG ABCDCDCD 4;
		TNT1 A 0 A_SpawnProjectile("MaliceSpawnedMaggotDec2", 0, 0, 0, 2, 0);
		TNT1 A 0 A_NoBlocking;
		Stop;
	}
}



Class MaliceSpawnedMaggotDec2 : Actor
{
  Default
  {
  Height 1;
  Radius 1;
  Mass 0;
  PROJECTILE;
  +Missile
  +NoBlockMap
  +DontSplash
  +FORCEXYBILLBOARD
  +CLIENTSIDEONLY
  +THRUACTORS
  -NOGRAVITY
  Gravity 1.0;
  Speed 0;
  }
  States
  {
  Spawn:
  Death:
    GRMG EFGHIJKLKJIHGFEFGHIJKLKJIHGF 4;
    Stop;
  }
}

Class MaggotSpawn : Actor
{
  Default
  {
  Height 1;
  Radius 1;
  Mass 1;
  +DontSplash
  +FORCEXYBILLBOARD
  +THRUACTORS
  Gravity 1.0;
  Speed 0;
  }
  States
  {
  Spawn:
	TNT1 A 0 A_Die;
  Death:
    MRMG EFGHIJKLKJIHGFEFGHIJKLKJIHGF 4;
    Stop;
  }
}

Class WMaggotSpawn : Actor
{
  Default
  {
  Height 1;
  Radius 1;
  Mass 1;
  +DontSplash
  +FORCEXYBILLBOARD
  +THRUACTORS
  Gravity 1.0;
  Speed 0;
  }
  States
  {
  Spawn:
	TNT1 A 0 A_Die;
  Death:
    MRMG EFGHIJKLKJIHGFEFGHIJKLKJIHGF 4;
    Stop;
  }
}

Class MaggotPuke : Actor
{
  Default
  {
  Damage 1;
  Height 1;
  Radius 1;
  Speed 9;
  Mass 1;
  +FORCEXYBILLBOARD
  Gravity 0.8;
  PROJECTILE;
  -NOGRAVITY
  }
  States
  {
  Spawn:
	MRMG EFGHIJKLKJIHGFEFGHIJKLKJIHGF 4;
  Death:
    MRMG EFGHIJKLKJIHGFEFGHIJKLKJIHGF 4;
    Stop;
  }
}

Class MaggotFall : Actor
{
  Default
  {
  Damage 1;
  Height 1;
  Radius 1;
  Speed 3;
  Mass 1;
  Scale 1.4;
  +FORCEXYBILLBOARD
  Gravity 0.7;
  PROJECTILE;
  -NOGRAVITY
  +ROLLSPRITE
  +ROLLCENTER
  }
  States
  {
  Spawn:
	TNT1 A 0 NODELAY A_QueueCorpse;
	MRMG EFGHIJKLKJIHGFEFGHIJKLKJIHGF 4
	{
	A_SetRoll(roll+random(-25,25));
	}
  Death:
	TNT1 A 0 A_SetRoll(0);
    MRMG EFGHIJKLKJIHGFEFGHIJKLKJIHGF 2;
    Stop;
  }
}