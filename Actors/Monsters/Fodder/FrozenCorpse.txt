Class PM_UmbralHorrorspawn : PMUmbralMonsterBase
{
  Default
  {
    PMMonsterBase.CodexLore
	"It's just a corpse. What more do you need to worry about?
	 Huh? Why is it on the list? Well.. who knows?
	 
	 Just don't turn your back on them.";
	PMMonsterBase.ForgetThreshold 9999;
	Health 60;
    Radius 20;
    Height 56;
    Speed 25;
    Obituary "%o felt the cold shoulder.";
	Tag "Frozen Horrorspawn";
	ActiveSound "Umbralspawn/TurnAround";
	DeathSound "Umbralspawn/Scream";
	PainChance 100;
    Monster;
	Species "Fodder";
	Scale 1.20;
	MeleeRange 80;
    +FLOORCLIP
	+PAINLESS
	+NOPAIN
	+DONTTHRUST
	+NOBLOOD
	+DROPOFF
	+NOICEDEATH
	+LOOKALLAROUND
	+NOINFIGHTING
	MeleeThreshold 400;
	BloodColor "Red";
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
  }

  override void PM_Gib()
  {
	  for(int gib=0;gib<5;gib++)
	 { A_SpawnItemEx("PM_NormalMeatChunkLauncher",0,0,random(10,30),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY); }
	  for(int a=0;a<3;a++)
	 { A_SpawnItemEx("PM_NormalBloodMulti",0,0,32,random(-1,1),random(-1,1),random(-1,1),frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY); }
	 A_SpawnItemEx("PM_NormalBloodMist",0,0,32,random(-1,1),random(-1,1),random(-1,1),frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY);
	   A_StartSound("GBloodSplashBig",CHAN_7,CHANF_DEFAULT,0.75);
  }

  States
  {
  Spawn:
	UMBE A 0 NoDelay;
	TNT1 A 10 PM_Look();
	Wait;
  Look:
    TNT1 A 0 { bTHRUACTORS = true; }
    TNT1 A 5 { for(int i=30;i>0;i--) A_Wander(); }
	  TNT1 A 0 A_CheckSight("See");
	Loop;
  See:
    TNT1 A 0 { bTHRUACTORS = false; }
  See2:
	UMBE A random(0, 8)
	{
		if (target)
		{
			double TAngle= atan2(self.pos.xy-target.pos.xy);
			double TPitch= atan2(target.pos.z-pos.z,(target.pos.xy-self.pos.xy).length());
			double SAngle= absangle(target.angle,TAngle);
			double SPitch= absangle(target.pitch,TPitch);
			
			
			if(CheckSight(target)&&(SAngle>70||SPitch>55)){
				if(Distance3D(target) < 50){
					A_FaceTarget(); A_SpawnProjectile("PM_PotsLunge"); A_Stop();
				}
				A_StartSound("Skeleton/Step",volume:0.45,pitch:1.15);
				A_Chase(); A_FaceTarget(); A_Recoil(-random(1, 10));
			}else{A_StopSound(15); A_Stop();}
			if(!CheckSight(target))A_Wander();
		}
	}
	Loop;
  Death:
  XDeath:
	UMBE A 0 A_Die();
	UMBE A 0
	{
		PM_Gib();
		A_StartSound("Ice/Freeze", CHAN_7, volume: 0.5);
		A_StartSound("Watcher/IShardExp",CHAN_6);
	}
	TNT1 AAA 0 A_SpawnItemEx("PM_GroundIceBouncer",0,0,2,random(-6,6),random(-6,6),random(-6,6),0,SXF_NOCHECKPOSITION,0);
	TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx ("IceChunk",0,0,2,random(-4,4),random(-4,4),random(-6,6),0,SXF_NOCHECKPOSITION,0);
	TNT1 AAAAAA 0 A_SpawnItemEx("PM_WatcherFrostCloud",0,0,random(5,30),frandom(-3,3),frandom(-3,3),frandom(-1,1),0,0);
	TNT1 AAAA 0 A_SpawnItemEx("PM_NormalMeatChunkLauncher",0,0,random(10,50),frandom(-3,3),frandom(-3,3),frandom(-1,1),0,0);
	TNT1 AAA 0 A_SpawnItemEx("PM_WhiteExplosionFlames",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A random(1, 40);
	TNT1 A 1
	{
		A_NoBlocking();
		A_Scream();
	}
	Stop;
  Raise:
	Stop;
  }
}

Class PM_FrozenCorpseSpawn : Actor
{
	States
	{
	Spawn:
	    TNT1 A 1 Nodelay;
		TNT1 AAAAAA 0 A_SpawnItemEx("PM_UmbralHorrorspawn",0,0,0,frandom(-4,4),frandom(-4,4),frandom(1,5),0,SXF_NOCHECKPOSITION,160);
		Stop;
	}
}