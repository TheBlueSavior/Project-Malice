Class PM_Corpulent : PMUnaffMonsterBase
 {
  Default
  {
   PMMonsterBase.CodexLore 
   "Armed with accurate flame cannons, Corpulents are seen as a refined step above their more ramshackle Hectebus cousins.
   Although still limited in intelligence, they're frequently used as guards, slave labor and hardy pawns in innumerable conflicts within Hell, renown for mercilessly scorching any opponent that makes the mistake of getting too close.
   
   Will use flamethrowers when close and spew fireballs at you when far away.";
   Health 600;
   Radius 48;
   Height 64;
   painchance 70;
   Speed 6;
   Scale 1.05;
   Mass 1000;
   SeeSound "Corpulent/Sight";
   PainSound "Corpulent/Pain";
   DeathSound "Corpulent/Death";
   ActiveSound "Corpulent/Active";
   //DropItem "RagePoint1"
   Obituary "%o was charred by a Corpulent.";
   Species "Mancubus";
   MinMissileChance 220;
   Tag "Corpulent";
   BloodColor "DarkRed";
   Monster;
   Bloodtype "PM_NormalBloodBase";
   +DONTHARMCLASS
   +DONTHARMSPECIES
   +FLOORCLIP
   TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
   }
   states
 {
  Spawn:
    M01A AAAAAAAAAA 10 PM_Look();
	M01A A 0
	{
	if (!random(0, 30))
			{
				A_StartSound(activesound,554,volume:frandom(0.1,0.5),pitch:frandom(0.60,0.78));
			}
	}
	M01A A 0 A_Jump(45,"Explore");
    Loop;
  Explore:
	TNT1 A 0
	{
	if (!random(0, 12))
			{
				A_StartSound(activesound,554,volume:frandom(0.1,0.5),pitch:frandom(0.60,0.78));
			}
	}
	TNT1 A 0 A_SetSpeed(3);
	TNT1 A 0 A_StartSound("MancubStep",4,volume:0.3);
	M01A AAAABBBBCCCC 3
	{
	PM_Look();
	A_Wander();
	}
	TNT1 A 0 A_StartSound("MancubStep",4,volume:0.3);
	M01A DDDDEEEEFFFF 3
	{
	A_Wander();
	PM_Look();
	}
	GHUL A 0 A_Jump(40,"Spawn");
    Loop;
  Look:
	TNT1 A 0 A_SetSpeed(6);
	TNT1 A 0 A_StartSound("MancubStep");
    M01A AAA 3 PM_LookingForPlayer();
	M01A BBBCCC 3 PM_LookingForPlayer();
	TNT1 A 0 A_JumpIfTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("MancubStep");
	M01A DDD 3 PM_LookingForPlayer();
	M01A EEEFFF 3 PM_LookingForPlayer();
    Loop;
  Spotted:
    TNT1 A 0 A_Jump(256,"SpotMissile","Roar");
  Roar:
    TNT1 A 0 PM_AlertSound();
    Goto See;
  See:
	TNT1 A 0 A_SetSpeed(6);
	TNT1 A 0 A_StartSound("MancubStep");
    M01A AAA 3 A_Chase();
	M01A BBBCCC 3 A_Chase();
	TNT1 A 0 A_StartSound("MancubStep");
	M01A DDD 3 A_Chase();
	M01A EEEFFF 3 A_Chase();
    Loop;
   SpotMissile:
    M01A G 5 A_FaceTarget();
   Goto Missile;
  Missile:
	TNT1 A 0 A_Jump(60,"QuickMissile");
	TNT1 A 0 A_JumpIfCloser(365,"Flamethrower");
  LongMissile:
	TNT1 A 0 { pm_attackloop = random(2,10); }
	TNT1 A 0 A_StartSound("Corpulent/Attack",CHAN_5);
	M01A G 1;
	M01A GGGGG 1 Light("AvatarFireBall2")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_OrangeFireEffect",18,random(34,42),random(23,27),0,0,2);
	A_SpawnItemEx("PM_OrangeFireEffect",18,random(-34,-42),random(23,27),0,0,2);
	}
	M01A GGGGGG 1 Light("AvatarFireBall2")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_OrangeFireEffect",18,random(34,42),random(23,27),0,0,2);
	A_SpawnItemEx("PM_OrangeFireEffect",18,random(-34,-42),random(23,27),0,0,2);
	}
	TNT1 A 0 A_Jump(128,"LMissileLoopNoLead");
   LMissileLoop:
	 M01A H 1 Light("AvatarFireBall2");
	 M01A H 4 Light("AvatarFireBall2")
		{
		PM_LeadTarget(20);
		A_SpawnProjectile("PM_CorpulentBall",28,-34,0,CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch*1);
		A_StartSound("Corpulent/Fire",CHAN_7);
		}
	 M01A IG random(3,4);
     M01A H 1 Light("AvatarFireBall2");
	 M01A H 4 Light("AvatarFireBall2")
		{
		PM_LeadTarget(20);
		A_SpawnProjectile("PM_CorpulentBall",28,34,0,CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch*1);
		A_StartSound("Corpulent/Fire",CHAN_7);
		pm_attackloop--;
		}
	M01A IG random(3,4) { if(pm_attackloop < 0) SetStateLabel("LMissileEnd"); }
	Loop;
LMissileEnd:
	M01A IG 6 A_FaceTarget();
	Goto See;
LMissileLoopNoLead:
	 M01A H 1 Light("AvatarFireBall2");
	 M01A H 4 Light("AvatarFireBall2")
		{
		A_FaceTarget();
		A_SpawnProjectile("PM_CorpulentBall",28,-34,random(-15,15));
		A_StartSound("Corpulent/Fire",CHAN_7);
		}
	 M01A IG random(3,4);
     M01A H 1 Light("AvatarFireBall2");
	 M01A H 4 Light("AvatarFireBall2")
		{
		A_FaceTarget();
		A_SpawnProjectile("PM_CorpulentBall",28,34,random(-15,15));
		A_StartSound("Corpulent/Fire",CHAN_7);
		pm_attackloop--;
		}
	M01A IG random(3,4) { if(pm_attackloop < 0) SetStateLabel("LMissileEnd"); }
	Loop;
  QuickMissile:
	 M01A GGG 5 A_FaceTarget();
	 M01A H 1 Light("AvatarFireBall2");
	 M01A H 5 Light("AvatarFireBall2")
		{
		A_SpawnProjectile("PM_CorpulentBall",28,34,0);
		A_StartSound("Corpulent/Fire",CHAN_7);
		}
	 M01A H 1 Light("AvatarFireBall2");
     M01A H 5 Light("AvatarFireBall2")
		{
		A_SpawnProjectile("PM_CorpulentBall",28,-34,0);
		A_StartSound("Corpulent/Fire",CHAN_7);
		}
	M01A IG 5 A_FaceTarget();
	Goto See;
  Flamethrower:
	TNT1 A 0 A_Jump(50,"LongMissile");
	TNT1 A 0 A_StartSound("Corpulent/Attack",CHAN_5);
	M01A G 1;
	M01A GGGGG 1 Light("AvatarFireBall2")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_OrangeFireEffect",18,random(34,42),random(23,27),0,0,2);
	A_SpawnItemEx("PM_OrangeFireEffect",18,random(-34,-42),random(23,27),0,0,2);
	}
	TNT1 A 0 A_StartSound("Fire/Unleash",CHAN_6);
	M01A GGGGGGG 1 Light("AvatarFireBall2")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_OrangeFireEffect",18,random(34,42),random(23,27),0,0,2);
	A_SpawnItemEx("PM_OrangeFireEffect",18,random(-34,-42),random(23,27),0,0,2);
	}
	TNT1 A 0 A_StartSound("Defiler/Breath",CHAN_7,1,1);
	M01A HHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 1 Light("AvatarFireball2")
	{
	A_SpawnProjectile("PM_CorpulentFlamer", 30, 34,random(-8,8));
	A_SpawnProjectile("PM_CorpulentFlamer", 30, -34,random(-8,8));
	A_FaceTarget();
	}
	TNT1 A 0 A_StopSound(CHAN_7);
	M01A IIIIII 1 A_FaceTarget();
	Goto See;
  Pain:
    M01A J 1;
	TNT1 A 0 A_StopSound(CHAN_7);
    M01A J 9 A_Pain;
    Goto See;
  Death:
	   TNT1 A 0;
	   M01A K 6
	   {
	   A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   }
       M01A L 6
	   {
	   PM_Scream();
	   A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,30),random(-6,6),random(-6,6),random(0,8),random(-50,50),SXF_NOCHECKPOSITION);
	   A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,30),random(-6,6),random(-6,6),random(0,8),random(-50,50),SXF_NOCHECKPOSITION);
	   }
       M01A M 6 A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
       M01A NOP 6
	   {
	   A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   }
	   TNT1 A 0 A_StartSound("Corpse/FallMetal",CHAN_6);
	   M01A QR 6 A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
       M01A S 6 A_NoBlocking;
       M01A T -1;
	   Stop;
  Raise:
	M01A SRQPONMLK 6;
    Goto Look;
  }
}

Class PM_CorpulentBall : PM_FatShotBase
{
  Default
  {
   PROJECTILE;
   Speed 20;
   FastSpeed 30;
   Alpha 1;
   Radius 4;
   Height 6;
   Scale 0.3;
   DamageFunction (random(30,35));
   DeathSound "HectebusBall/Exp";
   SeeSound "Corpulent/Whiz";
   Decal "BigScorch";
   DamageType "Fire";
   RenderStyle "Add";
  }
  
  override void PostBeginPlay()
  {
   tex[0] = TexMan.CheckForTexture("LEYSO0",TexMan.Type_Sprite);
   tex[1] = TexMan.CheckForTexture("DTPRO0",TexMan.Type_Sprite);
   tex[2] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
  }
  
  TextureID tex[3];
 
  States
  {
    Spawn:
    TNT1 A 0;
	  TNT1 A 0
	  {
		A_SpawnParticleEx
		(
		"",
		tex[0],
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: random(1,5),
		size: frandom(130,200),
		startalphaf: 1.0,
		fadestepf: 0,
		sizestep: frandom(0.0,3),
		startroll: random(-180,180)
		);
		A_SpawnParticleEx
		(
		"",
		tex[1],
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: random(1,7),
		size: frandom(90,155),
		startalphaf: 1.0,
		fadestepf: 0,
		sizestep: frandom(0.0,3),
		startroll: random(-180,180)
		);
		for(int i=random(5,8);i>0;i--)
		A_SpawnParticleEx
		(
		"",
		tex[2],
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,100),
		size: frandom(1,12),
		xoff: frandom(-2,2),
		yoff: frandom(-2,2),
		zoff: frandom(-2,2),
		velx: frandom(1,10),
		vely: frandom(-3,3),
		velz: frandom(-0.6,3),
		accelx: frandom(-0.005,0.005),
		accely: frandom(-0.005,0.005),
		accelz: frandom(-0.0,-0.8),
		startalphaf: 1.0,
		fadestepf: -1,
		sizestep: -0.2,
		startroll: random(-180,180)
		);
	  }
    Go:
      BLHD AB 1 Bright Light("HectebusBallLight") A_SpawnItem("PM_CorpulentBallTrail");
      Loop;
    Death:
	  TNT1 A 0 A_SpawnItemEx("OrangeShockwave",0,0,0,0,0,0,0,128,0);
	  TNT1 AAA 0 A_SpawnProjectile("PM_BigFireGlobs",10,0,random(0,360),2,random(0,360));
	  TNT1 A 0 Radius_Quake (2, 24, 0, 5, 0);
	  TNT1 AAA 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
	  TNT1 AAA 0 A_SpawnProjectile("PM_HectebusBallExplode",0,0,random(0,360),2,random(0,360));
      TABE CDE 3 Bright Light("YellowMediumFlicker2");
      Stop;
      }
}

Class PM_CorpulentFlamer : PM_CyberFodderFire
{
   Default
   {
   Radius 7;
   Height 7;
   Speed 15;
   FastSpeed 25;
   Scale 0.06;
   }
}