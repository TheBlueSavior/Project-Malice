Class PM_Sawcubus : PMFamineMonsterBase
{
	Default
	{
		PMMonsterBase.CodexLore
		"Berserk, reanimated humanoid corpses that only desire for the end of their starvation. Equiped with a mutated chainsaw, they are only capable of cutting living entities and carrion to shreds with the ol' reliable.
		The huge problem that they have is that they are crazed into the madness of Famine. Because of this, they have no choice but to hue and cry in desire. And the only solution for them is...
		
		\c[red]TO KILL AND MAIM EVERYTHING THAT MOVES. THEN DEVOUR EVERYTHING THAT YOU SEE.\c-";
		Health 140;
		Radius 20;
		Height 56;
		Speed 15;
		PainChance 90;
		Mass 400;
		+FLOAT
		+FLOORCLIP
		+QUICKTORETALIATE
		+MISSILEMORE
		+NOFEAR
		+SLIDESONWALLS
		+HARMFRIENDS
		+DOHARMSPECIES
		Species "Brute";
		Monster;
		MeleeDamage 4;
		MeleeRange 650;
		Scale 1.07;
		Bloodtype "PM_DarkRedBloodBase";
		SeeSound "Sawcubus/Sight";
		DeathSound "Sawcubus/Death";
		ActiveSound "Sawcubus/Active";
		PainSound "Sawcubus/Pain";
		Obituary "%o witnessed the slaugher of a sawcubus.";
		Tag "Sawcubus";
		TeleFogSourceType "PM_SmallDemonicTeleportFog";
		TeleFogDestType "PM_DemonicTeleportFog";
	}
	
	override void Tick()
	{
		super.Tick();
		HandleEating(400);
	}
	
	States
	{
	Spawn:
		CGOB AB 10 PM_Look();
		Loop;
	Look:
		CGOB AABBCCDD 8
		{
			PM_LookingForPlayer();
			A_Recoil(-0.65);
		}
		TNT1 A 0 A_JumpIfTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
		Loop;
	Eat:
		CGOB E 0 { A_StartSound("Sawcubus/SawIdle", 444, CHANF_LOOPING); A_StartSound("Sawcubus/Ready", 10); }
		CGOB EFEFEF 4;
		Goto See;
	Spotted:
		CGOB A 10
		{
			A_FaceTarget();
			A_StartSound("Sawcubus/Ready", 10);
		}
		CGOB A 0
		{
			PM_AlertSound();
			A_StartSound("Sawcubus/SawIdle", 444, CHANF_LOOPING);
		}
		TNT1 A 0 A_Jump(80, "Lunge");
	See:
		CGOB AABBCCDD 2
		{
			A_Chase();
			A_Recoil(-0.765);
		}
		CGOB F 0 A_Jump(42, "Dodge");
		Loop;
	Dodge:
		CGOB A 0
		{
			vel.z += random(4, 6);
			VelFromAngle(random(10, 15), frandom(0, 360));
		}
	    CGOB AAAAAAAAAAAAAA 1 A_FaceTarget();
		Goto See;
	Melee:
		CGOB A 0 A_JumpIfCloser(256, "Close");
		CGOB A 0 A_Jump(100, "Dodge");
		Goto Lunge;
	Lunge:
		CGOB A 20
		{
			A_FaceTarget();
			A_StartSound("Sawcubus/Attack", CHAN_VOICE);
		}
		CGOB E 0
		{
			vel.z += frandom(5, 10);
			A_StartSound("Sawcubus/Ready", 10);
		}
		CGOB EEFFEEFFEEFFEEFFEEFFEEFFEEFF 1
		{
			A_Recoil(-2);
			A_SpawnItemEx("PM_GenericPhaseGhost", zofs:6, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
			if (!(GetAge() % 2)) A_SpawnProjectile("PM_WeakMeleeHitbox",20,0,0);
		}
		Goto See;
	Close:
		CGOB A 20
		{
			A_FaceTarget();
			A_StopSound(444);
			A_StartSound("Sawcubus/SCREAM", CHAN_VOICE);
			A_StartSound("Sawcubus/Ready", 10);
		}
		CGOB E 0 A_StartSound("Sawcubus/AttackLoop", 444, CHANF_LOOPING);
		CGOB EEEFFF 1
		{
			A_FaceTarget(14, 14);
			A_Recoil(-.75);
			A_QuakeEx(1, 1, 0, 40, 0, 150, "", QF_3D|QF_SCALEDOWN);
			
			if (target && distance3D(target) < 90) A_StartSound("Sawcubus/Impact", CHAN_WEAPON);
			if (!(GetAge() % random(4, 5))) A_SpawnProjectile("PM_WeakMeleeHitbox",20,0,0);
			
			if (target && !CheckSight(target)) SetStateLabel("Cooldown");
			if (target && target.Health <= 0) SetStateLabel("Cooldown");
			if (target && distance3D(target) > 256) SetStateLabel("Cooldown");
		}
		Goto Close+2;
	Cooldown:
		CGOB E 0 { A_StopSound(CHAN_VOICE); A_StopSound(444); }
		CGOB E 0 A_StartSound("Sawcubus/SawIdle", 444, CHANF_LOOPING);
		CGOB EEEEEFFFFF 1;
		Goto See;
	Pain:
		CGOB G 3 A_StopSound(444);
		CGOB G 1 { A_Stop(); A_Pain(); A_StartSound("Sawcubus/SawIdle", 444, CHANF_LOOPING); }
		CGOB G 0 A_Jump(70, "Dodge");
		Goto See;
	Death:
		CGOB H 6;
		CGOB I 6
		{
			A_StopSound(444);
			PM_Scream();
		}
		CGOB J 6 A_NoBlocking();
		CGOB KL 6;
		CGOB L -1;
		Stop;
	Raise:
		CGOB LKJIH 6;
		Goto Spotted;
	}
}