Class PM_HellifiedSoldier : PMUnaffMonsterBase
{ 
    Default
	{
	PMMonsterBase.CodexLore
	"Humanity's struggle against evil continues to this day. More of the enemy continues to spill, yet they keep going. They hold onto the hope that one day, the evil shall be banished from their world, and only then will they finally live in peace.
	 They must keep fighting. Even if the possibility of that day keeps getting lower and lower.
	 
	 Having merged with their weapons, Hellified Soldiers attack with their experimental plasma rifles.
	 Can sometimes overcharge their plasma rifles to fire bursts of giant orbs that move close to the player and explode.";
    Health 50;
    Radius 20;
    Height 56;
    Speed 8;
    PainChance 200;
    Mass 100;
    MONSTER;
	+MISSILEevenMORE
    +FloorClip
	+HARMFRIENDS
	+DOHARMSPECIES
	Species "Zombie";
	Tag "Hellified Soldier";
    OBITUARY "%o was blasted by a Hellified Soldier.";
    SEESOUND "HelLSoldier/Sight";
    PainSound "HellSoldier/Pain";
    DeathSound "HellSoldier/Death";
    ACTIVESOUND "HellSoldier/Active";
	Bloodtype "PM_NormalBloodBase";
    DropItem "Clip";
	TeleFogSourceType "PM_PossessedTeleportFog";
	TeleFogDestType "PM_PossessedTeleportFog";
	}
    states
    {
    Spawn:
        HFS1 AA 10 PM_Look();
    Idle:
        HFS1 AAAAAAAAAA 10 PM_Look();
		HFS1 A 0
		{
		if (!random(0, 30))
			{
				A_StartSound(activesound,554,volume:frandom(0.1,0.5),pitch:frandom(0.75,0.88));
			}
		}
		HFS1 A 0 A_Jump(45,"Explore");
		Loop;
	Explore:
		TNT1 A 0 A_SetSpeed(6);
		TNT1 A 0
		{
		if (!random(0, 12))
			{
				A_StartSound(activesound,554,volume:frandom(0.1,0.5),pitch:frandom(0.75,0.88));
			}
		}
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",4,volume:0.5);
		HFS1 AAABBB 4
		{
		PM_Look();
		A_Wander();
		}
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",4,volume:0.5);
		HFS1 CCCDDD 4
		{
		A_Wander();
		PM_Look();
		}
		HFS1 A 0 A_Jump(40,"Idle");
		Loop;
    Look:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		HFS1 AABB 3 PM_LookingForPlayer();
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		HFS1 CCDD 3 PM_LookingForPlayer();
        Loop;
	Spotted:
	    TNT1 A 0 A_Jump(256,"Roar","SpotMissile","See");
		Goto See;
	Roar:
	    TNT1 A 0 PM_AlertSound();
		Goto See;
	See:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        HFS1 AA 3 A_Chase();
		HFS1 BB 3 A_Chase();
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		HFS1 CC 3 A_Chase();
		HFS1 DD 3 A_Chase();
        Loop;
    SpotMissile:
        TNT1 A 0 PM_AlertSound();
	    HFS1 FF 3 A_FaceTarget();
		Goto Missile;
    Missile:
		TNT1 A 0 A_Jump(75,"Missile2");
		TNT1 A 0 A_Jump(63,"Chargeup");
        HFS1 F 8
		{
	    A_FaceTarget(360,180,0,0);
	    A_SetAngle(angle+random(-10,10));
	    A_SetPitch(pitch+random(-15,15));
	    }
		TNT1 A 0 A_SpawnProjectile("CFodFlare",31,7);
        HFS1 G 4 Light("Shadowball2") A_SpawnProjectile("PM_HellifiedSoldierBall",31,7,random(-3, 3),CMF_AIMDIRECTION,pitch+frandom(-2, 2));
		HFS1 F 1 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_SpawnProjectile("CFodFlare",31,7);
		HFS1 G 4 Light("Shadowball2") A_SpawnProjectile("PM_HellifiedSoldierBall",31,7,random(-3, 3),CMF_AIMDIRECTION,pitch+frandom(-2, 2));
		HFS1 F 1 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_SpawnProjectile("CFodFlare",31,7);
		HFS1 G 4 Light("Shadowball2") A_SpawnProjectile("PM_HellifiedSoldierBall",31,7,random(-3, 3),CMF_AIMDIRECTION,pitch+frandom(-2, 2));
		HFS1 F 1 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_SpawnProjectile("CFodFlare",31,7);
		HFS1 G 4 Light("Shadowball2") A_SpawnProjectile("PM_HellifiedSoldierBall",31,7,random(-3, 3),CMF_AIMDIRECTION,pitch+frandom(-2, 2));
		HFS1 F 8 A_FaceTarget(10,20,0,0);
        Goto See;
	Missile2:
		HFS1 F 8
		{
	    A_FaceTarget(360,180,0,0);
	    A_SetAngle(angle+random(-10,10));
	    A_SetPitch(pitch+random(-15,15));
	    }
		TNT1 A 0 A_SpawnProjectile("CFodFlare",31,7);
        HFS1 G 2 Light("Shadowball2") A_SpawnProjectile("PM_HellifiedSoldierBall",31,7,random(-3, 3), CMF_OFFSETPITCH|CMF_AIMDIRECTION, random(-2, 2));
		HFS1 F 4 A_FaceTarget(15,20,0,0);
		TNT1 A 0 A_Jump(30,"See");
		D3CG E 0 A_MonsterRefire(60,"See");
		Goto Missile2+1;
    Chargeup:
        HFS1 F 8 Light("Shadowball2") A_FaceTarget();
        TNT1 A 0 A_StartSound("HellSoldier/ChargeReady");
		TNT1 A 0 A_StartSound("HellSoldier/ChargeReady2",CHAN_7);
        HFS1 FFFFFFFFFFFFFF 3 Light("ShadowBall2")
        {
        A_FaceTarget();
        A_SpawnItemEx("HellPlasmaTrail",20,14,29,random(-1,1),random(-1,1),random(-1,1),0,SXF_NOCHECKPOSITION);
        }
        TNT1 A 0 A_StartSound("HellSoldier/ChargeClose",CHAN_5);
        TNT1 A 0 A_StartSound("NetherConduit/HomingLoop",CHAN_6,1,true);
        HFS1 FFFFFFFFFFFFFF 2 Light("ShadowBall2")
        {
        A_FaceTarget();
        A_SpawnItemEx("HellPlasmaTrail",20,14,29,random(-2,2),random(-2,2),random(-2,2),0,SXF_NOCHECKPOSITION);
        A_SpawnItemEx("HellPlasmaChargeFlare",20,14,29,0,0,0,0,SXF_NOCHECKPOSITION);
        }
        TNT1 AAAA 0 A_SpawnItemEx("CFodFlare",20,5,29);
        HFS1 F 1 Light("ShadowBall2");
        HFS1 G 8 Light("ShadowBall3")
        {
        PM_LeadTarget(18);
        A_SpawnProjectile("PM_HellifiedSoldierChargeBall",31,7,random(-3,3),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch*1);
        }
        TNT1 A 0 A_StartSound("HellSoldier/ChargeClose",CHAN_5);
        HFS1 HHHHHHHHFFFFFF 2 Light("ShadowBall2")
        {
        A_FaceTarget();
        A_SpawnItemEx("HellPlasmaTrail",20,14,29,random(-2,2),random(-2,2),random(-2,2),0,SXF_NOCHECKPOSITION);
        A_SpawnItemEx("HellPlasmaChargeFlare",20,14,29,0,0,0,0,SXF_NOCHECKPOSITION);
        }
        TNT1 AAAA 0 A_SpawnItemEx("CFodFlare",20,5,29);
        HFS1 F 1 Light("ShadowBall2");
        HFS1 G 8 Light("ShadowBall3")
        {
        PM_LeadTarget(18);
        A_SpawnProjectile("PM_HellifiedSoldierChargeBall",31,7,random(-3,3),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch*1);
        }
        TNT1 A 0 A_StartSound("HellSoldier/ChargeClose",CHAN_5);
        HFS1 HHHHHHHHHFFFFFF 2 Light("ShadowBall2")
        {
        A_FaceTarget();
        A_SpawnItemEx("HellPlasmaTrail",20,14,29,random(-2,2),random(-2,2),random(-2,2),0,SXF_NOCHECKPOSITION);
        A_SpawnItemEx("HellPlasmaChargeFlare",20,14,29,0,0,0,0,SXF_NOCHECKPOSITION);
        }
        TNT1 AAAA 0 A_SpawnItemEx("CFodFlare",20,5,29);
        HFS1 F 1 Light("ShadowBall2");
        HFS1 G 8 Light("ShadowBall3")
        {
        PM_LeadTarget(18);
        A_SpawnProjectile("PM_HellifiedSoldierChargeBall",31,7,random(-3,3),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch*1);
        }
        HFS1 HHHHHHFFFF 1 A_FaceTarget();
        Goto See;
    Pain:
        HFS1 H 5;
        HFS1 H 3 A_Pain;
        Goto See;
    Death:
        HFS1 I 7 A_NoBlocking;
        HFS1 J 7 PM_Scream();
        HFS1 K 7;
        HFS1 L 5;
		TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
        HFS1 M 90;
		TNT1 A 0 { if(random(1,3) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
        HFS1 M -1;
        Stop;
    XDeath:
        TNT1 A 0 A_StopSound(555);
        HFS1 N 5 PM_Gib();
        HFS1 O 5 A_XScream;
        HFS1 P 5 A_NoBlocking;
        HFS1 QRSTU 5;
        HFS1 V -1;
        Stop;
    Raise:
        HFS1 MLKJI 5;
        Goto Look;
    }
}

Class HellPlasmaTrail : Actor
{
  Default
  {
  +CLIENTSIDEONLY
  +NOINTERACTION
  RenderStyle "Add";
  +Bright
  Scale 0.07;
  Alpha 0.95;
  }
States
{
Spawn:
	TNT1 A 2;
Go:
    DTPR R 1 Bright 
	{
	A_SetScale(Scale.X-0.008);
	A_FadeOut(0.27);
	}
    Loop;
    }
}

Class PM_HellifiedSoldierBall : PMProjectileBase
{
   Default
   {
     Height 3;
     Radius 3;
     Speed 18;
	 FastSpeed 38;
     Damagefunction (random(10,15));
	 DamageType "Plasma";
     Scale 0.07;
     Projectile;
     RenderStyle "Add";
     SeeSound "HellSoldier/Plasma";
     Decal "PM_HellPlasmaScorch";
     DeathSound "GenericPlasmaExp";
     PMProjectileBase.WhizSound "PM/PlasmaWhiz";
     PMProjectileBase.WhizRangeCheck 250;
     +BRIGHT
   }
   
   States
   {
   Spawn:
      DTPR R 1 Bright Light("Shadowball")
	  {
	   PM_CheckWhiz();
	   A_SpawnParticleEx("",TexMan.CheckForTexture("DTPRR0"),style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: 1,size: 60.0,xoff: -7,startalphaf: 0.2,fadestepf: -1,sizestep: -4);
	   A_SpawnParticleEx("f44336",TexMan.CheckForTexture("DTPRR0"),style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: 5,size: 25.0,xoff: -10,startalphaf: 0.7,fadestepf: -1,sizestep: -5,startroll: random(-180,180));
	   A_SpawnParticleEx("",TexMan.CheckForTexture("RLSSA0"),style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: random(5,14),size: random(2,8),xoff: -8,yoff: random(-3,3),zoff: random(-3,3),velx: random(-1,1),vely: random(-1,1),velz: random(-1,1),startalphaf: 0.9,fadestepf: -1,sizestep: -4);
	  }
    Loop;
   Death:
	  TNT1 AAAAAA 0 { A_SpawnParticleEx("",TexMan.CheckForTexture("RLSSA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(6,15),size: random(10,20),velx: random(-6,6),vely: random(-6,6),velz: random(-6,6),startalphaf: 0.7,fadestepf: -1,startroll: random(-180,180)); }
	  TNT1 AAA 0 A_SpawnItemEx("HellPlasmaExp",0,0,0);
	  TNT1 A 0 A_SpawnItemEx("HellPlasmaFlare",0,0,0);
	  TNT1 A 5 Light("Shadowball2");
      Stop;
	  }
}

Class HellPlasmaExp : Actor
{
    Default
	{
	Scale 0.3;
	Alpha 0.6;
	Renderstyle "Add";
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Bright
	+FORCEXYBILLBOARD
	Speed 1;
	}
	States
{
Spawn:
	FXPR AABBCDEFGHIJKLM 1 Bright A_FadeOut(0.06);
	Stop;
	}
}

Class HellPlasmaFlare : Actor
{
Default
{
 +CLIENTSIDEONLY
 +NOINTERACTION
 +FORCEXYBILLBOARD
 RenderStyle "Add";
 +Bright
 Scale 0.15;
 Alpha 0.95;
}
States
{
Spawn:
    DTPR RR 1 Bright 
	{
	A_FadeOut(0.05);
	A_SetScale(Scale.X+0.008);
	}
    Loop;
    }
}

Class HellifiedSoldierPuff : Actor
{
    Default
	{
    Radius 3;
    Height 3;
    RenderStyle "Add";
    Alpha 0.67;
	Scale 0.4;
    +NoGravity
    +NoBlockMap
    +DontSplash
    +ForceXYBillboard
    }
  States
  {
  Spawn:
    TNT1 A 1 Bright;
    HSB1 CDEFGHI 2 Bright;
    Stop;
  }
}

Class PM_HellifiedSoldierChargeBall : Actor
{
   Default
   {
   Height 3;
   Radius 3;
   Speed 21;
   DamageFunction (random(10,15));
   DamageType "Plasma";
   Scale 0.10;
   PROJECTILE;
   RenderStyle "Add";
   +BRIGHT
   +SEEKERMISSILE
   SeeSound "HellSoldier/ChargePlas";
   Decal "PM_HellPlasmaScorch";
   DeathSound "";
   }
   override void PostBeginPlay()
   {
    Super.PostBeginPlay();
	tex[0] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("RLSSA0",TexMan.Type_Sprite);
   }
   
   TextureID tex[2];

   void PM_HSChargeBallParticle()
   {
	 A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: 1,size: 150.0,xoff: -7,startalphaf: 0.45,fadestepf: -1,sizestep: -4);
	 A_SpawnParticleEx("f44336",tex[0],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: 5,size: 120.0,xoff: -10,startalphaf: 0.7,fadestepf: -1,sizestep: -5,startroll: random(-180,180));
	for(int i=random(1,3);i>0;i--)
	 { A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: random(5,50),size: random(2,20),xoff: -8,yoff: frandom(-5,5),zoff: frandom(-5,5),velx: frandom(-0.5,0.5),vely: frandom(-0.5,0.5),velz: frandom(-0.5,0.5),startalphaf: frandom(0.45,0.9),fadestepf: -1,sizestep: -2,startroll: random(-180,180)); }
   }

   void PM_HSChargeBallParticleRelease()
   {
	 A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: 1,size: 150.0,xoff: -7,startalphaf: 0.45,fadestepf: -1,sizestep: -4);
	 A_SpawnParticleEx("f44336",tex[0],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: 5,size: 150.0,xoff: -10,startalphaf: 0.7,fadestepf: -1,sizestep: -5,startroll: random(-180,180));
	for(int i=random(1,3);i>0;i--)
	 { A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: random(5,50),size: random(2,20),xoff: -8,yoff: frandom(-5,5),zoff: frandom(-5,5),velx: frandom(-2,2),vely: frandom(-2,2),velz: frandom(-2,2),startalphaf: frandom(0.45,0.9),fadestepf: -1,sizestep: -2,startroll: random(-180,180)); }
   } 
   States
   {
   Spawn:
    DTPR R 1 Bright Light("Shadowball3") PM_HSChargeBallParticle();
    TNT1 A 0 A_JumpIfTracerCloser(250,"Closeup");
    Loop;
   Closeup:
      DTPR RRRRRRRRR 1 Bright Light("Shadowball3") PM_HSChargeBallParticle();
      TNT1 A 0 A_ScaleVelocity(0.50);
      DTPR RRRRRR 1 Bright Light("Shadowball3") PM_HSChargeBallParticleRelease();
      TNT1 A 0 A_ScaleVelocity(0.45);
      DTPR RRRRR 1 Bright Light("Shadowball3") PM_HSChargeBallParticleRelease();
      TNT1 A 0 A_ScaleVelocity(0.20);
      DTPR RRRR 1 Bright Light("Shadowball3") PM_HSChargeBallParticleRelease();
      TNT1 A 0 A_ScaleVelocity(0);
      DTPR RRRRRRRRRRRRRRRRRRRR 1 Bright Light("Shadowball3") PM_HSChargeBallParticleRelease();
   Death:
      TNT1 A 0 A_StartSound("HellSoldier/ChargeExp");
      TNT1 A 0 A_Explode(16,130);
	  TNT1 AAAA 0 A_SpawnItemEx("HellPlasmaExp",0,0,0);
	  TNT1 AAAAAAA 0 A_SpawnItemEx("HellPlasmaFlare",0,0,0);
      TNT1 AAAA 0 A_SpawnItemEx("HellPlasmaChargeFlare",0,0,0);
      TNT1 AA 0 A_SpawnItemEx("HellPlasmaChargeExp",0,0,0);
      TNT1 AAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HellPlasmaChargeTrail2",0,0,0,random(8,-8),random(8,-8),random(8,-8),0,SXF_NOCHECKPOSITION);
	  TNT1 A 5 Light("Shadowball2");
      Stop;
	  }
}

Class HellPlasmaChargeFlare : Actor
{
    Default
	{
	Scale 0.18;
	Alpha 0.7;
	Renderstyle "Add";
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Bright
	+FORCEXYBILLBOARD
	Speed 1;
	}
	States
{
Spawn:
	DTPR RRRRRRRRRRRRRR 1 Bright A_FadeOut(0.06);
	Stop;
	}
}

Class HellPlasmaChargeExp : Actor
{
    Default
	{
	Scale 0.30;
	Alpha 0.8;
	Renderstyle "Add";
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Bright
	+FORCEXYBILLBOARD
	Speed 1;
	}
	States
{
Spawn:
	DTPR RRRRRRRRRRRRRRRRRRRRR 1 Bright 
    {
    A_FadeOut(0.06);
    A_SetScale(Scale.X+0.085);
    }
	Stop;
	}
}

Class HellPlasmaChargeTrail2 : Actor
{
 Default
 {
 +CLIENTSIDEONLY
 +NOINTERACTION
 +ROLLSPRITE
 +ROLLCENTER
 +FORCEXYBILLBOARD
 Scale 0.85;
 Alpha 0.3;
 RenderStyle "Add";
 +BRIGHT
 }
States
  {
 Spawn:
   TNT1 A 2;
 Go:
   RLSS AA 2
   {
   A_SetRoll(random(0,259),SPF_INTERPOLATE);
   A_FadeOut(0.05);
   }
   Loop;
  }
}