Class PM_Sharpshooter : PMEnvyMonsterBase
{
  int user_numbullets;
  int retreat;
  int annoyed;
  int standoff;
  Default
  {
   PMMonsterBase.CodexLore 
   "Envy sprites seem to favor whom they consider to be cowardly and even treacherous. Those who are possessed by these sprites and are thus given a second shot at life freely use it to their own selfish ends, demonstrating that most, if not all of those that Envy possesses show a quality of jealousy and covetousness.
    It's not known, however, if the victim itself is operating fully independent from the sprite or if it's just the sprite perfectly replicating every aspect of the victim it's possessing. Maybe they're just suffering all the same like the others.
	
	Uses a high-caliber sniper rifle, and would prefer keeping his distance away from you more than getting close.";
   Health 70;
   Radius 20;
   Height 56;
   Mass 100;
   Speed 12;
   PainChance 100;
   Monster;
   +FLOORCLIP
   +HARMFRIENDS
   +DOHARMSPECIES
   +MISSILEEVENMORE
   SeeSound "EnvyZombie/Sight";
   PainSound "EnvyZombie/Pain";
   DeathSound "EnvyZombie/Death";
   ActiveSound "EnvyZombie/Active";
   Tag "Resentful Sharpshooter";
   Obituary "%o was domed by a Resentful Sharpshooter.";
   Dropitem "Chaingun";
   Species "Chaingunner";
   Bloodtype "PM_NormalBloodBase";
   TeleFogSourceType "PM_PossessedTeleportFog";
   TeleFogDestType "PM_PossessedTeleportFog";
  }

	void PM_SharpshooterAimLaser(Vector2 off = (0, 0), double scal = 0.065)
	{
		if (!Target) return;
		double sa = sin(Angle);
		double ca = cos(Angle);
		Vector2 relOff = (off.X * ca + off.Y * sa, off.X * sa - off.Y * ca);
		PM_SpawnBolt(self, Target.Vec3Offset(0,0,Target.Height-40),"24ff24",TexMan.CheckForTexture("RLSSA0"),maxDiff: 0,scale: scal, offset: (relOff.X, relOff.Y, 42),baseTime: 1,fadeTime: 0);
	}
	
	void PM_SharpshooterChase()
	{
	 if(user_numbullets <= 0)
	  { SetStateLabel("ReloadRetreat"); }
     if(retreat)
	  {
	   retreat--;
	   A_SetTics(2);
	  }
	 A_Chase();
	}

  States
  {
  Spawn:
    SN0P A 0 NoDelay
    {
     user_numbullets = random(3,6);
    }
  Idle:
    SN0P AA 10 PM_Look();
    Loop;
  Look:
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
    SN0P AABB 3 PM_LookingForPlayer();
    TNT1 A 0 A_JumpIfTargetInLOS("Spotted",150,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
	SN0P CCDD 3 PM_LookingForPlayer();
    Goto Look+1;
  Spotted:
    TNT1 A 0 { PM_AlertSound(); }
  See:
    TNT1 A 0 { standoff == 0; A_SetSpeed(12); bMISSILEMORE = false; bFRIGHTENED = false; }
    SN0P AABB 3 PM_SharpshooterChase();
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
	SN0P CCDD 3 PM_SharpshooterChase();
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
	TNT1 A 0 
	{
	 if(target && Distance3D(target) < 250 && random(1,20) == 1)
      { SetStateLabel("BackCheck"); }
	}
	Loop;
   BackCheck:
    TNT1 A 0 A_Jump(150,"Backup");
	Goto Retreat;
   ReloadRetreat:
    TNT1 A 0 { bFRIGHTENED = true; }
    SN0P AABB 2 A_Chase(null,null);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
    SN0P CCDD 2 A_Chase(null,null);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
	TNT1 A 0 A_Jump(25,"ZReload");
	TNT1 A 0 A_CheckSight("ZReload");
	Goto ReloadRetreat+1;
  Missile:
    TNT1 A 0 A_Jump(85,"StandoffCheck");
    SN0P E 5
    {
     if(user_numbullets <= 0)
      { SetStateLabel("NoAmmo"); }
     A_FaceTarget(360,180);
     A_SetAngle(angle+random(-10,10));
     A_SetPitch(pitch+random(-15,15));
    }
	SN0P EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 
	{
	 if(annoyed <= 2)
	  {
	   if(LineTrace(angle,130,pitch,TRF_SOLIDACTORS,32))
	    { SetStateLabel("Retreat"); }
	  }
	  A_FaceTarget(10,10);
	  A_SpawnParticleEx( "00ff00", TexMan.CheckForTexture("SPKWE0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: random(1,2), size: frandom(30,40), xoff: 40, yoff: 0, zoff: 41, startalphaf: frandom(0.6,0.9),startroll: random(-180,180));
	  PM_SharpshooterAimLaser();
	}
	SN0P E 15
	{
	A_StartSound("Sharpshooter/Aim",13);
	A_SpawnParticleEx( "00ff00", TexMan.CheckForTexture("SPKWE0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: 5, size: 120, xoff: 40, yoff: 0, zoff: 41, startalphaf: 0.9, sizestep:-20,startroll: random(-180,180));
	}
    SN0P F 2 Bright Light("AvatarFireball2") 
    {
	 if(annoyed == 2)
	  { annoyed -= 2; }
	 PM_LeadTarget(56);
     A_StartSound("Sharpshooter/Shot",CHAN_5,CHANF_OVERLAP);
     A_StartSound("Sharpshooter/ShotDistant",CHAN_7,CHANF_OVERLAP);
     A_SpawnProjectile("PM_SniperBullet",41,10,frandom(-0.5,0.5),CMF_AIMDIRECTION,pitch+frandom(-0.5,0.5));
     user_numbullets--;
    }
    SN0P EEEEEEE 3 A_FaceTarget(3,3);
	SN0P E 12 A_StartSound("Sharpshooter/Reload",8);
	SN0P E 5 A_SpawnItemEx("PM_BulletCasing",-10,0,42,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
	SN0P EEEEE 3 A_FaceTarget(5,5);
	TNT1 A 0 { if(standoff) { SetStateLabel("StandoffRepos"); } }
    Goto See;
  StandoffCheck:
    TNT1 A 0 
	{
	 if(target && CheckSight(target) && Distance3D(target) < 250)
	  SetStateLabel("Retreat");
	 standoff == 1;
	}
  Standoff:
    TNT1 A 0 { bFRIGHTENED = false; }
	TNT1 A 0 { bMISSILEMORE = true; }
	TNT1 A 0 A_Jump(10,"See");
	TNT1 A 0 A_JumpIfCloser(250,"Retreat");
	SN0P BBBBBBBBBBBB 3 
	{
	 A_Chase("Retreat","Missile",CHF_DONTMOVE);
	 A_FaceTarget();
	}
	TNT1 A 0 A_Jump(120,"StandoffRepos");
	Loop;
  StandoffRepos:
	TNT1 A 0 { bFRIGHTENED = true; }
	SN0P A 1 A_Chase();
  StandoffReposLoop:
	TNT1 A 0 A_SetSpeed(14);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",4);
    SN0P AABB 2 A_Wander();
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",4);
	SN0P CCDD 2 A_Wander();
	TNT1 A 0 { bFRIGHTENED = false; }
	TNT1 A 0 A_Jump(80,"Standoff");
	Loop;
  NoAmmo:
	SN0P E 7 A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SN0P EE 10 A_FaceTarget();
	TNT1 A 0 A_Jump(200,"ZReload");
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SN0P E 15 A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SN0P AAAA 5 A_FaceTarget();
	Goto ZReload;
  ZReload:
     TNT1 A 0 A_StartSound("PM/RifleMagOut",CHAN_5);
     SN0P EEEE 5;
     TNT1 A 0 A_SpawnItemEx("PM_EmptyRifleMag",0,0,20,frandom(-1,1),frandom(-1,-5),random(-4,0));
     SN0P CCCBBBB 3;
     TNT1 A 0 A_StartSound("PM/RifleMagIn",CHAN_5);
     TNT1 A 0 { user_numbullets = (user_numbullets + random(3,8)); }
     SN0P DDBB 4;
     TNT1 A 0 A_StartSound("Sharpshooter/Reload",CHAN_5);
     SN0P AAAABB 2;
     Goto See;
  Retreat:
     TNT1 A 0
	 {
	  bFRIGHTENED = true;
	  retreat = random(20,30);
	  annoyed++;
	 }
	 Goto See;
  Backup:
     SN0P DDCC 2 { A_FaceTarget(); A_Recoil(3); }
	 TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
	 SN0P BBAA 2 { A_FaceTarget(); A_Recoil(3); }
	 TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
	 SN0P DDCC 2 { A_FaceTarget(); A_Recoil(3); }
	 TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
	 SN0P BBAA 2 { A_FaceTarget(); A_Recoil(3); }
	 TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
	 SN0P DDCC 2 { A_FaceTarget(); A_Recoil(3); }
	 TNT1 A 0 A_StartSound("ZombieSoldier/Step",9,CHANF_OVERLAP);
	 TNT1 A 0 
	 { 
	 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	 if(target)
	   {
	    if(!CheckSight(target))
		 SetStateLabel("See");
	    if(Distance3D(target) > 150)
		 SetStateLabel("Missile");
	   }
	}
	 Goto See;
  Pain:
     SN0P G 3;
     SN0P G 3 A_Pain;
     Goto See;
  Death:
     SN0D A 5;
     SN0D B 5 PM_Scream();
     SN0D C 5 A_NoBlocking;
     SN0D D 5;
	 TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
     SN0D E 90;
     TNT1 A 0 { if(random(1,3) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
     SN0D E -1;
     Stop;
  XDeath:
     TNT1 A 0 A_StopSound(555);
     SN0D F 5 PM_Gib();
     SN0D G 5 A_XScream;
     SN0D H 5 A_NoBlocking;
     SN0D IJK 5;
     SN0D L -1;
     Stop;
  Raise:
     SN0D E 5;
     SN0D DCBA 5;
     Goto Look;
  }
}

Class PM_SniperBullet : PM_SlugBullet
{
 Default
 {
  Speed 56;
  DamageFunction (random(40,50));
  Scale 0.85;
  RenderStyle "Add";
  Decal "PM_BulletHoleLarge";
  Alpha 1.0;
  PMProjectileBase.WhizSound "PM/HeavyBulletWhiz";
 }
 
States
{
   Spawn:
      TNT1 A 0;
	  TNT1 A 0
	  {
	   for(int i=random(4,8);i>0;i--)
	    { A_SpawnParticleEx( "#d2d2d2", tex[0], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,250),size: frandom(10,50), xoff: -25, velx: frandom(0,2), vely: frandom(-0.4,0.4), velz: frandom(-0.4,0.7),startalphaf: frandom(0.3,0.5), fadestepf: -0.5, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1)); }
	   A_SpawnParticleEx( "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: random(1,4), size: frandom(150,200), xoff: -22, startalphaf: frandom(0.75,1), sizestep: frandom(0.0,3),startroll: random(-180,180) );
	   A_SpawnParticleEx( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: random(1,4), size: frandom(150,300), xoff: -22, startalphaf: 1.0, sizestep: frandom(0.0,3),startroll: random(-180,180) );
	  }
	Go:
		TRCR A 1 Bright { PM_CheckWhiz(); A_Quake(1,4,0,100,0); A_SpawnParticleEx("",TexMan.CheckForTexture("GNSML0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 95,size: 15.0,xoff: -5, velx: frandom(0,2), startalphaf: 0.4,fadestepf: -1,startroll: random(-180,180)); }
        Loop;
	Death:
	    TNT1 AAAAA 0 A_SpawnItemEx("PM_HitPuff",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	    TRCR A 0 A_StartSound("PM/LargeBulletHit",2);
	    TNT1 A 0 A_SpawnItemEx("PM_OrangeEmbersSpawn");
	    TNT1 AAAAAA 0 A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	    TNT1 AAA 0 A_SpawnItemEx("PM_BulletHitDust",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,SXF_NOCHECKPOSITION,170);
	    TNT1 A 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
        TNT1 AAAAAAAAA 0 A_SpawnItemEx("PM_BulletShrapnelNoCheck",0,0,0,random(-10,10),random(-10,10),random(-10,10),random(-180,180),SXF_NOCHECKPOSITION,50);
	    TNT1 AA 0 A_SpawnItemEx("PM_BulletHitFog",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,SXF_NOCHECKPOSITION,150);
        Stop;
     XDeath:
	    TNT1 A 1 Bright A_RadiusThrust(3000,90);
        TNT1 A 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
        TRCR A 0 A_StartSound("PM/LargeBulletHit",2);
        Stop;
	}
}