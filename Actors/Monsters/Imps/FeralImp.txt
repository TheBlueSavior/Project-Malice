//ALL CREDITS TO Major Cooke's Doom 4 for Doom FOR THE LEAP CODE!
Class PM_FeralImp : PMWrathMonsterBase
{

  int delay;
  int user_leapstrength;
  
  Default
  {
  PMMonsterBase.CodexLore
  "Ravenous battle-hardened predators from the isolated badlands in the underworld. They are not as volcanic as their Charred brethren, yet fire immolates inside the body to the point where it gets out of the mouth, making them fireborne.
  Their anger and desire is off the charts when it comes to combat. Every time it wants to kill, its patience will start to decrease, and their hatred will start to gradually grow.
  When left in its devices, it will start to snap and they start to become lustful for destruction, fully taking advantage of their rage...
  
  They LOVE being in melee range, and can tear you apart in seconds. Alongside that, it will spew dual fireballs.";
  Obituary "%o burned to a crisp by a Feral Imp's fireballs.";
  HitObituary "%o was cut down by a Feral Imp.";
  Health 75;
  Radius 20;
  Height 56;
  Mass 120;
  Speed 11;
  PainChance 160;
  Monster;
  +DONTHARMCLASS
  +DONTHARMSPECIES
  //+NOINFIGHTING
  +NOTARGET
  +MISSILEMORE
  Tag "Feral Imp";
  Species "Imp";
  //DropItem "RagePoint1"
  SeeSound "FeralImp/Sight";
  PainSound "FeralImp/Pain";
  DeathSound "FeralImp/Death";
  ActiveSound "FeralImp/Active";
  BloodType "PM_LavaBloodBase";
  BloodColor "Orange";
  MeleeRange 155;
  +FLOORCLIP
  TeleFogSourceType "PM_SmallDemonicTeleportFog";
  TeleFogDestType "PM_WrathTeleportFog";
  }
  
  States
  {
  Spawn:
    1ROZ A 0 NoDelay { pm_irecount = random(45,80); }
  Idle:
    1ROO A 10 PM_Look();
    loop;
  Look:
	TNT1 A 0 A_SetSpeed(11);
	1ROO AA 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	TNT1 A 0 A_JumpIfTargetinLOS("Spotted",250,JLOSF_DEADNOJUMP);
	1ROO BBCC 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	1ROO DD 3 PM_LookingForPlayer();
    Loop;
  Spotted:
    TNT1 A 0 A_Jump(256,"Roar","RoarNoAnim","See","See");
  RoarNoAnim:
    TNT1 A 0 PM_AlertSound();
  See:
    TNT1 A 0
	{
	 if(ire == true) //if it's already pissed off..
	  { A_Jump(256,1); } //then this basically does nothing and moves onto the next state
	 else if(pm_irecount < 0) //if it isn't..
	  { SetStateLabel("IreRoar"); } //then IT WILL BE NOW BITCH
	}
	TNT1 A 0 A_SetSpeed(11);
	1ROO AA 3 
	{
	A_Chase();
	if(ire)
	  {
	   A_SetTics(2);
	   A_Recoil(-0.35);
	  }
	}
	TNT1 A 0
	{
	A_StartSound("Imp/Step",4);
	if(ire)
	A_SpawnItemEx("PM_HuskWalkFire",0,5,0,1,0,0,0,128);
	}
	1ROO BBCC 3
	{
	A_Chase();
	if(ire)
	 {
	   A_SetTics(2);
	   A_Recoil(-0.35);
	 }
	}
	TNT1 A 0 
	{
	A_StartSound("Imp/Step",4);
	if(ire)
	A_SpawnItemEx("PM_HuskWalkFire",0,-5,0,1,0,0,0,128);
	}
	1ROO DD 3
	{
	 A_Chase();
	if(ire)
	 {
	  A_SetTics(2);
	  A_Recoil(-0.35);
	 }
	}
	TNT1 A 0 A_Jump(46,"Rush");
	Loop;
  Rush:
	TNT1 A 0 A_SetSpeed(14);
	1ROO AA 2 
	{
	 A_Chase();
	 if(ire)
	 {
	 A_Recoil(-0.66);
	 A_SpawnItemEx("PM_HuskRushFire",random(-8,8),random(-8,8),random(10,50),0,0,frandom(1,3));
	 }
	}
	TNT1 A 0 
	{
	 A_StartSound("Imp/Step",4);
	 if(ire)
	 A_SpawnItemEx("PM_HuskWalkFire",0,-5,0,1,0,0,0,128);
	}
	1ROO BBCC 2 
	{
	 A_Chase();
	 if(ire)
	 {
	 A_Recoil(-0.66);
	 A_SpawnItemEx("PM_HuskRushFire",random(-8,8),random(-8,8),random(10,50),0,0,frandom(1,3));
	 }
	}
	TNT1 A 0 
	{
	 A_StartSound("Imp/Step",4);
	 if(ire)
	 A_SpawnItemEx("PM_HuskWalkFire",0,-5,0,1,0,0,0,128);
	}
	1ROO DD 2
	{
	 A_Chase();
	 if(ire)
	 {
	 A_Recoil(-0.66);
	 A_SpawnItemEx("PM_HuskRushFire",random(-8,8),random(-8,8),random(10,50),0,0,frandom(1,3));
	 }
	}
	TNT1 A 0 A_Jump(26,"See");
	Loop;
  Rip:
	1ROO EEEEE 1 A_FaceTarget();
	TNT1 A 0 ThrustThingZ(0,14,0,1);
	TNT1 A 0 A_Recoil(-9);
	TNT1 A 0 A_StartSound("FeralImp/Swing");
	1ROO FFF 1 A_FaceTarget();
	1ROO G 8 A_SpawnProjectile("PM_FeralLunge",18,0,0,CMF_AIMDIRECTION);
	1ROO IIIII 1 A_FaceTarget();
	TNT1 A 0 ThrustThingZ(0,14,0,1);
	TNT1 A 0 A_Recoil(-9);
	TNT1 A 0 A_StartSound("FeralImp/Swing");
	1ROA H 8 A_SpawnProjectile("PM_FeralLunge",18,0,0,CMF_AIMDIRECTION);
	1ROO A 10;
	TNT1 A 0 { pm_irecount -= random(5,8); }
	Goto See;
  Melee:
	TNT1 A 0 A_JumpIfCloser(120, "Rip");
	1ROO E 1 A_FaceTarget();
	1ROT CD 4;
    TNT1 A 0 ThrustThingZ(0,25,0,1);
    TNT1 A 0 A_FaceTarget();
    TNT1 A 0 A_Recoil (-20);
	TNT1 A 0 A_StartSound("FeralImp/Attack",CHAN_7);
	1ROT EF 2 A_SpawnProjectile("PM_FeralLunge",18,0,0,CMF_AIMDIRECTION);
    1ROT F 6 A_FaceTarget();
	1ROT G 1 A_SpawnProjectile("PM_FeralLunge",18,0,0,CMF_AIMDIRECTION);
	1ROT GG 2 A_SpawnProjectile("PM_FeralLunge",18,0,0,CMF_AIMDIRECTION);
	1ROT GHHH 2;
	TNT1 A 0 { pm_irecount -= random(5,8); }
	Goto See;
 Missile:
	TNT1 A 0 A_Jump(170,"Fireballs");
	1ROO E 0 A_StartSound("FeralImp/Leap");
	TNT1 A 0 
	{ 
	if(target && Distance3D(target) >= 768)
	SetStateLabel("Fireballs");
	}
	1ROZ AAAA 4 A_FaceTarget();
	1ROT EEEECD 4 A_FaceTarget();
    TNT1 A 0 A_CheckSight("LeapWait");
	1ROT A 0 A_StartSound("FeralImp/Attack",CHAN_7);
	1ROT A 0 A_Recoil(-(user_leapstrength/1.2));
	1ROO F 1 PM_LeapTo(target,768,"See");
	Goto LeapLoop;
  LeapLoop:
	1ROT FFFF 1 
	{
	if(random(1,15) == 1)
	   ThrustThing(random(0,360)*256/360,2,0,0);
	 if(target && Distance3D(target) <= 60)
	 { SetStateLabel("LeapMelee"); }
	}
	1ROT G 0 A_CheckFloor("LeapEnd");
	Loop;
   LeapMelee:
	1ROT G 1 A_Explode(8*random(1,5),50,XF_NOTMISSILE,0,50);
	1ROT GGGG 3 A_CheckFloor("LeapEnd");
	TNT1 A 0 ThrustThing(random(0,360)*256/360,2,0,0);
	Wait;
  LeapEnd:
	1ROT A 0 A_StartSound("fiend/land",CHAN_AUTO,CHANF_DEFAULT,1,2);
	1ROT A 0 
	{
	if(ire)
	 A_Jump(256,1);
	else
	 bNOPAIN = false;
	}
	1ROT G 4 A_Stop;
	1ROT H 8 { pm_irecount -= random(10,15); }
	Goto See;
  LeapWait:
    1ROT DD 2 A_FaceTarget();
	TNT1 A 0 A_JumpIfTargetInLOS("LeapWaitSee",360,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_Jump(4,"See");
	Loop;
  LeapWaitSee:
    1ROO DDD 2 A_FaceTarget;
	TNT1 A 0 A_StartSound("FeralImp/Attack",CHAN_7);
    1ROO F 1 PM_LeapTo(target,768,"See");
	Goto LeapLoop;
  Fireballs:
	TNT1 A 0 A_Jump(30,"Roar");
	TNT1 A 0 
	{
	 if(ire && random(1,3) == 1) //a_jump won't work with if checks so we use this
	 { SetStateLabel("IreDecide"); }      //LIFE IS PAIN. I HATE
	}
  Goto RealFireballs;
   IreDecide:
    TNT1 A 0 A_Jump(65,"FireSpit","Evade");
	Goto RealFireballs;
   Evade:
    TNT1 A 0 ThrustThing(randompick(0,128),10,0,0);
	TNT1 A 0 ThrustThingZ(0,10,0,1);
   RealFireballs:
	TNT1 A 0 A_StartSound("GenericChargeSmall");
    1ROO EEEEEEE 1 Light("Avatarfireball4") 
	{
	 A_FaceTarget();
	 A_SpawnitemEx("PM_FeralFireEffect",2,random(18,24),30,0,0,2);
	}
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	1ROO FF 3 Light("Avatarfireball4") 
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	}
	//TNT1 A 0 A_Recoil(-2)
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",10,random(10,12),33,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",15,random(10,12),33,0,0,2);
    1ROO G 8 Light("Avatarfireball4") 
	{
	  A_SpawnItemEx("PM_FeralFireEffect",15,random(10,12),33,0,0,2);
	  A_SpawnItemEx("PM_FeralFireEffect",15,random(10,12),33,0,0,2);
	 if(ire)
	 {
	  A_StartSound("FeralImp/Ireball",flags:CHANF_OVERLAP);
      A_SpawnProjectile("PM_FeralImpIreball",32,0,0);
	 }
	 else
	  A_SpawnProjectile("PM_FeralImpBall",32,0,random(-4,4),CMF_OFFSETPITCH,frandom(-0.7,0.7));
	  pm_irecount -= random(1,8);
	}
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(12,14),32,0,0,2);
	1ROO III 3 Light("Avatarfireball4") 
	{
	 A_FaceTarget();
	 A_SpawnItemEx("PM_FeralFireEffect",7,random(-12,-14),32,0,0,2);
	}
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(-12,-14),32,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",7,random(-12,-14),32,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",10,random(-10,-12),33,0,0,2);
	TNT1 A 0 A_SpawnItemEx("PM_FeralFireEffect",15,random(-10,-12),33,0,0,2);
    1ROA H 8 Light("Avatarfireball4") 
	{
	  A_SpawnItemEx("PM_FeralFireEffect",15,random(-10,-12),33,0,0,2);
	  A_SpawnItemEx("PM_FeralFireEffect",15,random(-10,-12),33,0,0,2);
	 if(ire)
	 {
	  A_StartSound("FeralImp/Ireball",11,flags:CHANF_OVERLAP);
      A_SpawnProjectile("PM_FeralImpIreball",32,0,0);
	 }
	 else
	  A_SpawnProjectile("PM_FeralImpBall",32,0,random(-4,4),CMF_OFFSETPITCH,frandom(-0.7,0.7));
	  pm_irecount -= random(1,8);
	}
    1ROO B 2 Light("Avatarfireball4") A_FaceTarget();
    Goto See;
  Roar:
	1ROT AAAAA 3 A_FaceTarget();
	TNT1 A 0 A_StartSound("FeralImp/Sight",CHAN_7);
	1ROT BBCCCCCCCCCCCCCC 3 
	{
	  A_FaceTarget();
	  A_SpawnItemEx("PM_OrangeEmbersFloat",3,0,45,random(1,3),random(-1,1),random(-1,1));
	 if(ire == true)
	  A_SetTics(2);
	}
	1ROO AAA 3 A_FaceTarget();
	Goto See;
  IreRoar:
  TNT1 A 0 A_StartSound("GenericChargeSmall",CHAN_5);
	1ROT AAAAA 3 A_FaceTarget();
	TNT1 A 0 
	{ 
     ire = true;
	 bNOINFIGHTING = true;
	 bNOPAIN = true;
	 bNOFEAR = true;
	 A_SpawnItemEx("PM_WrathIreActivate",0,0,20);
	 A_SpawnItemEx("PM_WrathIreEffect",0,0,20,flags:SXF_SETMASTER);
	}
	TNT1 A 0 PM_IreStartSound();
	TNT1 A 0 A_StartSound("FeralImp/Sight",CHAN_7,CHANF_DEFAULT,1,3);
	1ROT BBCCCCCCCCCCCCCC 2 A_FaceTarget();
	1ROO AAA 3 A_FaceTarget();
	Goto See;
  FireSpit:
	1ROT HH 5 A_FaceTarget();
	1ROT CCCCC 5 Light("Avatarfireball4")
	{
	 A_FaceTarget();
	 A_SpawnItemEx("CFireFlare",3,0,45);
	 A_SpawnItemEx("CFireFlare",3,0,45);
	 A_SpawnItemEx("PM_OrangeEmbersFloat",3,0,45,random(1,3),random(-1,1),random(-1,1));
	 A_SpawnProjectile("PM_FeralImpSpitFireball",45,0,random(-3,3),CMF_OFFSETPITCH,random(-3,3));
	}
	1ROS A 5 A_FaceTarget();
	TNT1 A 0;
	Goto See;
  Pain:
    1ROO H 2;
    1ROO H 2 A_Pain;
	TNT1 A 0 { pm_irecount -= random(13,17); }
    Goto See;
  Death:
   TNT1 A 0
    {
	   A_StopSound(19);
	   A_StopSound(20);
       A_RemoveChildren(1,RMVF_EVERYTHING);
    }
    2ROH K 1;
    2ROH K 8 PM_Scream();
    2ROH L 8;
    2ROH M 6;
    2ROH N 6 A_NoBlocking;
	TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
    2ROH O -1;
    Stop;
  XDeath:
	TNT1 A 0
    {
	   A_StopSound(19);
	   A_StopSound(20);
	}
	TNT1 A 0 A_StopSound(555);
    TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING);
    2RO4 A 5 PM_Gib();
    2RO4 B 5 A_XScream;
    2RO4 C 5;
    2RO4 D 5 A_NoBlocking;
    2RO4 E -1;
    Stop;
  Raise:
    D3IM ML 8;
    D3IM KJI 6;
    Goto Look;
	}
}

Class PM_FeralFireEffect : Actor
{
  Default
  {
  +NOINTERACTION
  +CLIENTSIDEONLY
  +ROLLSPRITE
  +FORCEXYBILLBOARD
  Renderstyle "Add";
  Scale 0.2;
  Translation "0:255=%[0,0,0]:[2.0,1.2,0.3]";
  +Bright
  }
  States
  {
  Spawn:
	TNT1 A 0;
	TNT1 A 0 A_SetRoll(random(0,360));
	Go:
	TNT1 A 0 A_SetScale(Scale.X-0.01);
	FIG1 A 1 A_FadeOut(0.08);
	Loop;
	}
}

/*Class FeralBallTrail : Actor
{
  Default
  {
  Translation "0:255=%[0,0,0]:[2.0,1.2,0.3]";
  Scale 0.75;
  +NOINTERACTION
  +CLIENTSIDEONLY
  +NOBLOCKMAP
  +THRUACTORS
  +DONTSPLASH
  +NOCLIP
  RenderStyle "Add";
  }
States
{
Spawn:
	TNT1 A 2;
	Start:
	TNT1 A 0 A_CheckSight("Disappear");
    NDEB ABC 1
	{
	A_SetScale(Scale.X-0.07);
	A_FadeOut(0.25);
	}
    Goto Start;
	Disappear:
	TNT1 A 0;
	Stop;
    }
}*/

Class PM_FeralImpSpitFireball : PM_CharredBall
{
    Default
	{
	DamageFunction (10);
	+NOGRAVITY
	-LOWGRAVITY
	//+SEEKERMISSILE
	DamageType "Fire";
	seesound "GenericSmallFireball";
	Speed 15;
	FastSpeed 20;
	Scale 0.93;
	}
	states
  	{
  Spawn:
    TNT1 A 1
	{
	//A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
	A_SpawnItemEx("PM_FeralSpitFlameTrails",0,0,0,0,0,0,0,128,0);
	}
	loop;
  Death:
	TNT1 A 0 A_SpawnItemEx("PM_OrangeEmbersSpawn");
	TNT1 A 0 A_SpawnItemEx("PM_OFireBallExplode");
	TNT1 A 0 A_SpawnItemEx("PM_FireballExplosionFlames");
    AFX5 D 2 bright Light("AvatarFireball4");
    AFX5 EFGH 2 Light("AvatarFireball4") bright;
    stop;
  }
}

Class PM_FeralImpBall : DoomImpBall
{
    Default
    {
	DamageFunction 14;
	+NOGRAVITY
	-LOWGRAVITY
	SeeSound "FeralImp/Ball";
	Decal "PM_OrangeFireScorch";
	FastSpeed 23;
	DamageType "Fire";
	Speed 12;
	Scale 0.85;
	Translation "0:255=%[0,0,0]:[2.0,1.2,0.3]";
    }
  
    override void PostBeginPlay()
    {
     Super.PostBeginPlay();
     tex[0] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
     tex[1] = TexMan.CheckForTexture("LEYSO0",TexMan.Type_Sprite);
     tex[2] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
     tex[3] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
	 tex[4] = TexMan.CheckForTexture("GNSML0",TexMan.Type_Sprite);
    }
  
    TextureID tex[5];
  
	States
  	{
    Spawn:
     NDEB AABBCC 1 bright Light("AvatarFireball3")
	{
	  A_SpawnItemEx("PM_FeralFlameTrails",random(-1,1),random(-1,1),0,0,0,0,0,128,0);
	  A_SpawnParticleEx("",tex[4],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 95,size: 30.0,xoff: -10,startalphaf: 0.4
	  ,fadestepf: 1,startroll: random(-180,180));
	  A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,70),size: frandom(0.2,10),xoff: random(-10,5),yoff: random(-5,5),zoff: random(-5,5),velx: frandom(-0.6,0.6),vely: frandom(-0.6,0.6),velz: frandom(-0.6,0.6),startalphaf: 1.0
	  ,fadestepf: 1,sizestep: -0.2,startroll: random(-180,180));
	  A_SpawnParticleEx("",tex[3],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 1,size: 80.0,xoff: -5,startalphaf: 0.6);
	}
	Loop;
  Death:
	TNT1 AAAAAAAAAAAAAAA 0 { A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(5,20),size: random(15,20),xoff: -7,velx: frandom(-5,5),vely: frandom(-5,5),velz: frandom(-5,5),startalphaf: 0.8
	,fadestepf: 1,startroll: random(-180,180)); }
	TNT1 A 0 A_SpawnItemEx("PM_FireballExplosionFlames");
    NDEB D 2 bright Light("AvatarFireball4");
    FIR3 CDEFGHIJKLMNOP 2 Light("AvatarFireball4") Bright;
    stop;
  }
}

Class PM_FeralImpIreball : PM_FeralImpBall
{
  Default
  {
   Speed 24;
   FastSpeed 28;
   DamageFunction 18;
   SeeSound "";
  }
  
  States
  {
  Spawn:
    TNT1 A 0;
    TNT1 A 0
	{
	  A_SpawnParticleEx("#d2d2d2",tex[0],style: STYLE_Normal,flags: SPF_RELATIVE|SPF_ROLL,lifetime: random(10,250),size: frandom(10,50),xoff: -25,velx: frandom(0,2),vely: frandom(-0.4,0.4),velz: frandom(-0.4,0.7),startalphaf: frandom(0.1,0.5)
	  ,fadestepf: 1,sizestep: frandom(0.0,1.4),startroll: random(-180,180),rollvel: frandom(-1,1));
	  A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: random(1,4),size: frandom(6,180),xoff: -22,startalphaf: 0.9,sizestep: frandom(0.0,3),startroll: random(-180,180));
    }
    TNT1 AAAAA 0
	{
	  A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,40),size: frandom(0.1,7),xoff: -22,velx: frandom(1,5),vely: frandom(-0.5,0.5),velz: frandom(-0.5,0.5),accelz: frandom(-0.0,-0.3),startalphaf: 1.0
	  ,fadestepf: 1,sizestep: -0.2,startroll: random(-180,180));
	  A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,40),size: frandom(0.1,7),xoff: -22,velx: frandom(1,4),vely: frandom(-0.5,0.5),velz: frandom(-0.5,0.5),accelz: frandom(-0.0,-0.2),startalphaf: 1.0
	  ,fadestepf: 1,sizestep: -0.2,startroll: random(-180,180));
	}
  Go:
    NDEB AABBCC 1 bright Light("AvatarFireball3")
	{
	  A_SpawnItemEx("PM_FeralFlameTrails",random(-1,1),random(-1,1),0,0,0,0,0,128,0);
	  A_SpawnParticleEx("ffb521",tex[4],style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 45,size: 55.0,xoff: -10,startalphaf: 0.6
	  ,fadestepf: 1,startroll: random(-180,180));
	  A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,40),size: frandom(0.2,10),xoff: random(-10,5),yoff: random(-5,5),zoff: random(-5,5),velx: frandom(-0.6,0.6),vely: frandom(-0.6,0.6),velz: frandom(-0.6,0.6),startalphaf: 1.0
	  ,fadestepf: 1,sizestep: -0.2,startroll: random(-180,180));
	  A_SpawnParticleEx("",tex[3],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 1,size: 80.0,xoff: -5,startalphaf: 0.6);
	}
	Loop;
  Death:
	TNT1 A 0
	{
	 for(int i=random(10,15);i>0;i--)
	  A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(5,20),size: random(15,20),xoff: -7,velx: frandom(-5,5),vely: frandom(-5,5),velz: frandom(-5,5),startalphaf: 0.8
	  ,fadestepf: 1,startroll: random(-180,180));
	  A_SpawnParticleEx("",tex[3],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 30,size: 40.0,xoff: -5,startalphaf: 0.6,sizestep: 3,startroll: random(-180,180));
	}
	TNT1 A 0 A_SpawnItemEx("PM_FireballExplosionFlames");
    NDEB D 2 bright Light("AvatarFireball4");
    FIR3 CDEFGHIJKLMNOP 2 Light("AvatarFireball4") Bright;
    Stop;
  }
}

Class PM_FeralLunge : Actor
{
  Default
  {
   Height 15;
   Radius 24;
   Speed 15;
   DamageFunction (10);
   DamageType "Melee";
   DeathSound "";
   SeeSound "";
   Projectile;
  }
States
{
Spawn:
	TNT1 A 2;
    Stop;
    }
}