Class PM_Host : PMPestilenceMonsterBase
{
    Default
	{
	//$Category "PM_Monsters/Imps"
    Obituary "%o was poisoned by a Host.";
    HitObituary "A Host had %o for lunch.";
    Health 125;
    Radius 20;
    Height 56;
    Mass 120;
    Speed 8;
	Bloodcolor "DarkGreen";
	BloodType "PM_PestilenceBloodBase";
	Species "Imp";
    PainChance 180;
    SeeSound "Host/Sight";
    PainSound "Host/Pain";
    DeathSound "Host/Death";
    ActiveSound "Host/Active";
	Tag "Host";
    MONSTER;
    +FLOORCLIP
    +DONTHARMCLASS
	+DONTHARMSPECIES
	+MISSILEMORE
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	}
 
  States
  {
  Spawn:
    HOS1 AB 10 PM_Look();
    Loop;
  Look:
	TNT1 A 0 A_SetSpeed(8);
    HOS1 AA 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	HOS1 BBCC 3 PM_LookingForPlayer();
	TNT1 A 0 A_JumpIfTargetInLOS("Spotted",180,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("Imp/Step",4);
	HOS1 DD 3 PM_LookingForPlayer();
    Goto Look+1;
  Spotted:
    TNT1 A 0 A_Jump(256,"Roar","RoarNoAnim","SpotMissile");
	Goto See;
  RoarNoAnim:
    TNT1 A 0 PM_AlertSound();
  SpotMissile:
    TNT1 A 0 PM_AlertSound();
    HOS1 EEE 5 A_FaceTarget;
	Goto Missile;
  SeeStopped:
	TNT1 A 0 A_StopSound(CHAN_6);
  See:
	TNT1 A 0 { bNOPAIN = false; }
	TNT1 A 0 A_SetSpeed(8);
    HOS1 AA 3 A_Chase();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	HOS1 BBCC 3 A_Chase();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	HOS1 DD 3 A_Chase();
	HOS1 A 0 A_JumpIfCloser(330, "ChargeSee");
    Loop;
  Melee:
    HOS1 E 8 A_FaceTarget;
    HOS1 F 1 A_StartSound("monster/vulswg");
    HOS1 F 7 A_FaceTarget;
    HOS1 G 6 A_CustomMeleeAttack(4*Random(1, 8), "monster/vulhit");
    Goto See;
  Missile:
	TNT1 A 0 A_Jump(20,"Roar");
	AGUR A 0 A_Jump(70, "Missile2","Missile4");
    HOS1 EEF 5 Light("HostShot") A_FaceTarget();
	HOS1 G 6 Light("HostShot2")
	{
	for(int a=0;a<3;a++)
	A_SpawnProjectile("PM_HostPoisonShotSmall",37,0,random(-5,5),CMF_OFFSETPITCH,frandom(-1,1));
	}
    Goto See;
   Roar:
	HOS1 FFFFFF 3 A_FaceTarget;
	TNT1 A 0 A_StartSound("Host/Sight");
	HOS1 EEEEEEEEE 3 A_FaceTarget;
	HOS1 FFFFF 3 A_FaceTarget;
	Goto See;
   Missile2:
	HOS1 A 0 A_Jump(20,"Missile4");
    HOS1 EEEF 5 Light("HostShot") A_FaceTarget;
	HOS1 G 6 Light("HostShot2") A_SpawnProjectile("PM_HostPoisonShot", 37, 0, 0, 0, 0);
	Goto See;
  ChargeSee:
	TNT1 A 0 A_JumpIfTargetInLOS("Charge",360,JLOSF_DEADNOJUMP);
	Goto See;
  Charge:
	TNT1 A 0 A_Jump(70,"See");
	TNT1 A 0 A_JumpIfHigherOrLower("See","See", 10,10);
	TNT1 A 0 A_StartSound("Host/Charge",CHAN_6);
  Missile3:
    TNT1 A 0 A_SetSpeed(8);
	TNT1 A 0 A_JumpIfCloser(130,"Puke");
	HOS1 AA 1 A_Chase(null,null);
	TNT1 A 0 A_SpawnProjectile("PM_HostDrool",43,0,random(5,-5),CMF_AIMDIRECTION);
	TNT1 A 0 A_StartSound("Imp/Step",4);
	HOS1 BB 1 A_Chase(null,null);
	TNT1 A 0 A_SpawnProjectile("PM_HostDrool",43,0,random(5,-5),CMF_AIMDIRECTION);
	HOS1 CC 1 A_Chase(null,null);
	TNT1 A 0 A_SpawnProjectile("PM_HostDrool",43,0,random(5,-5),CMF_AIMDIRECTION);
	TNT1 A 0 A_StartSound("Imp/Step",4);
	HOS1 DD 1 A_Chase(null,null);
	TNT1 A 0 A_JumpIfCloser(370,"Missile3");
    Goto See;
  Puke:
	HOS1 E 7 A_FaceTarget;
	HOS1 F 2 
	{
	A_FaceTarget();
	ThrustThingZ(0,15,0,1);
	A_Recoil(-8);
	}
	TNT1 A 0 A_StartSound("GenericBloodPuke",2);
	TNT1 A 0 A_StartSound("Afflicted/Puke");
	HOS1 FFFFFFFFFFFFFFFFFFFFF 1 Light("HostPuke")
	{
	A_SpawnProjectile("PM_HostSprayPuke",44,0,random(5,-5),CMF_AIMDIRECTION);
	}
	HOS1 A 6;
	TNT1 A 0 A_JumpIfTargetInLOS("Missile3",360,JLOSF_DEADNOJUMP);
	Goto See;
  Missile4:
	TNT1 A 0 { bNOPAIN = true; }
	TNT1 A 0 A_StartSound("Host/Active",CHAN_6);
	HOS1 EEEE 5 A_FaceTarget;
	TNT1 A 0 A_StartSound("GenericChargeSmall");
	HOS1 FFFFFFFFFFFF 2 Light("Feralfireball4")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_HostFlyFire",7,random(16,21),40,0,0,2);
	A_SpawnItemEx("PM_HostFlyFire",7,random(-16,-21),40,0,0,2);
	}
	TNT1 A 0 A_StartSound("HostFly");
	HOS1 FFFFFFFFFFFF 2 Light("Feralfireball4")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_HostFlyFire",7,random(16,21),40,0,0,2);
	A_SpawnItemEx("PM_HostFlyFire",7,random(-16,-21),40,0,0,2);
	}
	TNT1 A 0 A_StartSound("HostFlyLoop",CHAN_6,CHANF_LOOPING,1);
   MissileFlyAttack:
   TNT1 A 0 { bNOPAIN = false; }
	HOS1 F 2 Light("Feralfireball4")
	{
	A_FaceTarget();
	A_SpawnProjectile("PM_HostAttackFlies",40,20,0);
	A_SpawnItemEx("PM_HostFlyFire",7,random(16,21),40,0,0,2);
	A_SpawnItemEx("PM_HostFlyFire",7,random(-16,-21),40,0,0,2);
	}
	HOS1 F 2 Light("Feralfireball4")
	{
	A_FaceTarget();
	A_SpawnProjectile("PM_HostAttackFlies",40,-20,0);
	A_SpawnItemEx("PM_HostFlyFire",7,random(16,21),40,0,0,2);
	A_SpawnItemEx("PM_HostFlyFire",7,random(-16,-21),40,0,0,2);
	}
	TNT1 A 0 A_Jump(5,"SeeStopped");
	TNT1 A 0 A_MonsterReFire(150,"SeeStopped");
	Goto MissileFlyAttack;
  Pain:
	TNT1 A 0 A_StopSound(CHAN_6);
    HOS1 H 2;
    HOS1 H 2 A_Pain;
    goto See;
  Death:
    HOS1 I 8 A_RemoveChildren(1,RMVF_EVERYTHING);
    HOS1 J 8 PM_Scream();
    HOS1 KL 6;
    HOS1 M 6 A_NoBlocking;
	TNT1 AAAAAAAA 0 A_SpawnItemEx("PM_HostSwarmFly",0,0,5);
	TNT1 A 0 A_SpawnItemEx("PM_ActiveSwarm",0,0,7,0,0,0,0,0,200);
	TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
	HOS1 NNNNNNNNNN 30
	{
	  A_SpawnParticleEx("252D00",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(80,200),size: frandom(15,45),velx: frandom(-0.1,0.1),vely: frandom(-0.1,0.1),velz: frandom(1,2),accelx: frandom(-0.02,0.02),accely: frandom(-0.02,0.02),accelz: frandom(-0.03,0.01),startalphaf: frandom(0.3,0.6),fadestepf: -0.001,sizestep: frandom(0.2,4),startroll: random(-180,180),rollvel: random(-2,2));
	  A_SpawnItemEx("PM_HostSmokeCloud2",0,0,1,random(-2,2),random(-2,2),random(0,1),0,0);
	}
    HOS1 N -1;
    Stop;
  XDeath:
    TNT1 A 0 A_StopSound(555);
	  TNT1 AAA 0 A_SpawnItemEx("PM_HostSmokeCloud2",0,0,1,random(-3,3),random(-3,3),random(0,2),0,0);
	  TNT1 A 0 PM_Gib();
    HOS1 O 5 A_RemoveChildren(1,RMVF_EVERYTHING);
    HOS1 P 5 A_XScream;
    HOS1 Q 5;
    HOS1 R 5 A_NoBlocking;
    HOS1 STU 5;
    HOS1 V -1;
    Stop;
  Raise:
    HOS1 ML 8;
    HOS1 KJI 6;
    Goto Look;
  }
}

Class PM_HostPoisonSplash : Actor
{
    Default
	{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+ROLLSPRITE
	RenderStyle "Add";
	Translation "0:255=%[0,0,0]:[0.8,1.4,0.0]";
	Scale 1.7;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"Splash1","Splash2","Splash3");
	Splash1:
		TNT1 A 0 A_SetRoll(random(0,360));
		X057 ABCDEF 2 Bright A_FadeOut(0.05);
		X057 GHIJK 1 Bright A_FadeOut(0.05);
		X057 LMNOPQRS 1 Bright A_FadeOut(0.03);
		Wait;
	Splash2:
		TNT1 A 0 A_SetRoll(random(0,360));
		X058 ABCDEF 2 Bright A_FadeOut(0.05);
		X058 GHIJK 1 Bright A_FadeOut(0.05);
		X058 LMNOPQR 1 Bright A_FadeOut(0.03);
		Wait;
	Splash3:
		TNT1 A 0 A_SetRoll(random(0,360));
		X059 ABCD 2 Bright A_FadeOut(0.03);
		X059 EFGHIJ 1 Bright A_FadeOut(0.04);
		X059 KLM 1 Bright A_FadeOut(0.05);
		Wait;
	}
}

Class PM_HostPoisonSplashSmall : PM_HostPoisonSplash
{ Default { Scale 0.75; } }

Class PM_HostAttackFlies : DoomImpBall
{
  Default
  {
  Height 2;
  Radius 2;
  DamageFunction 2;
  Scale 3;
  Speed 15;
  SeeSound "Flying/Fly";
  DeathSound "";
  RenderStyle "Normal";
  Bouncetype "Doom";
  BounceFactor 1;
  BounceCount 3;
  +RANDOMIZE
  }
  States
  {
  Spawn:
	SFLY ABCABC 1;
	TNT1 A 0 A_ChangeVelocity(0,random(-1,1),random(-1,1));
    Loop;
  Death:
    Stop;
  }
}

Class PM_HostDrool : Actor
{
  Default
  {
  Damage 0;
  Height 1;
  Radius 1;
  Speed 8;
  Mass 1;
  PROJECTILE;
  +FORCEXYBILLBOARD
  +CLIENTSIDEONLY
  -NOGRAVITY
  Scale 0.05;
  RenderStyle "Add";
  }
  States
  {
  Spawn:
  TNT1 A 0 A_Jump(255,"One","Two","Three","Four");
  Loop;
  One:
	FBLG A 2 BRIGHT;
	Loop;
  Two:
	FBLG B 2 BRIGHT;
	Loop;
  Three:
	FBLG C 2 BRIGHT;
	Loop;
  Four:
	FBLG D 2 BRIGHT;
	Loop;
  Death:
    Stop;
  }
}

Class PM_HostSprayPuke : Actor
{
  Default
  {
  DamageFunction 3;
  Height 1;
  Radius 1;
  Speed 15;
  Mass 1;
  +FORCEXYBILLBOARD
  Scale 0.20;
  Gravity 0.5;
  RenderStyle "Add";
  PROJECTILE;
  -NOGRAVITY
  }
  States
  {
  Spawn:
  TNT1 A 0 A_Jump(255,"One","Two","Three","Four");
  Loop;
  One:
	FBLG A 2 BRIGHT A_SetScale(Scale.X+0.05);
	Loop;
  Two:
	FBLG B 2 BRIGHT A_SetScale(Scale.X+0.05);
	Loop;
  Three:
	FBLG C 2 BRIGHT A_SetScale(Scale.X+0.05);
	Loop;
  Four:
	FBLG D 2 BRIGHT A_SetScale(Scale.X+0.05);
	Loop;
  Death:
	BDRG BCDEFGH 3 Bright Light("HostPuke");
    Stop;
  }
}

Class PM_HostGasTrail : Actor
{
  Default
  {
  +NOINTERACTION
  Renderstyle "Translucent";
  Alpha 0.4;
  Scale 0.32;
  }
  States
  {
  Spawn:
	GSM1 A 1 A_FadeOut(0.03);
	Wait;
	}
}

Class PM_HostSwarmFly : Actor
{
  
  Default
  {
  Radius 1;
  Height 1;
  Speed 1;
  PROJECTILE;
  +NOCLIP
  ReactionTime 25;
  Scale 1;
  }
  States
  {
  Spawn:
   SFLY A 1 ThrustThingZ(0, random (-1, 1), random (1, 0), 1);
   TNT1 A 0 A_ChangeVelocity(frandom(-2,2),frandom(-1,1),frandom(-2,2),1);
   SFLY B 1 ThrustThingZ(0, random (-1, 1), random (1, 0), 1);
   TNT1 A 0 A_ChangeVelocity(frandom(-2,2),frandom(-1,1),frandom(-2,2),1);
   SFLY A 0 A_CountDown;
   Loop;
   Death:
   SFLY A 1;
   Stop;
  }
}

Class PM_HostPoisonShot : Actor
{
    Default
	{
    Radius 8;
    Height 16;
    Speed 15;
    DamageFunction 18;
    PoisonDamage 32;
    RENDERSTYLE "Add";
    ALPHA 0.8;
	Scale 1.3;
    Seesound "HostFire";
    DeathSound "BeholderEyeExp";
    PROJECTILE;
    +THRUGHOST
	}
  States
  {
  Spawn:
    FVUL AAABBB 1 Bright Light("HostShot")
	{
	A_SpawnParticle("A6 FE 6C",SPF_RELATIVE,37,7,0,random(-3,3),random(-3,3),random(-3,3),0,0,0,0,0,0,1,-1,0);
	A_SpawnItemEx("PM_HostGasTrail", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE, 0);
	}
    loop;
  Death:
	TNT1 AAAAA 0 A_SpawnItemEx("PM_HostPoisonSplash",random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),frandom(-1,1));
	TNT1 AAAAA 0 A_SpawnItemEx("PM_HostBallSplatter",random(-3,3),random(-3,3),random(-3,3),random(-4,4),random(-4,4),frandom(-4,5));
	TNT1 A 0 A_SpawnItemEx("PM_HostSmokeCloud2",0,0,1,random(-1,1),random(-1,1),random(-1,1),0,0);
    FVUL CDEF 4 Bright Light("HostShot2");
    stop;
  }
}

Class PM_HostPoisonShotSmall : Actor
{
    Default
	{
    Radius 8;
    Height 16;
    Speed 18;
    DamageFunction 8;
    RENDERSTYLE "Add";
    ALPHA 0.8;
	Scale 0.75;
    Seesound "HostFire";
    DeathSound "TaintedBloodExp";
    PROJECTILE;
    +THRUGHOST
	}
  States
  {
  Spawn:
    FVUL AAABBB 1 Bright Light("HostShot2")
	{
	A_SpawnParticle("a6fe6c",SPF_RELATIVE,random(12,18),random(7,12),0,random(-3,3),random(-3,3),random(-3,3),startalphaf:1,fadestepf:-1,sizestep:0);
	A_SpawnParticle("59bd2e",SPF_RELATIVE,random(10,15),random(7,12),0,random(-3,3),random(-3,3),random(-3,3),startalphaf:1,fadestepf:-1,sizestep:0);
	}
    loop;
  Death:
	TNT1 AA 0 A_SpawnItemEx("PM_HostPoisonSplashSmall",random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),frandom(-1,1));
	TNT1 AAA 0 A_SpawnItemEx("PM_HostBallSplatter",random(-3,3),random(-3,3),random(-3,3),random(-4,4),random(-4,4),frandom(-4,5));
    FVUL CDEF 4 Bright Light("HostShot");
    stop;
  }
}

Class PM_HostBallSplatter : Actor
{
  Default
  {
  Damage 0;
  Height 1;
  Radius 1;
  Speed 8;
  Mass 1;
  BounceType "Doom";
  BounceFactor 0.2;
  +FORCEXYBILLBOARD
  +CLIENTSIDEONLY
  Scale 0.13;
  Gravity 0.5;
  RenderStyle "Add";
  Projectile;
  -NOGRAVITY
  }
  States
  {
  Spawn:
  TNT1 A 0 A_Jump(255,"One","Two","Three","Four");
  Loop;
  One:
	FBLG AAAAAAAA 2 BRIGHT
	{
	A_FadeOut(0.002);
	A_SetScale(Scale.X-0.003);
	}
	Wait;
  Two:
	FBLG BBBBBBBB 2 BRIGHT
	{
	A_FadeOut(0.002);
	A_SetScale(Scale.X-0.003);
	}
	Wait;
  Three:
	FBLG CCCCCCCC 2 BRIGHT
	{
	A_FadeOut(0.002);
	A_SetScale(Scale.X-0.003);
	}
	Wait;
  Four:
	FBLG DDDDDDDD 2 BRIGHT
	{
	A_FadeOut(0.002);
	A_SetScale(Scale.X-0.003);
	}
	Wait;
  Death:
    Stop;
  }
}

Class PM_HostBarbTrail : Actor
{   
    Default
	{
    Radius 0;
    Height 1;
    RenderStyle "Add";
    Alpha 0.85;
	Scale 1.3;
    Projectile;
	}
  States
  {
  Spawn:
    TNT1 A 2 Bright;
    SSFX ABCDEFG 2 Bright;
    Stop;
  }
}

Class PM_HostSmokeCloud : Actor
{
  Default
  {
  Radius 2;
  Height 2;
  +DONTTHRUST
  +NOBLOCKMAP
  +NOGRAVITY
  +DROPOFF
  +NODAMAGETHRUST
  +DONTSPLASH
  +NOTELEPORT
  +SKYEXPLODE
  +FORCEXYBILLBOARD
  +FOILINVUL
  +CANBLAST
  +RIPPER
  +THRUACTORS
  +BLOODLESSIMPACT
  +BLOCKEDBYSOLIDACTORS
  +FORCERADIUSDMG
  +DOOMBOUNCE
  +ROLLSPRITE
  BounceFactor 0.3;
  WallBounceFactor 0.3;
  RenderStyle "Translucent";
  Alpha 0.93;
  Scale 1.5;
  DeathSound "RevGhoul/cloud";
  }
  states
  {
  Spawn:
	TNT1 A 1;
	TNT1 A 0 A_SetRoll(random(0,360));
    GSM1 A 10 A_SetTranslucent(0.2);
	GSM1 A 10 A_SetTranslucent(0.4);
	GSM1 A 10 A_SetTranslucent(0.8);
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	GSM1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 10
	{
	A_Explode(Random(1,5),68);
	A_FadeOut(0.02);
	}
  Death:
	TNT1 A 1;
	Stop;
  }
}

Class PM_HostSmokeCloud2 : PM_HostSmokeCloud
{
  Default
  {
    BounceFactor 0.2;
    WallBounceFactor 0.2;
    RenderStyle "Normal";
    Alpha 0.3;
    Scale 1.2;
  }
  States
  {
  Spawn:
	TNT1 AAAAAAAAA random(1,8) { A_SpawnParticleEx("252D00",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(80,200),size: frandom(15,45),velx: frandom(-0.2,0.2),vely: frandom(-0.2,0.2),velz: frandom(-0.2,0.2),startalphaf: frandom(0.1,0.3),fadestepf: -0.001,sizestep: frandom(0.2,2),startroll: random(-180,180),rollvel: random(-2,2)); }
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA random(4,20)
	{
	  A_SpawnParticleEx("252D00",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(400,900),size: frandom(25,65),xoff: random(-20,20),yoff: random(-20,20),zoff: random(-20,20),velx: frandom(-0.2,0.2),vely: frandom(-0.2,0.2),velz: frandom(-0.2,0.2),startalphaf: frandom(0.1,0.6),fadestepf: -1,sizestep: frandom(0.1,1),startroll: random(-180,180),rollvel: frandom(-1,1));
	  A_Explode(Random(3,5),48);
	  A_FadeOut(0.02);
	  A_SetScale(scale.x+0.01);
	}
  Death:
	TNT1 A 1;
	Stop;
  }
}