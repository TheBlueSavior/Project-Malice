Class PM_Exalted : PMPrideMonsterBase
{
    actor cast;
	bool spawned;
	int tplimit;
	int exaburst;
    Default
	{
	PMMonsterBase.CodexLore
	"\cgHumans. Demons. All the same. Fools who lust for the simplest manifestations of power. Cretins.
	
	\cgWE wield the power of the light at our fingertips. It is high time that we show them the full extent of our capabilities.";
	PMMonsterBase.ForgetThreshold 9999; //[NERU] putting this in for now
    Health 1000;
    Radius 22;
    Height 64;
    Speed 14;
	Scale 1.2;
    PainChance 16;
    Mass 1500;
    MONSTER;
	Bloodtype "PM_NormalBloodBase";
    +FLOORCLIP
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+FLATSPRITE
	+THRUACTORS
	+MISSILEMORE
	+CANTSEEK
	-SHOOTABLE
	+DONTTHRUST
	Species "Nobles";
    SeeSound "Exalt/Sight";
    PainSound "Exalt/Pain";
    DeathSound "Exalt/Death";
    ActiveSound "Exalt/Active";
    MeleeSound "baron/melee";
	RenderStyle "AddShaded";
	StencilColor "Red";
	Tag "Exalted";
    Obituary "%o was cast down from ascension by an Exalted.";
    MeleeDamage 8;
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_PrideTeleportFog";
	PMMonsterBase.RealHeight 70;
	}
	
	override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 tex[0] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
	 tex[3] = TexMan.CheckForTexture("REDLA0",TexMan.Type_Sprite);
	 tex[4] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	 tex[5] = TexMan.CheckForTexture("SPE1A0",TexMan.Type_Sprite);
	}
	
	TextureID tex[6];
	
	void PM_ExaCast(class<actor> effect)
	{
	 cast = Spawn(effect,pos);
	 cast.master = self;
	}
	
	void PM_ExaltedVoice(sound whattoplay = "None", bool force = false)
	{
	 if(CountInv("PM_ExaltVoiceCD") && force == false) return;
	 if(whattoplay == "None") A_StartSound("Exalt/Voice",16,attenuation:0.3);
	 else A_StartSound(whattoplay,16,attenuation:0.3);
	 A_GiveInventory("PM_ExaltVoiceCD");
	}
	
	void PM_ExaTPStart() { bTHRUACTORS = true; bSHOOTABLE = false; bCANTSEEK = true; bDONTTHRUST = true; }
	void PM_ExaTPEnd() { bTHRUACTORS = false; bSHOOTABLE = true; bCANTSEEK = false; bDONTTHRUST = false; }
	
	void PM_ExaBurstSpread()
	{
	  switch (exaburst)
	  {
		case 0: A_SpawnProjectile("PM_ExaltedMagicShot",65,0,0); break;
		case 1: for (int i = 1; i > -2; i -= 2) A_SpawnProjectile("PM_ExaltedMagicShot",65,0,i*27); break;
		case 2: for (int i = 1; i > -2; i -= 2) { A_SpawnProjectile("PM_ExaltedMagicShot",65,0,i*25); A_SpawnProjectile("PM_ExaltedMagicShot",65,0,i*33); } break;
		case 3: A_SpawnProjectile("PM_ExaltedMagicShot",65,0,0); for (int i = 1; i > -2; i -= 2) { A_SpawnProjectile("PM_ExaltedMagicShot",65,0,i*25); A_SpawnProjectile("PM_ExaltedMagicShot",65,0,i*55); } break;
	  }
	}
	
    States
    {
    Spawn:
	    EXAL E 0 NoDelay;
        TNT1 A 10 
        { 
        	if(spawned)
            {
		     SetStateLabel("LookSpawned");
		     return;
            }
		 PM_Look(); 
	    }
        Wait;
	LookSpawned:
	    EXAL A 10 PM_Look();
		Wait;
    Look:
		TNT1 A 0 { 
		if(spawned)
		 SetStateLabel("See");
		A_SetScale(1.1);
		A_StartSound("PMGazer/Hover",100,CHANF_LOOPING);
		A_StartSound("Draugr/BuffLoop",101,CHANF_LOOPING);
		alpha = 0.0; 
		A_SoundVolume(100,alpha);
		A_SoundVolume(101,alpha);
		}
	Looking:
	    TNT1 A 0 { for(int i=random(15,40);i>0;i--) A_Wander(); } 
        TNT1 AA 10 PM_LookingForPlayer();
		TNT1 A 0 A_Jump(15,"Spotted");
        Loop;
    Spotted:
        EXAF A 1 Bright
		{
		 if(alpha >= 1)
		 SetStateLabel("Appear");
		 alpha += 0.00625;
		 A_SoundVolume(100,alpha);
		 A_SoundVolume(101,alpha);
		 A_SetAngle(angle+0.625);
		 if (!(getAge() % 5)) A_QuakeEx(2,2,2,10,0,900,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,rollintensity:0.3,rollwave:0.3);
		 if (!random(0, 15) && !(GetAge() % random(1, 4))) A_SpawnItemEx("PM_ExaltedLightRay", frandom(-350, 350), frandom(-350, 350), flags: SXF_NOCHECKPOSITION);
		 if (!(getAge() % 2)) A_SpawnItemEx("PM_ExaltedBeacon",frandom(-45,45),frandom(-50,50),0,flags:SXF_TRANSFERALPHA);
		}
		Loop;
	Appear:
	    EXAL E 5 Bright
		{
		spawned = true; bFLATSPRITE = false; bTHRUACTORS = false; bSHOOTABLE = true; bCANTSEEK = false; bDONTTHRUST = false;
		A_SetAngle(0);
		A_StopSound(100);
		A_StopSound(101);
		A_SetScale(1.15);
		A_SpawnItemEx("PM_ExaltedIntroBang",flags:SXF_NOCHECKPOSITION);
		A_SetRenderStyle(1.0,STYLE_Normal);
		A_StartSound("Director/BigPlasmaExp",91,volume:0.45,attenuation:0.2);
		A_StartSound("PMGazer/Sight",92,CHANF_DEFAULT,1,pitch:0.7);
		A_StartSound("PMGazer/Sight",93,CHANF_DEFAULT,1,pitch:0.9);
		A_StartSound("PMGazer/Sight",94,CHANF_DEFAULT,1,pitch:1.2);
		A_SpawnParticleEx("ff2424", tex[5], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: 20, size: 150, xoff: 10, zoff: 45, startalphaf: 1.0, fadestepf: -1, sizestep: 80, startroll: random(-180,180), rollvel: frandom(-8, 8));
		for(int i=3;i>0;i--) A_SpawnParticleEx("", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: 45, size: 850, xoff: 10, zoff: 45, startalphaf: 1.0, fadestepf: -1, sizestep: frandom(18, -18), startroll: random(-180,180), rollvel: frandom(-2, 2));
		for(int i=10;i>0;i--) A_SpawnParticleEx("AF0000", tex[1], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE, lifetime: random(50,100), size: 10.0, xoff: random(100,-100), yoff: random(100,-100), zoff: random(0,30), velx: frandom(-1,1), vely: frandom(-1,1), accelx: frandom(-0.05,0.05), accely: frandom(-0.05,0.05), startalphaf: frandom(0.2,0.9), fadestepf: -1, sizestep: 4, startroll: random(-180,180), rollvel: frandom(-1,1));
		for(int i=10;i>0;i--) A_SpawnParticleEx("", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: int(ceil(frandom(10,50))), size: random(15,30), xoff: frandom(-45,45), yoff: frandom(-45,45), zoff: random(0,32), velx: frandom(-5,5), vely: frandom(-5,5), velz: frandom(0,5), startalphaf: 1.0, fadestepf: -1, startroll: random(-180,180), rollvel: frandom(-5,5));
		for(int i=10;i>0;i--) A_SpawnParticleEx("", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: random(1,10), size: random(1,100), xoff: frandom(-60,60), yoff: frandom(-60,60), zoff: frandom(0,50), velx: random(-6,6), vely: random(-6,6), velz: random(-6,6), startalphaf: frandom(0.8,0.95), startroll: random(-180,180), rollvel: frandom(-30,30));
		}
		EXAL EEEEEE 5 Bright A_FaceTarget();
		TNT1 A 0 PM_AlertSound();
		EXAL EEEEEEEE 5 Bright A_FaceTarget();
		Goto See;
    See:
        EXAL AAAABBBB 2 A_Chase();
		TNT1 A 0 A_Quake(1, 4, 0, 312);
		TNT1 A 0 { bSHOOTABLE = true; }
		TNT1 A 0 A_StartSound("hoofstep",9);
		EXAL CCCCDDDD 2 A_Chase();
		TNT1 A 0 A_Quake(1, 4, 0, 312);
		TNT1 A 0 A_StartSound("hoofstep",9);
		TNT1 A 0 A_Jump(56,"PhaseJump");
        Loop;
	PhaseJump:
	    EXAL E 10 Bright A_FaceTarget();
	    EXAL E 0 {
		  A_StopSound(16);
		  A_StartSound("Exalt/TPStart",20); 
		  PM_ExaTPStart();
		  for (int i = -15; i <= 15; i+= 10)
		   { A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,i,0,0,flags:SXF_TRANSFERSTENCILCOL|SXF_TRANSFERALPHA|SXF_TRANSFERSPRITEFRAME); }
		  for(int i=3;i>0;i--)
		   { A_SpawnParticleEx("", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 30, size: 500, zoff: 75, startalphaf: 0.95, fadestepf: -1, sizestep: -35, startroll: random(-180,180),rollvel: frandom(-10,10), rollacc: frandom(-1,1) ); }
		}
		TNT1 A 35;
		TNT1 A 0 { 
	    for(int i=random(40,60);i>0;i--)
		A_Wander();
		}
	TNT1 AAAAAAAAAAAAAAAAAAAAAA random(1,3) Light("ShadowBall3")
	{
		A_SpawnParticleEx(   
		"", tex[2], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: 50, size: frandom(25,35), xoff: frandom(-65.5,65.5), yoff: frandom(-65.5,65.5), zoff: frandom(15,90),
		velx: frandom(-1,1), vely: frandom(-1,1), velz: frandom(-1,1), startalphaf: 1, fadestepf: -1, sizestep: -2 );
		A_SpawnParticleEx(   
		"cc0000", TexMan.CheckForTexture("EXALE1"), style: STYLE_Stencil, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: 25, size: 175, xoff: frandom(-5.5,5.5), yoff: frandom(-5.5,5.5), zoff: 55, velx: frandom(-0.1,0.1), vely: frandom(-0.1,0.1),
		velz: frandom(-0.1,0.1), startalphaf: 0.25, fadestepf: -1, sizestep: -2, startroll: 180);
	}
    EXAL E random(10,20) Bright
	{
	    A_FaceTarget();
	    A_StartSound("Exalt/TPEnd",20); 
	    PM_ExaTPEnd();
		for(int i=3;i>0;i--)
		A_SpawnParticleEx(
		"", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: 30, size: 500, zoff: 75, startalphaf: 0.95, fadestepf: -1, sizestep: -35, startroll: random(-180,180), rollvel: frandom(-10,10), rollacc: frandom(-1,1));
		for (int i = -15; i <= 15; i+= 10)
		A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,i,0,0,flags:SXF_TRANSFERSTENCILCOL|SXF_TRANSFERALPHA|SXF_TRANSFERSPRITEFRAME);
	}
		TNT1 A 0 A_Jump(45,"BurstShot");
		Goto See;
	Missile:
	    TNT1 A 0 { 
		if(CountInv("PM_ExaGigalaserCD")) 
		  A_Jump(256,1);
		if(random(1,15) == 1)
		  SetStateLabel("GigalaserSigil");
		}
	    TNT1 A 0 A_Jump(45,"CultSummon");
	    TNT1 A 0 A_Jump(65,"BallBarrage","LaserSigil");
	BurstShot:
	    EXAL E 18 
		{
		 pm_attackloop = random(2,6);
		 A_FaceTarget(360,180);
		 A_SetAngle(angle+random(-15,15));
		 A_SetPitch(pitch+random(-5,5));
		 PM_ExaCast("PM_ExaltedCastReady");
		 A_StartSound("PM/SigilSpawn",17,pitch:0.85);
		 A_StartSound("PMGazer/Sight",18,pitch:1.3);
		}
	BurstLoop:
	   EXAL F 12 Bright
	   {
	    A_FaceTarget();
		A_StartSound("PM/RuneFill",pitch:frandom(0.85,1.45));
		PM_ExaBurstSpread();
	   }
	   EXAL F 5 
	   { 
	    if(pm_attackloop <= 0)
	     { SetStateLabel("MissileRedEnd"); }
		if(exaburst == 3)
		 { exaburst -= 4; }
	    pm_attackloop--; 
		exaburst++;
	   }
	   Loop;
	BallBarrage:
	    EXAL E 20 
		{
		 pm_attackloop = random(5,12);
		 A_FaceTarget(360,180);
		 A_SetAngle(angle+random(-15,15));
		 A_SetPitch(pitch+random(-10,10));
		 PM_ExaCast("PM_ExaltedCastReady");
		 A_StartSound("Exalt/CastAdd",20);
		 A_StartSound("PMGazer/Sight",18,pitch:1.3);
		} 
	BBarrageLoop:
	   EXAL FFFFFFFFFFFFFFFFFFF 1
	   {
	    A_FaceTarget(1.5,1.5);
	    A_SpawnProjectile("PM_AngelsHandBHBall",65,0,frandom(-5,5),CMF_OFFSETPITCH|CMF_AIMDIRECTION,pitch+frandom(-6,6)+1);
		if(random(0,4) == 1)
		 { A_SpawnProjectile("PM_BlashemerBHBall",65,0,frandom(-60,60),CMF_OFFSETPITCH|CMF_AIMDIRECTION,pitch+frandom(-30,30)+1); }
	   }
	   EXAL F 1 Light("ShadowBall3") 
	   {
	    if(pm_attackloop <= 0)
		 { SetStateLabel("SuperCastEnd"); }
	    A_FaceTarget(1.5,1.5);
	    A_SpawnProjectile("PM_ExaltedSuperBall",65,0,frandom(-50,50),CMF_OFFSETPITCH,frandom(-25,25));
		pm_attackloop--;
	   }
	   Loop;
	LaserSigil:
	    EXAL F 15
		{
		  A_FaceTarget();
		  PM_ExaCast("PM_ExaltedCastReady");
		  A_StartSound("PM/SigilSpawn",17,pitch:0.85);
		  A_StartSound("Exalt/CastAdd",18,pitch:1.3);
		}
	   EXAL E 10 Bright
	   {
	    A_FaceTarget();
		A_SpawnProjectile("PM_ExaltedLaserSigil",70,0,0,CMF_AIMDIRECTION|CMF_OFFSETPITCH,-60);
		A_SpawnProjectile("PM_ExaltedLaserSigil",70,0,-35,CMF_AIMDIRECTION|CMF_OFFSETPITCH,-40);
		A_SpawnProjectile("PM_ExaltedLaserSigil",70,0,35,CMF_AIMDIRECTION|CMF_OFFSETPITCH,-40);
		A_SpawnProjectile("PM_ExaltedLaserSigil",70,0,-45,CMF_AIMDIRECTION|CMF_OFFSETPITCH,-20);
		A_SpawnProjectile("PM_ExaltedLaserSigil",70,0,45,CMF_AIMDIRECTION|CMF_OFFSETPITCH,-20);
	   }
	   EXAL EEEEEEEEEEEEEEE 3 A_FaceTarget();
	   Goto See;
	GigalaserSigil:
	   TNT1 A 0 { if(CountInv("PM_ExaGigalaserCD")) SetStateLabel("BurstShot"); }
	   EXAL F 40 Light("ShadowBall4")
	   {
		A_FaceTarget(360,180);
		if(random(1,2) == 1)
		PM_ExaltedVoice();
		A_SpawnItemEx("PM_ExaltedSuperSigilRise",flags:SXF_SETMASTER);
	    A_SpawnItemEx("PM_ExaltedSigilRotate",flags:SXF_SETMASTER);
		A_StartSound("Exalt/SuperCastReady",17);
		A_StartSound("Exalt/CastAdd",20);
		A_StartSound("PMGazer/Sight",18,pitch:1.25);
		A_StartSound("PMGazer/Sight",19,pitch:1.45);
	   }
	   EXAL E 8 Bright
	   {
	    A_FaceTarget();
		A_SpawnProjectile("PM_ExaltedGLSigilSummon",70,0,0,CMF_AIMDIRECTION|CMF_OFFSETPITCH,-50);
		A_GiveInventory("PM_ExaGigalaserCD",1);
		pm_attackloop--;
	   }
	   EXAL EEE 3 A_FaceTarget();
	   Goto See;
	CultSummon:
	   EXAL E 3 Light("ShadowBall4")
	   {
	    pm_attackloop = random(3,6);
		A_FaceTarget(360,180);
		if(random(1,2) == 1)
		PM_ExaltedVoice();
		A_SpawnItemEx("PM_ExaltedSuperSigilRise",flags:SXF_SETMASTER);
	    A_SpawnItemEx("PM_ExaltedSigilRotate",flags:SXF_SETMASTER);
		A_StartSound("Exalt/SuperCastReady",17);
		A_StartSound("Exalt/CastAdd",20);
		A_StartSound("PMGazer/Sight",18,pitch:1.3);
		A_StartSound("PMGazer/Sight",19,pitch:1.15);
	   }
	   EXAL EEEEEEE 3 Light("ShadowBall4")
	   {
		A_SpawnItemEx("PM_GenericPhaseGhost",xofs:-0.65,zofs:6,zvel:3,flags:SXF_TRANSFERSTENCILCOL|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
		if (!random(0, 1)) A_SpawnItemEx("PM_ExaltedLightRay", frandom(-350, 350), frandom(-350, 350), flags: SXF_NOCHECKPOSITION);
	   }
	CSLoop:
	   TNT1 A 0 { if(pm_attackloop <= 0) SetStateLabel("SuperCastEnd"); }
	   EXAL EE 3 Light("ShadowBall4")
	   {
		A_SpawnItemEx("PM_GenericPhaseGhost",xofs:-0.65,zofs:6,zvel:3,flags:SXF_TRANSFERSTENCILCOL|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
		if (!random(0, 1)) A_SpawnItemEx("PM_ExaltedLightRay", frandom(-350, 350), frandom(-350, 350), flags: SXF_NOCHECKPOSITION);
	   }
	   EXAL E 3 Light("ShadowBall4") {
	   A_SpawnItemEx("PM_GenericPhaseGhost",xofs:-0.65,zofs:6,zvel:3,flags:SXF_TRANSFERSTENCILCOL|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
	   if (!random(0, 1)) A_SpawnItemEx("PM_ExaltedLightRay", frandom(-350, 350), frandom(-350, 350), flags: SXF_NOCHECKPOSITION);
	   A_SpawnProjectile("PM_ExaltedCultSummon",32,0,frandom(-180,180));
	   pm_attackloop--;
	   }
	   Loop;
	SuperCastEnd:
	   EXAL E 15;
	   TNT1 A 0 { if(random(1,2) == 1) SetStateLabel("PhaseJump"); }
	   Goto See;
	MissileRedEnd:
	   EXAL F random(5,15);
	   TNT1 A 0 { if(random(1,6) == 1) SetStateLabel("PhaseJump"); }
	   Goto See;
    Pain:
		EXAL K 6 A_Pain;
		TNT1 A 0 A_Jump(32,"PainTeleport");
        Goto See;
	PainTeleport:
	    EXAL E 5 
		{ 
		  if(CountInv("PM_ExaPainTPCD")) 
		   { SetStateLabel("See"); }
		  bNOPAIN = true; 
		}
		EXAL E 13 
		{ 
		  A_StartSound("Exalt/TPEnd",30,pitch:1.45);
		  A_StartSound("Exalt/TPStart",31,pitch:1.35);
		  for(int i=random(35,50);i>0;i--)
		  {
		   A_Wander();
		   A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,0,0,0,flags:SXF_TRANSFERSTENCILCOL|SXF_TRANSFERALPHA|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		  }
         tplimit++;		 
		}
		EXAL E 5 
		{
		 A_SpawnParticleEx("", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: 45, size: 600, xoff: 10, zoff: 40,startalphaf: 1.0, fadestepf: -1, sizestep: -36, startroll: random(-180,180), rollvel: frandom(-10, 10));
		 if(tplimit == 3)
		  {
		   tplimit = 0;
		   A_GiveInventory("PM_ExaPainTPCD",1);
		  }
		  bNOPAIN = false;
		}
		Goto See;
    Death:
        EXAL L 5
		{
		  bDONTTHRUST = true;
		  bDONTGIB = true;
		  PM_Scream();
		  A_StartSound("Exalt/Crumble",104,pitch:frandom(0.95,1.15));
		  A_StartSound("PMGazer/Hover",103,CHANF_LOOPING,volume:0.55,pitch:0.75);
		  for(int i=25;i>0;i--)
		  A_SpawnParticleEx("545454", tex[4], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,lifetime: random(25,85), size: frandom(1,50), xoff: frandom(-25,25), yoff: frandom(-25,25), zoff: random(0,65),velx: frandom(-8,8), vely: frandom(-8,8), velz: frandom(-0.1,30), accelz: frandom(-0.1,-0.5), startalphaf: 1.0,sizestep: -0.2, startroll: random(-180,180), rollvel: random(0,4));
		}
		EXAL M 0 
		{
		 A_Fall();
		 A_StartSound("PMGazer/Sight",102,CHANF_OVERLAP,pitch:frandom(0.30,0.75));
		 A_StartSound("Abhor/BallExp",100,attenuation:0.86,pitch:0.30);
		for(int i=15;i>0;i--)
		  A_SpawnParticleEx("f42424", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(65,105), size: frandom(45,75), xoff: frandom(-25.5,25.5), yoff: frandom(-25.5,25.5), zoff: random(0,15),velx: frandom(-0.5,0.5), vely: frandom(-0.5,0.5), velz: frandom(0.3,2), startalphaf: frandom(0.5,0.9),fadestepf: -1,sizestep: 5);
		}
		EXAL MMNOOPPQQRRSSTTTTTTTTTTTTTTTTTTTTTTTT 3
		{
		if(random(1,8) == 1)
		 A_StartSound("PMGazer/Sight",102,CHANF_OVERLAP,volume:0.35,pitch:frandom(0.30,0.75));
		if(random(1,15) == 1)
	     A_StartSound("PMGazer/Sight",103,CHANF_OVERLAP,volume:0.15,pitch:frandom(1.35,1.75));
		 A_SpawnParticleEx("f42424", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(65,105), size: frandom(45,75), xoff: frandom(-25.5,25.5), yoff: frandom(-25.5,25.5), zoff: random(0,15),velx: frandom(-0.5,0.5), vely: frandom(-0.5,0.5), velz: frandom(0.3,2), startalphaf: frandom(0.5,0.9),fadestepf: -1,sizestep: 5);
		 A_SpawnParticleEx("545454", TexMan.CheckForTexture("DUSTC0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(40,70),size: frandom(1,40), xoff: frandom(-25,25), yoff: frandom(-25,25), zoff: random(0,65), velx: frandom(-8,8), vely: frandom(-8,8),velz: frandom(-8,12), accelz: frandom(-0.1,-0.5), startalphaf: 1.0, sizestep: -0.2, startroll: random(-180,180), rollvel: random(0,4));
		 A_SpawnParticleEx("545454", TexMan.CheckForTexture("DUSTC0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,lifetime: random(20,120), size: frandom(1,45), xoff: random(-30,30), yoff: random(-30,30),zoff: 5, velx: frandom(-1,1), vely: frandom(-1,1), velz: frandom(1,4), startalphaf: 1.0, fadestepf: -1, sizestep: -0.4, startroll: random(-180,180),rollvel: random(0,4));
		}
		EXAL TTTTTTTTTTTTTTTTTTTTTTTTTTT 5
		{
		if(random(1,10) == 1)
		 { A_StartSound("PMGazer/Sight",102,CHANF_OVERLAP,volume:0.65,pitch:frandom(0.30,0.75)); }
		if(random(1,18) == 1)
		 { A_StartSound("PMGazer/Sight",103,CHANF_OVERLAP,volume:0.45,pitch:frandom(1.35,1.75)); }
		 A_SpawnParticleEx("f42424", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(65,105), size: frandom(45,75), xoff: frandom(-25.5,25.5), yoff: frandom(-25.5,25.5), zoff: random(0,15),velx: frandom(-0.1,0.1), vely: frandom(-0.1,0.1), velz: frandom(0.3,2), startalphaf: frandom(0.5,0.9),fadestepf: -1,sizestep: 5);
		 A_SpawnParticleEx("830000", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 45, size: frandom(45,75), xoff: frandom(-25.5,25.5), yoff: frandom(-25.5,25.5), zoff: random(0,15),velx: frandom(-0.5,0.5), vely: frandom(-0.5,0.5), velz: frandom(0.3,2), startalphaf: frandom(0.5,0.9), fadestepf: -1,sizestep: 5);
		 A_SpawnParticleEx("545454", TexMan.CheckForTexture("DUSTC0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,lifetime: random(20,120), size: frandom(1,25), xoff: random(-30,30), yoff: random(-30,30),zoff: 5, velx: frandom(-1,1), vely: frandom(-1,1), velz: frandom(1,4), startalphaf: 1.0, fadestepf: -1, sizestep: -0.4, startroll: random(-180,180),rollvel: random(0,4));
		}
		EXAL TTTTTTTTTTTTTTTTTTTTTT 4
		{
		if(random(1,12) == 1)
		 { A_StartSound("PMGazer/Sight",102,CHANF_OVERLAP,volume:0.35,pitch:frandom(0.30,0.75)); }
		if(random(1,18) == 1)
		 { A_StartSound("PMGazer/Sight",103,CHANF_OVERLAP,volume:0.15,pitch:frandom(1.35,1.75)); }
		 A_SpawnParticleEx("830000", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 45, size: frandom(45,75), xoff: frandom(-25.5,25.5), yoff: frandom(-25.5,25.5),zoff: random(0,15), velx: frandom(-0.5,0.5), vely: frandom(-0.5,0.5), velz: frandom(0.3,2),startalphaf: frandom(0.5,0.9), fadestepf: -1, sizestep: 5);
		 A_SpawnParticleEx("545454", TexMan.CheckForTexture("DUSTC0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,lifetime: random(20,120), size: frandom(1,25), xoff: random(-30,30), yoff: random(-30,30),zoff: 5, velx: frandom(-1,1), vely: frandom(-1,1), velz: frandom(1,4), startalphaf: 1.0, fadestepf: -1, sizestep: -0.4, startroll: random(-180,180),rollvel: random(0,4));
		}
		EXAL T 5 { bDONTGIB = false; bDONTTHRUST = false; A_StopSound(103); }
		EXAL T -1;
        Stop;
	Raise:
	    EXAL TSRQPONMLK 4;
		Goto LookSpawned;
    }
}

Class PM_ExaltVoiceCD : Powerup { Default { Powerup.Duration -8; } }
Class PM_ExaPainTPCD : Powerup { Default { Powerup.Duration -4; } }
Class PM_ExaGigalaserCD : Powerup { Default { Powerup.Duration -30; } }

Class PM_ExaltedCastReady : Actor
{
  Default
  {
   +NOINTERACTION
   +FLATSPRITE
   +ROLLSPRITE
   +BRIGHT
   Scale 0.65;
   Renderstyle "Add";
  }
  
  override void PostBeginPlay()
  {
   Super.PostBeginPlay();
   pitch = 270;
  }
  States
  {
  Spawn:
   EXAF AAAAAAAAAAAAAA 1
    {
	 A_SetRoll(roll+10,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,35,0,75);
	}
   EXAF A 1
    {
	 A_SetScale(Scale.X-0.05);
	 A_SetRoll(roll+20,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,35,0,75);
	 A_FadeOut(0.1);
	}
	Wait;
   }
  }

Class PM_ExaltedBeacon : Actor
{
 Default
 {
 XScale 4;
 YScale 120;
 Alpha 1;
 RenderStyle "AddShaded";
 StencilColor "Red";
 +BRIGHT
 +NOINTERACTION
 }
 
 float destined;
 
 override void PostBeginPlay()
 {
  Super.PostBeginPlay();
  destined = alpha / 3;
  tex[0] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
  tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
  alpha = 0.01;
 }
 
 TextureID tex[2];
 
 void BeaconFX()
 {
	scale.x += 0.005;
	
	if (!(GetAge() % random(1,2)))
	{
		if(!random(0, 12)) A_SpawnParticleEx("", tex[0], STYLE_Add, SPF_FULLBRIGHT|SPF_ROLL|SPF_RELATIVE, lifetime: random(15,200), size: random(5,15), xoff: frandom(-120,120), yoff: frandom(-120,120), zoff: frandom(0,25), velx: frandom(-2.5,2.5), vely: frandom(-2.5,2.5), velz: frandom(0,4), accelx: frandom(-0.05, 0.05), accely: frandom(-0.05, 0.05), accelz: frandom(-0.001, 0.05), startalphaf: frandom(0.65, 1)
		, fadestepf: frandom(-0.01,-0.04), sizestep: -0.005, startroll: random(-180,180));
		if(!random(0, 4)) A_SpawnParticleEx("e53737", tex[1], STYLE_Add, SPF_ROLL|SPF_RELATIVE, lifetime: random(100, 200), size: random(150,250), xoff: frandom(-100,100), yoff: frandom(-100,100), zoff: frandom(0,10), velx: frandom(-0.5,0.5), vely: frandom(-0.5,0.5), velz: frandom(0,1.5), accelz: 0.04, startalphaf: frandom(.2,.4), fadestepf: -1, startroll: random(-180,180), rollvel: frandom(-2, 2));
	}
 }
 
 States
 {
 Spawn:
  NTGD Z 1
  {
	A_FadeIn(0.01);
	BeaconFX();
	if (alpha >= destined) SetStateLabel("Fade");
  }
  Loop;
 Fade:
  NTGD Z 1 { A_FadeOut(0.02); BeaconFX(); }
  Wait;
 }
}

Class PM_ExaltedLightRay : Actor
{
	Default
	{
		Scale 0.9;
		RenderStyle "Add";
		Alpha 0.01;
		+BRIGHT
		+NOINTERACTION
	}
	
	override void PostbeginPlay()
	{
		super.PostBeginPlay();
		scale.x = frandom(1.59, 1.61);
		scale.y = scale.x;
		scale.x *= 1.1;
	}
	
	States
	{
	Spawn:
		PRAY ZZZZZZZZZZZZZZZZZZZZ 1 A_FadeIn(0.03);
		PRAY Z 1 A_FadeOut(0.03);
		Wait;
	}
}

Class PM_ExaltedIntroBang : Actor
{
	Default
	{
		Scale 1.2;
		+FLATSPRITE
		+BRIGHT
		+NOINTERACTION
		-SOLID
		RenderStyle "AddShaded";
		StencilColor "Red";
	}
	
	States
	{
	Spawn:
		EXAF A 1 Bright
		{
			 A_FadeOut(0.03);
			 A_SetAngle(angle+0.625);
			 scale.x += 0.05;
			 scale.y = scale.x;
		}
		Wait;
	}
}

Class PM_ExaltedMagicShot : Actor
{
  Default
  {
    Radius 5;
    Height 5;
    Speed 23;
    DamageFunction 25;
    Renderstyle "Add";
    ALPHA 0.90;
    Scale 0.50;
	SeeSound "PM/BulletHellSpawn";
    DeathSound "Exalt/BHShotExp";
    PROJECTILE;
	Decal "PM_RedFireScorch";
	DamageType "PM_BulletHellDamage";
	+FORCEXYBILLBOARD
	+ROLLSPRITE
	}

    override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 tex[0] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("REDLA0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
	 tex[3] = TexMan.CheckForTexture("SPKRC0",TexMan.Type_Sprite);
	}
	
	TextureID tex[4];
	
  States
  {
  Spawn:
	TNT1 A 0;
	TNT1 A 0
	{
	 A_SpawnParticleEx("cc0000",TexMan.CheckForTexture("C1TEA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: 5,size: 125,xoff: -20,startalphaf: 1.0,fadestepf: -1,sizestep: frandom(0.0,3),startroll: frandom(-180,180));
	 A_SpawnParticleEx("", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: random(1,20),size: 260.0, xoff: -10, startalphaf: 0.85, fadestepf: -1, startroll: random(-180,180) );
	 for(int i=random(1,5);i>0;i--)
	  { A_SpawnParticleEx("", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: random(2,25), size: random(60,195),xoff: -10, velx: frandom(-0.1,0.1), vely: frandom(-0.1,0.1), velz: frandom(-0.1,0.1), startalphaf: 0.9, startroll: random(-180,180)); }
	  }
  Go:
	SPKR A 1 Bright
	{
	 A_SetRoll(random(0,256));
	 A_Weave(random(-2.0,2.0),random(-2.0,2.0),frandom(-0.5,0.5),frandom(-0.5,0.5));
	 if(random(0,4) == 1)
	  {
	   A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(7,15),size: frandom(90,120),xoff: frandom(-3.5,3.5),yoff: frandom(-3.5,3.5),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: 1,fadestepf: -1,sizestep: -7,startroll: frandom(-180,180),rollvel: frandom(-5,5));
	   A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(2,4),size: random(1,50),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: frandom(0.0,0.9),startroll: random(-180,180),rollvel: frandom(-30,30));
	  }
	 A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(15,37), size: random(100,200), xoff: frandom(-5,5), yoff: frandom(-10,10), zoff: frandom(-10,10),velx: frandom(-0.1,0.1),vely: frandom(-0.1,0.1),velz: frandom(-0.1,0.1),startalphaf: frandom(0.3,0.8),sizestep: -12,startroll: random(-180,180) );
	}
    Loop;
  Death:
	TNT1 A 0
	{
	 A_SpawnParticleEx("",tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(5,25), size: 350.0, xoff: -10, startalphaf: frandom(0.55,0.85),fadestepf: -1, sizestep: -6,startroll: random(-180,180) );
	for(int i=random(5,8);i>0;i--)
	 { A_SpawnParticleEx( "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(25,100), size: random(5,45), xoff: frandom(-25,25), yoff: frandom(-25,25), zoff: frandom(-25,25),velx: frandom(-3.5,3.5), vely: frandom(-3.5,3.5), velz: frandom(-0.5,3.5),accelz: -0.295, startalphaf: 0.9, startroll: random(-180,180) ); }
	for(int i=random(1,4);i>0;i--)
	 {
	 A_SpawnParticleEx("", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(5,30), size: 195.0,xoff: frandom(-12,12),yoff: frandom(-12,12),zoff: frandom(-12,12),startalphaf: 0.9, startroll: random(-180,180) );
	 }
	}
    TNT1 AAA 1 Bright Light("Shadowball2");
	TNT1 AA 1 Bright Light("Shadowball3");
    Stop;
  }
}

Class PM_ExaltedSuperSigil : Actor
{
  Default
  {
   +NOINTERACTION
   +ROLLSPRITE
   +FORCEXYBILLBOARD
   +BRIGHT
   Scale 0.45;
   RenderStyle "Add";
   StencilColor "Red";
  }
  
  States
  {
  Spawn:
   TNT1 A 0;
   EXAF A 0;
   EXAF AAA 1
    {
	 A_SetScale(Scale.X+0.05);
	 A_SetRoll(roll+10,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,5,0,85);
	}
   EXAF AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
    {
	 A_SetRoll(roll+10,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,5,0,85);
	}
   EXAF A 1
    {
	 A_SetScale(Scale.X-0.05);
	 A_SetRoll(roll+20,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,5,0,85);
	 A_FadeOut(0.1);
	}
	Wait;
  }
}

Class PM_ExaltedSuperSigilRise : Actor
{
  int rise;
  Default
  {
   +NOINTERACTION
   +ROLLSPRITE
   +FLATSPRITE
   +FORCEXYBILLBOARD
   +BRIGHT
   Scale 0.60;
   RenderStyle "Add";
   StencilColor "Red";
  }
  
  States
  {
  Spawn:
   TNT1 A 0;
   EXAF A 0;
   EXAF AAAA 1
    {
	 if(rise >= 105)
	 SetStateLabel("Death");
	 A_SetRoll(roll+10,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,0,0,rise);
	 A_SpawnParticleEx(
      "", TexMan.CheckForTexture("REDLA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: random(2,40), size: random(60,195),
	  xoff: -10, velx: frandom(-0.1,0.1), vely: frandom(-0.1,0.1), velz: frandom(-0.1,0.1), startalphaf: 0.9, startroll: random(-180,180));
	 rise += 5;
	}
   Wait;
  Death:
   EXAF A 1
    {
	 A_SetRoll(roll+20,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,5,0,rise);
	 A_FadeOut(0.2);
	 rise++;
	}
	Wait;
  }
}
  

Class PM_ExaltedSigilRotate : Actor
{
  int rise;
  Default
  {
   +NOINTERACTION
   +ROLLSPRITE
   +FORCEXYBILLBOARD
   +BRIGHT
   +FLATSPRITE
   Scale 1.35;
   RenderStyle "Stencil";
   StencilColor "Red";
  }
  
  override void PostBeginPlay()
  {
   Super.PostBeginPlay();
   tex[0] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
  }
  
  TextureID tex[1];
  
  States
  {
  Spawn:
   C1TE AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
    {
	 if(rise >= 105)
	 SetStateLabel("Death");
	 A_SetRoll(roll+10,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,50,65,rise,8,WARPF_INTERPOLATE|WARPF_USECALLERANGLE);
	 A_SpawnParticleEx(
	 "", tex[0], STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
	  lifetime: 50, size: random(1,30), xoff: frandom(-30,30), yoff: frandom(-30,30), zoff: frandom(-30,30),
	  velx: frandom(-0.5,0.5), vely: frandom(-0.5,0.5), velz: frandom(-0.5,0.5),
	  accelz: -0.005, startalphaf: frandom(0.5,0.8), startroll: random(-180,180) );
	rise += 2;
	}
   C1TE A 1
    {
	 A_SetScale(Scale.X-0.05);
	 A_SetRoll(roll+20,SPF_INTERPOLATE);
	 A_Warp(AAPTR_MASTER,50,65,rise,8,WARPF_INTERPOLATE|WARPF_USECALLERANGLE);
	 A_FadeOut(0.1);
	 rise += 2;
	}
	Wait;
   }
}

Class PM_ExaltedSuperBall : Actor
{
  int timer;
  Default
  {
  Radius 20;
  Height 20;
  Projectile;
  Speed 8;
  Alpha 0.15;
  RenderStyle "Add";
  SeeSound "Exalt/SuperBall";
  BounceType "Hexen";
  BounceFactor 0.3;
  Scale 5.5;
  +BRIGHT
  +FORCEXYBILLBOARD
  +ROLLSPRITE
  +ROLLCENTER
  +SEEKERMISSILE
  +THRUACTORS
  }
  
  override void PostBeginPlay()
  {
   Super.PostBeginPlay();
   tex[0] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
   tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
   tex[2] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
   tex[3] = TexMan.CheckForTexture("SPKRC0",TexMan.Type_Sprite);
   timer = random(45,100);
  }
  
  TextureID tex[4];

  States
  {
  Spawn:
   TNT1 A 0;
   TNT1 A 0 
   {
      for(int i=3;i>0;i--)
	   { A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: 30,size: 400,startalphaf: 1,fadestepf: -1,sizestep: -15,startroll: random(-180,180)); }
   }
  Go:
   BH_B AAAA 3 Bright
   {
     if(timer <= 0)
	  { SetStateLabel("Death"); }
     A_SetRoll(roll+10);
	 A_SpawnParticleEx("fd3535", tex[1], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(6,10), size: 185.0, xoff: -5, startalphaf: frandom(0.45,0.75), fadestepf: -1, startroll: random(-180,180));
	 A_SpawnParticleEx("",tex[3], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(15,65), size: random(1,35), xoff: frandom(-45,45), yoff: frandom(-45,45),zoff: frandom(-45,45),velx: frandom(-0.2,0.2),vely: frandom(-0.2,0.2),velz: frandom(-0.2,0.2),startalphaf: frandom(0.55,1), fadestepf: -1, startroll: random(-180,180));
     A_Weave(random(-1,1),random(-1,1),frandom(-2.5,2.5),frandom(-2.5,2.5));
	 timer--;
   }
   TNT1 A 0 A_Explode(8,85,XF_NOTMISSILE|XF_NOSPLASH,damagetype:"PM_BulletHellDamage");
   Loop;
  Death:
   BH_B A 0
    {
     for(int i=3;i>0;i--)
	  { A_SpawnParticleEx("", tex[0], style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, lifetime: 35,size: 350, xoff: -25, startalphaf: 0.45, fadestepf: -1, sizestep: 12, startroll: random(-180,180) ); }
     for(int i=2;i>0;i--)
	  { A_SpawnParticleEx("", tex[3], style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, lifetime: 20,size: 600, xoff: -25, startalphaf: 0.65, fadestepf: -1, sizestep: -20, startroll: random(-180,180) ); }
	 A_StartSound("Exalt/SuperBallExp",18);
   }
   BH_B A 2
    {
	 A_SetScale(Scale.X+0.05);
	 A_FadeOut(0.25);
	}
   Wait;
   }
}

Class PM_ExaltedBHBall : PM_BlashemerBHBall
{
 Default { Radius 12; Speed 14; SeeSound ""; +SEEKERMISSILE }
 
 States
 {
  Spawn:
   TNT1 A 0;
   TNT1 A 0 A_SeekerMissile(0,2);
   TNT1 AA 0 { A_SpawnParticleEx("",TexMan.CheckForTexture("DTPRR0"),style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: random(3,8),size: random(80,130),startalphaf: 1,fadestepf: -1,sizestep: -15,startroll: random(-180,180)); }
  Go:
   TRCR A 1 Bright A_SetRoll(roll+25);
   Loop;
  Death:
   TNT1 AA 0
   { A_SpawnParticleEx("",TexMan.CheckForTexture("DTPRR0"),style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: random(2,8),size: random(100,170),startalphaf: 0.95,fadestepf: -1,sizestep: -15,startroll: random(-180,180)); }
   TNT1 AAAAA 0
   { A_SpawnParticleEx( "", TexMan.CheckForTexture("RLSSA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(6,15), size: random(10,20), velx: random(-4,4), vely: random(-4,4), velz: random(-4,4),startalphaf: 0.7, fadestepf: -1, startroll: random(-180,180)); }
   TNT1 A 5 Light("Shadowball2");
   Stop;
 }
}

Class PM_ExaltedBHBallFast : PM_ExaltedBHBall
{ Default { Speed 20; } }

Class PM_ExaltedCultSummon : Actor
{
  Default
  {
  Radius 10;
  Height 10;
  Projectile;
  Speed 8;
  RenderStyle "Normal";
  BounceType "Hexen";
  BounceFactor 0.3;
  Scale 1.0;
  Alpha 0.95;
  +FLOATBOB
  +BRIGHT
  +DONTTHRUST
  +THRUACTORS
  +FORCEXYBILLBOARD
  +SEEKERMISSILE
  +FLOORHUGGER
  }
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	tex[0] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	tex[2] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
	tex[3] = TexMan.CheckForTexture("REDLA0",TexMan.Type_Sprite);
  }
	TextureID tex[4];
  
  States
  {
  Spawn:
   TNT1 A 0;
   TNT1 AAAAAAAAAAAA random(1,5);
  Go:
   TNT1 A 0 {
   A_ScaleVelocity(0);
   A_SpawnItemEx("PM_ExaltedBeacon",flags:SXF_NOCHECKPOSITION); }
   TNT1 AAAAAAA 3 A_FaceTracer();
   TNT1 A 0 { A_FaceTracer(); A_SpawnItemEx("PM_ExaltCultist",0,0,10,0,0,0,0,flags:SXF_NOCHECKPOSITION); }
   Stop;
  }
}

Class PM_ExaltedLaserSigil : PM_ExaltedCultSummon
{
  Default
  {
  RenderStyle "Add";
  BounceType "Hexen";
  BounceFactor 0.3;
  Speed 5;
  Scale 0.85;
  Alpha 0.85;
  +ROLLSPRITE
  -FLOATBOB
  -FLOORHUGGER
  }
  
  States
  {
  Spawn:
   TNT1 A 0 NoDelay A_SetPitch(90);
   TNT1 AAAAAAAAA 2;
  Go:
   EXAF A 0 
   {
    A_SpawnParticleEx("", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: 30, size: 560, xoff: 10, zoff: 0,startalphaf: 0.75, fadestepf: -0.002, sizestep: -25, startroll: random(-180,180));
    A_StartSound("PM/RuneFill",21,CHANF_OVERLAP); 
    A_ScaleVelocity(0); 
   }
   EXAF AAAAAAAAAAAA 1 A_SetScale(Scale.X-0.05);
   EXAF AAAAAAA 3 { A_SetRoll(roll+5); A_FaceTracer(12,12); }
   EXAF AAAAAA 4 
   {
     A_SetRoll(roll+10);
     A_FaceTracer(12,12);
     A_StartSound("PM/BulletHellSLaser",20);
     A_SpawnProjectile("PM_ExaltedBHBallFast",5,0,frandom(-5,5),CMF_AIMDIRECTION|CMF_TRACKOWNER,pitch+frandom(-5.5,5.5)); 
   }
   EXAF AAAAAAAAAAAAAAAAAAAA 1 { 
   A_SetScale(Scale.X+0.05);
   A_FadeOut(0.1); }
   Stop;
  }
}

Class PM_ExaltedGLSigilSummon : PM_ExaltedCultSummon
{
  Default
  {
  Speed 5;
  +SEEKERMISSILE
  -FLOORHUGGER
  }
  
  States
  {
  Spawn:
   TNT1 A 0;
   TNT1 AAAAAA random(1,3);
  Go:
   TNT1 AAAAAAA 3 A_FaceTracer();
   TNT1 A 0 {
    A_SpawnItemEx("PM_ExaltedGigalaserSigil",0,0,10,0,0,0,0,flags:SXF_NOCHECKPOSITION); }
   Stop;
  }
}

Class PM_ExaltedGigalaserSigil : Actor
{
 int attackloop;
 Default
 {
  Radius 40;
  Height 56;
  Health 250;
  Scale 0.55;
  Monster;
  +ROLLSPRITE
  -COUNTKILL
  -SOLID
  +NOGRAVITY
  +DONTFALL
  +FLOAT
  +NOBLOOD
  +THRUSPECIES
  +DONTTHRUST
  +THRUACTORS
  +NEVERTARGET
  +DOHARMSPECIES
  RenderStyle "Add";
  StencilColor "DarkRed";
  Tag "Gigalaser Sigil";
  Obituary "%o was completely wiped out by a gigalaser sigil belonging to an Exalted.";
 }

  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	attackloop = 300;
	tex[0] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	tex[2] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
	tex[3] = TexMan.CheckForTexture("REDLA0",TexMan.Type_Sprite);
  }
  TextureID tex[4];

 States
 {
 Spawn:
  TNT1 A 0;
  TNT1 A 0 {
    A_StartSound("Exalt/CastAdd",20);
	for(int i=3;i>0;i--)
	A_SpawnParticleEx(
		"", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: 35, size: 550, xoff: 10, zoff: 0,
		startalphaf: 1.0, fadestepf: -0.002, sizestep: -18, startroll: random(-180,180));
	for(int i=5;i>0;i--)
		A_SpawnParticleEx(
	    "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: int(ceil(frandom(10,50))), size: random(10,20),
		xoff: frandom(-15,15), yoff: frandom(-15,15), zoff: random(0,32), velx: frandom(-5,5), vely: frandom(-5,5), velz: frandom(0,5), startalphaf: 1.0, fadestepf: -1, startroll: random(-180,180), rollvel: frandom(-5,5));
  }
 See:
  EXAF AAAAA 5 { A_SetRoll(roll+5); A_FaceTracer(5,5); }
  Laser:
   TNT1 A 0 A_StartSound("PM/GigalaserStart",22);
   EXAF AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 Light("ShadowBall2") 
   {
    A_SetRoll(roll+20);
    A_FaceTarget(0.45,0.45);
	A_SpawnParticleEx( "", TexMan.CheckForTexture("SPKRC0"), style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_ROLL|SPF_RELATIVE,
	lifetime: random(1,10), size: random(150,450), xoff: random(18,20), yoff: random(-5,5), zoff: 0,
	velx: frandom(0.1,0.5), vely: frandom(-0.5,0.5), velz: frandom(-0.5,0.5),
	startalphaf: frandom(0.75,1), fadestepf: -0.95, startroll: random(-180,180));
   }
   TNT1 A 0 { A_StartSound("PM/GigalaserStart",22); }
   EXAF AAAA 2 Light("ShadowBall3")
   {
    A_SetRoll(roll+20);
    A_FaceTarget(0.55,0.55);
	A_SpawnProjectile("PM_ExaltGigalaser",0,0,0,CMF_AIMDIRECTION,pitch+frandom(-0.1,0.1));
	A_SpawnParticleEx( "", tex[2], style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_ROLL|SPF_RELATIVE,
	lifetime: random(1,10), size: random(150,450), xoff: random(18,20), yoff: random(-5,5), zoff: 0,
	velx: frandom(0.1,0.5), vely: frandom(-0.5,0.5), velz: frandom(-0.5,0.5),
	startalphaf: frandom(0.75,1), fadestepf: -0.95, startroll: random(-180,180));
   }
  LaserLoop:
   TNT1 A 0 A_StartSound("PM/GigalaserLoop",23);
   EXAF AAAA 1 Light("ShadowBall3")
   {
    A_SetRoll(roll+20);
    A_FaceTarget(0.55,0.55);
	A_SpawnProjectile("PM_ExaltGigalaser",0,0,0,CMF_AIMDIRECTION,pitch+frandom(-0.1,0.1));
	A_SpawnParticleEx( "", tex[2], style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_ROLL|SPF_RELATIVE,
	lifetime: random(1,10), size: random(150,450), xoff: random(18,20), yoff: random(-5,5), zoff: 0,
	velx: frandom(0.1,0.5), vely: frandom(-0.5,0.5), velz: frandom(-0.5,0.5),
	startalphaf: frandom(0.75,1), fadestepf: -0.95, startroll: random(-180,180));
	attackloop--;
   }
   EXAF A 0 { if(attackloop <= 0) SetStateLabel("ByeBye"); }
   Loop;
  Death:
  XDeath:
  Crash:
  ByeBye:
     EXAF A 5 A_StopSound(23);
	 EXAF A 10;
	 EXAF A 0 {
     A_StartSound("Exalt/CastAdd",20,pitch:1.45);
	 for(int i=3;i>0;i--)
	 A_SpawnParticleEx(
		"", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: 45, size: 350, xoff: 10, zoff: 0,
		startalphaf: 1.0, fadestepf: -0.002, sizestep: -18, startroll: random(-180,180));
	 for(int i=5;i>0;i--)
	 A_SpawnParticleEx(
	    "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: int(ceil(frandom(10,50))), size: random(10,20),
		xoff: frandom(-15,15), yoff: frandom(-15,15), zoff: random(0,32), velx: frandom(-5,5), vely: frandom(-5,5), velz: frandom(0,5), startalphaf: 1.0, fadestepf: -1, startroll: random(-180,180), rollvel: frandom(-5,5));
     }
	 EXAF A 1 { A_SetRoll(roll+5); A_SetScale(Scale.X+0.05); A_FadeOut(0.1); }
	 Wait;
  }
}

Class PM_ExaltGigalaser : PM_DirectorPlasmaLaser
{
 Default { +SEEKERMISSILE }
 States
 {
 Spawn:
    TNT1 A 0;
	TNT1 A 0 A_SeekerMissile(0,2);
	TNT1 A 0
	{
	for(int i=random(1,4);i>0;i--)
	 A_SpawnParticleEx
	 ( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
     lifetime: random(1,50), size: random(10,20), velx: frandom(1,4.5), vely: frandom(-4,4), velz: frandom(-4,2),
	 accelz: frandom(-0.0095,-0.0115), startalphaf: frandom(0.55,0.95), fadestepf: -1, startroll: random(-180,180) );
	}
	Goto Go;
 }
}

Class PM_ExaltCultist : Actor
{
 Default
 {
  Radius 20;
  Height 56;
  Health 80;
  Monster;
  -COUNTKILL
  +FLOAT
  +THRUSPECIES
  +DONTHARMSPECIES
  StencilColor "DarkRed";
  BloodType "PM_NormalBloodBase";
  Species "Nobles";
  Tag "Imperial Blasphemer";
  Obituary "%o was struck down by an Exalted's servant.";
 }

  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	tex[0] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	tex[2] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
	tex[3] = TexMan.CheckForTexture("REDLA0",TexMan.Type_Sprite);
  }
	TextureID tex[4];
	
 States
 {
 Spawn:
  TNT1 A 0;
  TNT1 A 0 {
    A_StartSound("PM/PrideTele",20);
	for(int i=3;i>0;i--)
	A_SpawnParticleEx(
		"", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: 45, size: 350, xoff: 10, zoff: 45,
		startalphaf: 1.0, fadestepf: -0.002, sizestep: -18, startroll: random(-180,180));
	for(int i=5;i>0;i--)
		A_SpawnParticleEx(
	    "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: int(ceil(frandom(10,50))), size: random(10,20),
		xoff: frandom(-15,15), yoff: frandom(-15,15), zoff: random(0,32), velx: frandom(-5,5), vely: frandom(-5,5), velz: frandom(0,5), startalphaf: 1.0, fadestepf: -1, startroll: random(-180,180), rollvel: frandom(-5,5));
  }
 See:
  DISC AAAAAAAAAA 5 A_FaceTarget(5,5);
  TNT1 A 0 A_Jump(56,"Fireball","Laser"); //,"GroundCrasher");
  Normal:
   DISC E 30  { A_StartSound("PMBlasphemer/Yell",21); A_FaceTracer(5,5); }
   DISC EEEEE 5 {
		A_SpawnProjectile("PM_ExaltCultBall",36,randompick(-4, 4),0,CMF_AIMDIRECTION,pitch+frandom(-0.2,0.2));
		A_FaceTarget(3,3);
   }
   Goto ByeBye;
  Laser:
   TNT1 A 0 A_StartSound("GenericChargeSmall",22);
   DISC EEEEEEEEEEEEEEEE 2 Light("ShadowBall2") 
   {
    A_FaceTarget(5,5);
	A_SpawnParticleEx( "", TexMan.CheckForTexture("SPKRC0"), style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_ROLL|SPF_RELATIVE,
	lifetime: random(1,10), size: random(90,150), xoff: random(18,20), yoff: random(-5,5), zoff: 36,
	velx: frandom(0.1,0.5), vely: frandom(-0.5,0.5), velz: frandom(-0.5,0.5),
	startalphaf: frandom(0.75,1), fadestepf: -0.95, startroll: random(-180,180));
   }
   TNT1 A 0 A_StartSound("PM/BulletHellLaserStart",23);
   DISC FFFFFFFF 2 Light("ShadowBall3")
   {
    A_FaceTarget(0.50,0.50);
	A_SpawnProjectile("PM_ExaltCultBeam",36,0,0,CMF_AIMDIRECTION,pitch+frandom(-0.2,0.2));
   }
   TNT1 A 0 A_StartSound("PM/BulletHellLaserLoop",24);
   DISC FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 2 Light("ShadowBall3")
   {
    A_FaceTarget(0.50,0.50);
	A_SpawnProjectile("PM_ExaltCultBeam",36,0,0,CMF_AIMDIRECTION,pitch+frandom(-0.2,0.2));
   }
   DISC E 10 Light("ShadowBall2") A_FaceTarget(0.50,0.50);
   Goto ByeBye;
  Fireball:
	DISC E 2
	{
	 A_FaceTarget();
	 A_StartSound("PMBlasphemer/Yell",21,pitch:frandom(0.7, 1.2));
	 A_StartSound("GenericChargeSmall",20);
	}
	DISC EEEEEEEEEEEEEEEEEEE 1 Light("Avatarfireball4")
	{
	for (int i = -20; i < 21; i += 40)
	 {
	 A_SpawnParticleEx(   
	"FF7800", TexMan.CheckForTexture("DTPRR0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	lifetime: random(1,8), size: frandom(20,270), xoff: frandom(5,10), yoff: frandom(-3.5,3.5) + i, zoff: frandom(-3.5,3.5) + 70,
	startalphaf: 0.9, fadestepf: -1, startroll: random(-180,180));
	 }
    for (int i = -20; i < 21; i += 40)
	 {
	 A_SpawnParticleEx(   
	 "",
	 TexMan.CheckForTexture("F1R1A0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	 lifetime: random(12,20), size: frandom(40,70), xoff: frandom(5,10), yoff: frandom(-3.5,3.5) + i, zoff: frandom(-3.5,3.5) + 74,
	 velx: frandom(-1,1), vely: frandom(-1,1), velz: frandom(0.5,3.5), startalphaf: 1, sizestep: -5, startroll: random(-180,180));
	 }
	  A_FaceTarget(5,5);
     }
	 DISC F 7 Light("Avatarfireball4")
	 {
	  A_StartSound("PM/SigilSpawn",17,pitch:0.85);
	  A_FaceTarget(5,5);
	 }
	 DISC FFFFFFFFFF 1 Light("Avatarfireball4")
	 {
	  A_FaceTarget(5,5);
	  A_SpawnParticleEx(   
	  "FF7800", TexMan.CheckForTexture("DTPRR0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	  lifetime: random(2,10), size: frandom(80,300), xoff: 5, zoff: 40, startalphaf: 0.5,
	  fadestepf: -1, startroll: random(-180,180));
	 }
	 DISC F 0
	 {
	  A_FaceTarget(5,5);
	  A_StartSound("PM/RuneFill",21);
      for(int i=3;i>0;i--)
	  A_SpawnParticleEx(
	  "", TexMan.CheckForTexture("DTPRO0"), style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,
	  lifetime: 10, size: 190, xoff: 40, zoff: 40, startalphaf: 1, fadestepf: -1, sizestep: -15, startroll: random(-180,180));
	 }
	 DISC FFFFFFFFFFFFFFFFF 1 Light("Avatarfireball4")
	 {
	  A_FaceTarget(5,5);
	  A_SpawnParticleEx(   
	  "FF7800", TexMan.CheckForTexture("DTPRR0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	  lifetime: random(2,10), size: frandom(80,300), xoff: 5, zoff: 40, startalphaf: 0.5, fadestepf: -1, startroll: random(-180,180));
	 }
	TNT1 A 0 A_SpawnProjectile("PM_ExaltCultFireball",36,0,0,CMF_AIMDIRECTION,pitch+frandom(-0.2,0.2));
	Goto ByeBye;
	Death:
	 DISC G 15 A_StartSound("PMBlasphemer/Pain",21);
	 DISC E 15;
     DISC E 0 {
     A_StartSound("Exalt/TPStart",20,pitch:frandom(1.15,1.45));
	 for(int i=3;i>0;i--)
	 A_SpawnParticleEx(
		"", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: 45, size: 350, xoff: 10, zoff: 45,
		startalphaf: 1.0, fadestepf: -0.002, sizestep: -18, startroll: random(-180,180));
	 for(int i=5;i>0;i--)
	 A_SpawnParticleEx(
	    "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: int(ceil(frandom(10,50))), size: random(10,20),
		xoff: frandom(-15,15), yoff: frandom(-15,15), zoff: random(0,32), velx: frandom(-5,5), vely: frandom(-5,5), velz: frandom(0,5), startalphaf: 1.0, fadestepf: -1, startroll: random(-180,180), rollvel: frandom(-5,5));
	 for (int i = -15; i <= 15; i+= 10)
		A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,i,0,0,flags:SXF_TRANSFERSTENCILCOL|SXF_TRANSFERALPHA|SXF_TRANSFERSPRITEFRAME);
     }
	 Stop;
	ByeBye:
     DISC E 30;
     DISC E 0 {
     A_StartSound("Exalt/TPStart",20,pitch:frandom(1.15,1.45));
	 for(int i=3;i>0;i--)
	 A_SpawnParticleEx(
		"", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: 45, size: 350, xoff: 10, zoff: 45,
		startalphaf: 1.0, fadestepf: -0.002, sizestep: -18, startroll: random(-180,180));
	 for(int i=5;i>0;i--)
	 A_SpawnParticleEx(
	    "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: int(ceil(frandom(10,50))), size: random(10,20),
		xoff: frandom(-15,15), yoff: frandom(-15,15), zoff: random(0,32), velx: frandom(-5,5), vely: frandom(-5,5), velz: frandom(0,5), startalphaf: 1.0, fadestepf: -1, startroll: random(-180,180), rollvel: frandom(-5,5));
	 for (int i = -15; i <= 15; i+= 10)
		A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,i,0,0,flags:SXF_TRANSFERSTENCILCOL|SXF_TRANSFERALPHA|SXF_TRANSFERSPRITEFRAME);
     }
	 Stop;
	}
}

Class PM_ExaltCultBall : PM_BlasphemerBall
{
  Default { Species "Nobles"; +MTHRUSPECIES }
}

Class PM_ExaltCultFireball : PM_BlasphemerFireball
{
 Default { DamageFunction 30; Species "Nobles"; +MTHRUSPECIES }
}

Class PM_ExaltCultBeam : PM_DraugrLightningArcs
{
  Default
  {
  Speed 36;
  DamageType "PM_BulletHellDamage";
  Species "Nobles";
  +MTHRUSPECIES
  }
}

 
 
