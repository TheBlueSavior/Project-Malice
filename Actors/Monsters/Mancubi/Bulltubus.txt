Class PM_Bulltubus : PMWrathMonsterBase
 {
  int casingcount;
  Default
  {
   PMMonsterBase.CodexLore 
   "Bulltubi are the heavy weapon elites of the circle of Wrath, being born from molten magma and a heart of pure hatred towards humanity.
    They are much more hardheaded and used more as elites compared to their Corupulent brethren. With a gruesome smile, they will immolate anything that it's in their way.
	
	Will use flame-ignited chainguns, and a bodyslam if the opponent is nearby its sight. When it reaches its boiling point, it can unleash hot laser beams.";
   Health 600;
   Radius 48;
   Height 64;
   painchance 70;
   Speed 7;
   Scale 1.1;
   Mass 1000;
   SeeSound "Bulltubus/Sight";
   PainSound "Bulltubus/Pain";
   DeathSound "Bulltubus/Death";
   ActiveSound "Bulltubus/Active";
   Obituary "%o was utterly obliterated by a Bulltubus.";
   Species "Mancubus";
   MinMissileChance 220;
   MeleeRange 170;
   Tag "Bulltubus";
   BloodColor "Orange";
   DamageFactor "PM_BulltubusMelee", 0;
   Monster;
   Bloodtype "PM_LavaBloodBase";
   +DONTHARMCLASS
   +DONTHARMSPECIES
   +FLOORCLIP
   TeleFogSourceType "PM_SmallDemonicTeleportFog";
   TeleFogDestType "PM_WrathTeleportFog";
   }
 
   
   override void PostBeginPlay()
   {
    Super.PostBeginPlay();
	tex[0] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
	tex[2] = TexMan.CheckForTexture("LEYSO0",TexMan.Type_Sprite);
	tex[3] = TexMan.CheckForTexture("BLHDA0",TexMan.Type_Sprite);
	tex[4] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	tex[5] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
   }
   
   TextureID tex[6];
   
   void PM_BulltubusFlamerCharge()
   {
	 A_SpawnParticleEx
		(   
		"",
	    tex[0],
		style: STYLE_Add,
		flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	    lifetime: random(150,250),
		size: frandom(10,150),
		xoff: frandom(20,25),
		yoff: 34,
		zoff: frandom(-3.5,3.5) + 25,
		startalphaf: 0.07,
		fadestepf: -1,
		startroll: random(-180,180)
		);
	 A_SpawnParticleEx
		(   
		"",
		tex[0],
		style: STYLE_Add,
		flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: random(150,250),
		size: frandom(10,150),
		xoff: frandom(20,25),
		yoff: -34,
		zoff: frandom(-3.5,3.5) + 25,
		startalphaf: 0.07,
		fadestepf: -1,
		startroll: random(-180,180)
		);
	 A_SpawnParticleEx
		(
		"",
		tex[1],
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,80),
		size: random(3,10),
		xoff: frandom(20,25),
		yoff: 34,
		zoff: frandom(-3.5,3.5) + 25,
		velx: frandom(0.85,1.35),
		vely: frandom(-0.95,0.95),
		velz: frandom(-0.35,0.35),
		startalphaf: frandom(0.25,0.9),
		fadestepf: 0,
		sizestep: 0,
		startroll: random(-180,180),
		rollvel: frandom(-30,30)
		);
	A_SpawnParticleEx
		(
		"",
		tex[1],
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,50),
		size: random(3,10),
		xoff: frandom(20,25),
		yoff: -34,
		zoff: frandom(-3.5,3.5) + 25,
		velx: frandom(0.85,1.35),
		vely: frandom(-0.95,0.95),
		velz: frandom(-0.35,0.35),
		startalphaf: frandom(0.25,0.9),
		fadestepf: 0,
		sizestep: 0,
		startroll: random(-180,180),
		rollvel: frandom(-30,30)
		);
   }
   States
 {
  Spawn:
    FEFE G 0 NoDelay { pm_irecount = random(10,114); }
  Idle:
    FEFE A 10 PM_Look();
    Loop;
  Look:
	TNT1 A 0 A_StartSound("MancubStep");
    FEFE AAA 3 PM_LookingForPlayer();
	FEFE BBBCCC 3 PM_LookingForPlayer();
	TNT1 A 0 A_JumpIfTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDD 3 PM_LookingForPlayer();
	FEFE EEEFFF 3 PM_LookingForPlayer();
    Loop;
  Spotted:
    TNT1 A 0 A_Jump(85,"SpotMissile");
  Roar:
    TNT1 A 0 PM_AlertSound();
    Goto See;
  See:
	TNT1 A 0 
	{
	 if(ire == true)
	 { A_Jump(256,1); }
	 else if(pm_irecount < 0)
	 { SetStateLabel("Ire"); }
	}
	TNT1 A 0 A_StartSound("MancubStep");
    FEFE AAA random(2,3)
	{
	 if(ire) A_SetTics(2);
	 A_Chase();
	}
	FEFE BBBCCC random(2,3)
	{
	 if(ire) A_SetTics(2);
	 A_Chase();
	}
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDD random(2,3)
	{
	 if(ire) A_SetTics(2);
	 A_Chase();
	}
	FEFE EEEFFF random(2,3)
	{
	 if(ire) A_SetTics(2);
	 A_Chase();
	}
	TNT1 A 0 { pm_irecount -= 5; }
    Loop;
  Rush:
	TNT1 A 0 { bNOPAIN = true; }
	TNT1 A 0 PM_AlertSound();
	TNT1 A 0 A_StartSound("MancubStep");
    FEFE AAAA 1 A_Chase(null,null);
	FEFE BBBBCCCC 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDDD 2 A_Chase(null,null);
	FEFE EEEEFFFF 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE A 0 A_Chase(null,"Melee",CHF_DONTMOVE);
	TNT1 A 0 { if(target && Distance3D(target) < 400 && random(1,3) == 1)
	SetStateLabel("Shotgun"); }
    FEFE AAAA 1 A_Chase(null,null);
	FEFE BBBBCCCC 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDDD 1 A_Chase(null,null);
	FEFE EEEEFFFF 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE A 0 A_Chase(null,"Melee",CHF_DONTMOVE);
	TNT1 A 0 { if(target && Distance3D(target) < 400 && random(1,3) == 1)
	SetStateLabel("Shotgun"); }
    FEFE AAAA 1 A_Chase(null,null);
	FEFE BBBBCCCC 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDDD 1 A_Chase(null,null);
	FEFE EEEEFFF 1 A_Chase(null,null);
	FEFE A 0 A_Chase(null,"Melee",CHF_DONTMOVE);
	TNT1 A 0 { if(target && Distance3D(target) < 400 && random(1,3) == 1)
	SetStateLabel("Shotgun"); }
	TNT1 A 0
	{
	if(ire)
	  bNOPAIN = true;
	 else
	  bNOPAIN = false;
	}
    Goto See;
  Melee:
  TNT1 A 0 A_Jump(75,"Shotgun");
	TNT1 A 0 {bNOPAIN = True;}
	TNT1 A 0 A_StartSound("Bulltubus/Attack",10);
	TNT1 A 0 A_StartSound("Bruiser/LeapCharge",11);
	FEFE GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 Light("HectebusBallLight")
	{
	A_SpawnParticleEx
	( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
	lifetime: random(50,80), size: frandom(1,400), xoff: random(-50,50), yoff: random(-50,50),
	zoff: random(-10,70), startalphaf: frandom(0.3,0.7), startroll: random(-180,180) );
	Radius_Quake (1, 2, 0, 7, 0);
	A_FaceTarget();
	}
	TNT1 A 0 { Radius_Quake (8, 24, 0, 10, 0);
	A_StartSound("Bruiser/LeapPropel",12); 
	A_SpawnParticleEx
	( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
	lifetime: 8, size: 360, zoff: 40, startalphaf: 1, startroll: random(-180,180) );
	A_SpawnParticleEx
	( "", tex[3], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	lifetime: 20, size: 800, zoff: 50, startalphaf: 1, fadestepf: -1, sizestep: -25,
    startroll: random(-180,180) );
	}
	FEFE I 5 PM_LeapTo(target,565,"See");
  MeleeGo:
    FEFE IIII 1
	{
	if(random(1,15) == 1)
	   ThrustThing(random(0,360)*256/360,2,0,0);
	A_SpawnParticleEx
	( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
	lifetime: random(50,80), size: frandom(1,200), xoff: random(-50,50), yoff: random(-50,50),
	zoff: random(-10,70), startalphaf: frandom(0.3,0.7), startroll: random(-180,180) );
	Radius_Quake (1, 2, 0, 7, 0);
	}
	FEFE I 0 A_CheckFloor("MeleeEnd");
	Loop;
   MeleeEnd:
    TNT1 A 0 {bNOPAIN = false;}
	TNT1 A 0 A_RadiusThrust(4000,300);
	TNT1 A 0 A_Explode(50,200,XF_NOTMISSILE,0,50,0,0,"BulletPuff","PM_BulltubusMelee");
    TNT1 A 0 {
    	A_StartSound("Doom4/Monsters/BaronFloorPound",12,CHANF_OVERLAP);
    	A_StartSound("PMInfernal/MeleeBlast",13,CHANF_OVERLAP); }
    FEFE G 40 
	{
	A_Stop();
	A_SpawnParticleEx
	    ( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
	    lifetime: 8, size: 250, zoff: 40, startalphaf: 1, startroll: random(-180,180) );
	A_SpawnParticleEx
	    ( "", tex[3], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	    lifetime: 20, size: 600, zoff: 50, startalphaf: 1, fadestepf: -1, sizestep: -25,
        startroll: random(-180,180) );
	for(int i=8;i>0;i--) A_SpawnParticleEx
	    ( "727272", tex[4], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,
	    lifetime: random(40,70), size: frandom(2,35), zoff: random(10,60),
	    velx: frandom(-8,8), vely: frandom(-8,8), velz: frandom(-8,12),
	    accelz: frandom(-0.1,-0.5), startalphaf: 1.0, sizestep: -0.2, startroll: random(-180,180), rollvel: random(-4,4) );
		for(int i=8;i>0;i--) A_SpawnParticleEx
	     ( "6C6C6C", tex[5], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(30,60),
	     size: frandom(80,220), zoff: random(10,60), velx: random(-9,9), vely: random(-9,9),
		 velz: random(-9,9), startalphaf: frandom(0.4,0.75), fadestepf: -1, sizestep: 12, startroll: random(-180,180),
		 rollvel: random(-2,2) );
	for(int i=8;i>0;i--) A_SpawnParticleEx
		 ( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		 lifetime: random(80,120), size: frandom(1,35), zoff: random(10,60), velx: random(-8,8),
		 vely: random(-8,8), velz: random(-8,8), accelz: frandom(-0.1,-0.3),
		 startalphaf: 1, sizestep: -0.2, startroll: random(-180,180), rollvel: random(-2,2) );
	}
	Goto See;
   SpotMissile:
    FEFE G 5 A_FaceTarget();
   Goto Missile;
  Missile:
    TNT1 A 0;
	TNT1 A 0 {
	if(target && Distance3D(target) < 400 && random(1,2) == 1)
	SetStateLabel("Shotgun");
	if(ire && random(1,8) == 1)
	SetStateLabel("Flamethrower"); }
  Minigun:
	TNT1 A 0 { if(random(1,3) == 1) A_StartSound("Bulltubus/Attack",10);
	if(ire)
	 {
	  A_StartSound("Bulltubus/WindUp",pitch:1.25);
	  pm_attackloop = random(65,70);
	 }
	else
	 {
	  A_StartSound("Bulltubus/WindUp");
	  pm_attackloop = random(35,40); 
	 }
	casingcount = 0;  
	}
	FEFE GGGGGG 5 A_FaceTarget();
	TNT1 A 0
	{
	A_SetAngle(angle+random(-30,30));
	A_SetPitch(pitch+random(-15,30));
	}
	TNT1 A 0 { 
	if(ire)
	 {
	 bNOPAIN = true;
	 A_StartSound("Bulltubus/WindLoop",11,CHANF_LOOPING,pitch:1.25);
	 A_StartSound("Bulltubus/IreWindLoop1",14,CHANF_LOOPING);
	 A_StartSound("Bulltubus/IreWindLoop2",15,CHANF_LOOPING);
	 A_StartSound("Bulltubus/IreGun",16,CHANF_LOOPING);
	 }
	else
	A_StartSound("Bulltubus/WindLoop",11,CHANF_LOOPING);
	}
  MinigunLoop:
    FEFE G 3 Light("AvatarFireball2")
	{
	 if(pm_attackloop <= 0)
	 SetStateLabel("MinigunEnd");
	 if(ire)
	 {
	  A_SpawnProjectile("PM_BulltubusFastTracer",32,-34,frandom(-2,2),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-2,2));
	  A_SpawnProjectile("PM_BulltubusFastTracer",32,34,frandom(-2,2),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-2,2));
	  A_SetTics(1);
	 }
	 else
	 {
	  A_StartSound("Bulltubus/Gun",23,CHANF_OVERLAP);
	  A_StartSound("GunnerRifleDistant",22,CHANF_OVERLAP);
	  A_SpawnProjectile("PM_BulltubusSlowTracer",32,-34,frandom(-4,4),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-4,4));
	  A_SpawnProjectile("PM_BulltubusSlowTracer",32,34,frandom(-4,4),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-4,4));
	 }
	 casingcount++;
	 pm_attackloop--;
	}
    FEFE I 3 Light("AvatarFireball3") { if(ire)A_FaceTarget(0.45,3,0,0); else A_FaceTarget(1.5,3,0,0); }
	Loop;
  MinigunEnd:
    TNT1 A 0 { A_StopSound(11); A_StopSound(14); A_StopSound(15); A_StopSound(16); 
	if(ire)
	{
	A_StartSound("Bulltubus/IreGunDown",16);
	A_StartSound("Bulltubus/WindDown",12,pitch:1.25);
	}
	else 
	{ A_StartSound("Bulltubus/WindDown",12); }
	}
    FEFE G 20 A_FaceTarget(0.85,3,0,0);
	TNT1 A 0 A_StartSound("PM/HeavyRifleBoltSlide",13,pitch:0.75);
	FEFE G 15 A_FaceTarget();
	TNT1 A 0 A_StartSound("Bulltubus/MagOpen",14);
	FEFE I 10 { 
	for(int i=casingcount;i>0;i--)
	 {
	 A_SpawnItemEx("PM_BigBulletCasing",-20,-34,40,frandom(0,-25),frandom(-5,-15),frandom(5,15),0,SXF_NOCHECKPOSITION);
     A_SpawnItemEx("PM_BigBulletCasing",-20,34,40,frandom(0,-25),frandom(5,15),frandom(5,15),0,SXF_NOCHECKPOSITION);
	 }
	A_FaceTarget();
	casingcount = 0;
	}
	FEFE III 5 A_FaceTarget();
	TNT1 A 0 A_StartSound("Bulltubus/MagClose",15);
	FEFE G 15 A_FaceTarget();
	Goto See;
 Shotgun:
    TNT1 A 0 {
	if(ire)
	  bNOPAIN = true;
	 else
	  bNOPAIN = false;
	A_StartSound("Bulltubus/Attack",17);
	A_StartSound("Hectebus/Aim",16); }
    FEFE G 1 A_FaceTarget();
	FEFE GGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 PM_BulltubusFlamerCharge();
	FEFE I 5 Light("AvatarFireball2")
	{
	 A_StartSound("GenericShotgunFire",16,CHANF_OVERLAP,pitch:0.80);
	 A_StartSound("Bulltubus/Shotgun",15);
	 A_FaceTarget(1.5,3,0,0);
	 for(int i=4;i>0;i--)
	 {
	 A_SpawnProjectile("PM_BulltubusShotgunTracer",32,-34,frandom(-12,12),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-5,5));
	 A_SpawnProjectile("PM_BulltubusShotgunTracer",32,34,frandom(-12,12),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-5,5));
	 }
	 A_RadiusThrust(2000,600,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE);
	 A_Recoil(6);
	}
	TNT1 A 0 { if(ire) SetStateLabel("ShotgunIre"); }
	Goto ShotgunEnd;
 ShotgunIre:
    FEFE G 0 A_FaceTarget(1.5,3,0,0);
	FEFE GGGGGGGGGGGG 1 PM_BulltubusFlamerCharge();
	FEFE I 5 Light("AvatarFireball2")
	{
	 A_StartSound("GenericShotgunFire",16,CHANF_OVERLAP,pitch:0.80);
	 A_StartSound("Bulltubus/Shotgun",15,CHANF_OVERLAP);
	 A_FaceTarget(1.5,3,0,0);
	 for(int i=4;i>0;i--)
	 A_SpawnProjectile("PM_BulltubusShotgunTracer",32,-34,frandom(-12,12),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-5,5));
	 A_RadiusThrust(1000,600,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE);
	 A_Recoil(3);
	}
	FEFE G 0 A_FaceTarget(1.5,3,0,0);
	FEFE GGGGGGGG 1 PM_BulltubusFlamerCharge();
	FEFE I 5 Light("AvatarFireball2")
	{
	 A_StartSound("GenericShotgunFire",16,CHANF_OVERLAP,pitch:0.80);
	 A_StartSound("Bulltubus/Shotgun",15,CHANF_OVERLAP);
	 A_FaceTarget(1.5,3,0,0);
	 for(int i=4;i>0;i--)
	 A_SpawnProjectile("PM_BulltubusShotgunTracer",32,34,frandom(-12,12),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-5,5));
	 A_RadiusThrust(1000,600,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE);
	 A_Recoil(3);
	}
 ShotgunEnd:
    FEFE G 25 A_FaceTarget(0.85,3,0,0);
	TNT1 A 0 A_StartSound("PM/HeavyRifleBoltSlide",13,pitch:0.75);
	FEFE G 15 A_FaceTarget();
	TNT1 A 0 A_StartSound("Bulltubus/MagOpen",14);
	FEFE I 10 { 
	for(int i=6;i>0;i--)
	 {
	 A_SpawnItemEx("PM_BigBulletCasing",-20,-34,40,frandom(0,-25),frandom(-5,-15),frandom(5,15),0,SXF_NOCHECKPOSITION);
     A_SpawnItemEx("PM_BigBulletCasing",-20,34,40,frandom(0,-25),frandom(5,15),frandom(5,15),0,SXF_NOCHECKPOSITION);
	 }
	A_FaceTarget();
	}
	FEFE III 5 A_FaceTarget();
	TNT1 A 0 A_StartSound("Bulltubus/MagClose",15);
	Goto See;
  Flamethrower:
	FEFE G 1;
	TNT1 A 0 { bNOPAIN = true; pm_attackloop = 160; }
    FEFE GGGGGG 3 A_FaceTarget();
	TNT1 A 0 A_StartSound("Bulltubus/FlamerStart",CHAN_6);
	FEFE GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 Light("AvatarFireBall2")
	{
	A_FaceTarget();
	PM_BulltubusFlamerCharge();
	A_SpawnItemEx("OrangeFireEffect",18,random(34,42),random(23,27),0,0,2);
	A_SpawnItemEx("OrangeFireEffect",18,random(-34,-42),random(23,27),0,0,2);
	}
	TNT1 A 0 {
	A_SetAngle(angle+random(-25,25));
	A_SetPitch(pitch+random(-25,25));
	A_StartSound("Bulltubus/Roar");
	A_StartSound("Defiler/Breath",30,CHANF_LOOPING);
	A_StartSound("Bulltubus/FlamerLoop",31,CHANF_LOOPING);
	}
  FTLoop:
	FEFE HHH 1 Light("AvatarFireball2")
	{
	if(pm_attackloop < 0)
	SetStateLabel("FTEnd");
	A_SpawnProjectile("PM_BulltubusFlamer", 30, 34,frandom(-1.5,1.5),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-0.05,0.05));
	A_SpawnProjectile("PM_BulltubusFlamer", 30, -34,frandom(-1.5,1.5),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-0.05,0.05));
	A_FaceTarget(0.55,0.55,0,0);
	pm_attackloop--;
	}
	FEFE H 1 A_FaceTarget(0.55,0.55,0,0);
	Loop;
  FTEnd:
	TNT1 A 0 { 
	ire = false;
	bNOINFIGHTING = false;
	bNOFEAR = false;
	bNOPAIN = false;
    pm_irecount = random(40,164);
	A_RemoveChildren(1,RMVF_EVERYTHING); 
	A_StopSound(30); A_StopSound(31); A_StopSound(20); A_StopSound(19); }
	FEFE GGIIGGIIGGII 4 A_FaceTarget(0.35,0.35,0,0);
	Goto See;
  Ire:
    FEFE AA 5 A_FaceTarget();
	TNT1 A 0 PM_AlertSound();
	FEFE GGG 5 Light("AvatarFireball2") A_FaceTarget();
	TNT1 A 0 
	{ 
	 ire = true;
	 bNOINFIGHTING = true;
	 bNOFEAR = true;
	 bNOPAIN = true;
	 A_SpawnItemEx("PM_WrathIreActivate",0,0,15);
	 A_SpawnItemEx("PM_WrathIreEffect",0,0,45,flags:SXF_SETMASTER);
    }
	TNT1 A 0 PM_IreStartSound();
	FEFE GGIIGGIIGGIIGGIIGGII 4 Light("AvatarFireball3") 
	{
	A_FaceTarget();
	if (!(level.time % 2)) A_SpawnItemEx("OrangeEmbersFloat",0,random(-10,10),15,random(1,12),random(-3,3),random(-3,3));
	}
	Goto See;
  Pain:
    FEFE J 1;
	TNT1 A 0 A_StopSound(11);
	TNT1 A 0 { pm_irecount -= 10; }
	TNT1 A 0 A_Jump(120,"Rush");
    FEFE J 9 A_Pain;
    Goto See;
  Death:
	   TNT1 A 0;
	   TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING);
	   FDFD A 6
	   {
	   A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   }
       FDFD B 6 PM_Scream();
       FDFD C 6 A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
       FDFD DEF 6 A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   TNT1 A 0 A_StartSound("Corpse/FallMetal",CHAN_6);
	   FDFD GH 6 A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
       FDFD I 6 A_NoBlocking;
       FDFD J -1;
	   Stop;
  Raise:
	FDFD JIHGFEDCBA 6;
	TNT1 A 0 { bNOPAIN = false; }
    Goto Look;
  }
}

Class PM_BulltubusSlowTracer : Fatshot
{
  Default
  {
   Projectile;
   Speed 22;
   Alpha 0.85;
   Radius 4;
   Height 6;
   Scale 1.0;
   DamageFunction 20;
   SeeSound "";
   DeathSound "FeralImp/Ireball";
   Decal "PM_PlasmatronScorchLarge";
   DamageType "Fire";
   RenderStyle "Add";
  }
  
  override void PostBeginPlay()
  {
   Super.PostBeginPlay();
   tex[0] = TexMan.CheckForTexture("SPKOC0",TexMan.Type_Sprite);
   tex[1] = TexMan.CheckForTexture("DTPRO0",TexMan.Type_Sprite);
   tex[2] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
   tex[3] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
   tex[4] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
  }
  
  TextureID tex[5];

  override void Tick()
  {
   if(level.IsFrozen()) return;
   Super.Tick();
   if(level.maptime % 5)
   {
	A_SpawnParticleEx
	 (
		"",
		tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: 1, size: 80.0, xoff: -5, startalphaf: 0.6
	 );
    }
  }
 
  States
  {
    Spawn:
    TNT1 A 0;
	  TNT1 A 0
	  {
		A_SpawnParticleEx
		( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: random(2,5), size: frandom(100,170), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3),
		startroll: random(-180,180));
		
		A_SpawnParticleEx
		( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,7),
		size: frandom(90,155), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3), startroll: random(-180,180) );
		
		A_SpawnParticleEx
		( "#d2d2d2", tex[4], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,300),
		size: frandom(10,90), xoff: -10, velx: frandom(0,6), vely: frandom(-0.8,0.8), velz: frandom(-0.4,0.7), startalphaf: frandom(0.3,0.7),
		fadestepf: -1, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1));
		
		for(int i=random(1,3);i>0;i--)
		A_SpawnParticleEx
		( "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,100), size: frandom(1,12), xoff: frandom(-2,2), yoff: frandom(-2,2), zoff: frandom(-2,2),
		velx: frandom(1,10), vely: frandom(-3,3), velz: frandom(-0.6,3),
		accelx: frandom(-0.005,0.005), accely: frandom(-0.005,0.005), accelz: frandom(-0.0,-0.8), startalphaf: 1.0, fadestepf: -0.5,
		sizestep: -0.2, startroll: random(-180,180));
	  }
    Go:
      TRCR AA 1 Bright;
      Loop;
    Death:
	  TNT1 A 0 A_StartSound("HellionProjExp",volume:0.75,pitch:frandom(1.0,1.85));
	  TNT1 A 0 Radius_Quake (1, 2, 0, 5, 0);
	  TNT1 A 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
	  TNT1 A 0
	  {
	   for(int i=random(5,12);i>0;i--)
	   A_SpawnParticleEx
	   (
	   "",
	   tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: random(18,40),
	   size: frandom(18,35), velx: frandom(-3,3), vely: frandom(-3,3), velz: frandom(-3,5),
	   accelz: frandom(-0.0,-0.8), startalphaf: 1.0, sizestep: -0.2, startroll: random(-180,180)
	   );
	  A_SpawnParticleEx
       ( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,20),
	   size: frandom(100,120), xoff: -5, startalphaf: frandom(0.75,0.95), sizestep: frandom(0.0,0.65), 
	   startroll: random(-180,180) );
	  A_SpawnParticleEx
	   ( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT,
	   lifetime: random(1,5), size: 90.0, xoff: -5, startalphaf: 0.6 );
	  }
      TNT1 AAA 3 Bright Light("YellowMediumFlicker2");
      Stop;
      }
}

Class PM_BulltubusFastTracer : PM_BulltubusSlowTracer
{ Default { Speed 40; } }

Class PM_BulltubusShotgunTracer : PM_BulltubusSlowTracer
{
  int timer;
  Default
  {
   Speed 40;
  }
   States
   {
    Spawn:
      TNT1 A 0;
	  TNT1 A 0
	  {
	    timer = random(1,3);
		A_SpawnParticleEx
		( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: random(2,5), size: frandom(250,300), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3),
		startroll: random(-180,180));
		
		A_SpawnParticleEx
		( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,7),
		size: frandom(130,300), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3), startroll: random(-180,180) );
		
		A_SpawnParticleEx
		( "#d2d2d2", tex[4], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,300),
		size: frandom(10,90), xoff: -10, velx: frandom(0,6), vely: frandom(-0.8,0.8), velz: frandom(-0.4,0.7), startalphaf: frandom(0.3,0.7),
		fadestepf: -1, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1));
		
	  }
    Go:
      TRCR AA 1 Bright
	  {
	   if(timer > 12)
	   SetStateLabel("Death");
	   A_SpawnParticleEx
	   (
		"",
		tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,90), size: frandom(0.2,10), xoff: frandom(-8,8), yoff: frandom(-8,8),
		zoff: frandom(-8,8), velx: frandom(-4.4,4.4), vely: frandom(-4.4,4.4), velz: frandom(-4.4,4.4),
		startalphaf: 1.0, fadestepf: -0.5, sizestep: -0.2, startroll: random(-180,180)
	   );
	   A_SpawnParticleEx
	   (
		"e69138",
		TexMan.CheckForTexture("GNSML0"),
		style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: 45, size: 90.0, xoff: frandom(-10,10), yoff: frandom(-10,10), zoff: frandom(-10,10),
		accelx: frandom(0.080,0.090), accely: frandom(-0.040,0.040), accelz: frandom(-0.090,0.090),
		startalphaf: 0.6,
		fadestepf: -1,
		startroll: random(-180,180)
	   );
	   timer++;
	  }
      Loop;
    Death:
	  TNT1 A 0 A_StartSound("HellionProjExp",volume:0.75,pitch:frandom(1.0,1.85));
	  TNT1 A 0 Radius_Quake (1, 2, 0, 5, 0);
	  TNT1 A 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
	  TNT1 A 0
	  {
	  if(random(1,2) == 1)
	  A_SpawnItemEx("PM_HuskMeteorFire",0,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),SXF_NOCHECKPOSITION);
	  A_SpawnParticleEx
       ( "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,20),
	   size: frandom(100,120), xoff: -5, startalphaf: frandom(0.75,0.95), sizestep: frandom(0.0,0.65), 
	   startroll: random(-180,180) );
	  A_SpawnParticleEx
	   ( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT,
	   lifetime: random(1,5), size: 90.0, xoff: -5, startalphaf: 0.6 );
	  }
      Stop;
      }
}

Class PM_BulltubusFlamer : Actor
{
   Default
   {
   Radius 7;
   Height 7;
   Speed 37;
   DamageFunction 3;
   PROJECTILE;
   RENDERSTYLE "Add";
   DamageType "Fire";
   Scale 3.0;
   DeathSound "Fire/Unleash";
   Decal "Scorch";
   +THRUGHOST
   +ROLLSPRITE
   +FORCEXYBILLBOARD
   }

  override void PostBeginPlay()
  {
   Super.PostBeginPlay();
   tex[0] = TexMan.CheckForTexture("SPKOC0",TexMan.Type_Sprite);
   tex[1] = TexMan.CheckForTexture("DTPRO0",TexMan.Type_Sprite);
   tex[2] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
   tex[3] = TexMan.CheckForTexture("GNSML0",TexMan.Type_Sprite);
  }
  
  TextureID tex[4];
  
   States
   {
   Spawn:
      TNT1 A 0;
	  TNT1 A 0
	  {
		A_SpawnParticleEx
		( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: random(2,5), size: frandom(100,170), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3),
		startroll: random(-180,180));
		
		A_SpawnParticleEx
		( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,7),
		size: frandom(90,155), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3), startroll: random(-180,180) );
	  }
   Go:
      TRCR A 2 Bright;
	  Loop;
   Death:
      TNT1 A 0
	  {
	    A_SetRoll(random(0,360));
		A_SetScale(1.5);
		for(int i=random(1,2);i>0;i--)
		A_SpawnParticleEx
			(
			"",
			tex[3],
			style: STYLE_Normal,
			flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: random(18,65),
			size: frandom(55,110),
			velx: frandom(-3,3),
			vely: frandom(-3,3),
			velz: frandom(-3,3),
			accelz: frandom(-0.1,0.1),
			startalphaf: frandom(0.75,1.0),
			sizestep: 1.0,
			startroll: random(-180,180),
			rollvel: frandom(-1,1)
			);
		A_SpawnParticleEx
			(
			"",
			tex[1],
			style: STYLE_Add,
			flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: random(1,5),
			size: frandom(110,200),
			velx: frandom(-0.1,0.1),
			vely: frandom(-0.1,0.1),
			velz: frandom(-0.1,0.1),
			startalphaf: frandom(0.45,1.0),
			sizestep: 1.0,
			startroll: random(-180,180),
			rollvel: frandom(-1,1)
			);
	  }
      FRFX HIJKLMNO 2 Bright;
      Stop;
	XDeath:
      TNT1 A 0
	  {
	    A_SetRoll(random(0,360));
		A_SetScale(1.5);
	  }
      FRFX HIJKLMNO 2 Bright;
      Stop;
   }
}