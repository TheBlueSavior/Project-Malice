Class PM_Wretched : PMUnaffMonsterBase
{
	Default
	{
		PMMonsterBase.CodexLore
		"Wretched are a common sight above Hell's 'skies', usually floating aimlessly until they find lesser demons to eat or harass.
		 They are commonly sent into battle as aerial opposition, bombarding infantry from above with paralyzing orbs of electricity.
		 Wretched propel themselves through strange means, and while research regarding how they tick is very little, most have concluded that it may have something to do with the electricity generated by some of their internal organs.
		 
		 Fires a single electric orb at you, but can charge up to fire multiple.";
		Speed 4;
		Radius 31;
		Height 56;
		Mass 400;
		Health 400;
		BloodColor "Blue";
		Obituary "%o was zapped by a Wretched.";
		HitObituary "%o was devoured by a Wretched.";
		SeeSound "Wretched/Sight";
		ActiveSound "Wretched/Active";
		PainSound "Wretched/Pain";
		DeathSound "Wretched/Death";
		//DropItem "RagePoint1"
		FloatBobStrength 0.4;
		+DONTHARMSPECIES
		MeleeRange 185;
		Species "Cacodemon";
		Tag "Wretched";
		BloodType "PM_BlueBloodBase";
		+FLOATBOB
		+FLOAT
		+NOGRAVITY
		+MissileMore
		PainChance 120;
		TeleFogSourceType "PM_SmallDemonicTeleportFog";
		TeleFogDestType "PM_DemonicTeleportFog";
	}

	States
	{
		Spawn:
			C1C2 AAAAAAAAAA 10 PM_Look();
			C1C2 A 0
			{
			if (!random(0, 30))
				{
					A_StartSound(activesound,554,volume:frandom(0.05,0.3),pitch:frandom(0.60,0.78));
				}
			}
			C1C2 A 0 A_Jump(45,"Explore");
			Loop;
		Explore:
			TNT1 A 0
			{
			if (!random(0, 12))
				{
					A_StartSound(activesound,554,volume:frandom(0.05,0.3),pitch:frandom(0.60,0.78));
				}
			}
			TNT1 A 0 A_SetSpeed(2);
			C1C2 AAAAAAAA 3
			{
			PM_Look();
			A_Wander();
			}
			C1C2 AAAAAAAA 3
			{
			A_Wander();
			PM_Look();
			}
			C1C2 A 0 A_Jump(40,"Spawn");
			Loop;
		Look:
			TNT1 A 0
			{
				bMissileEvenMore = False;
				bFRIGHTENED = False;
			}
			TNT1 A 0 A_SetSpeed(4);
			C1C2 AAAA 2 PM_LookingForPlayer();
			TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
			Loop;
		Spotted:
			TNT1 A 0 A_Jump(256,"Roar","RoarNoAnim","RoarNoAnim");
		RoarNoAnim:
			TNT1 A 0 PM_AlertSound();
		See:
			TNT1 A 0
			{
				bMissileEvenMore = False;
				bFRIGHTENED = False;
			}
			TNT1 A 0 A_SetSpeed(4);
			C1C2 AAAA 2 
			{
				A_Chase();
				A_FaceTarget();
			}
			CA2B AAAA 2 
			{
				A_Chase();
				A_FaceTarget();
			}
			C1C2 AAAA 2 
			{
				A_Chase();
				A_FaceTarget();
			}
			CA2C AAAA 2 
			{
				A_Chase();
				A_FaceTarget();
			}
			TNT1 A 0 A_Jump(35,"Avoid");
			TNT1 A 0 A_Jump(15,"Rush");
			Loop;
		Bite:
			C1C2 B 4 A_FaceTarget();
			TNT1 A 0 A_Jump(100,"AvoidBack");
			TNT1 A 0 A_JumpIfCloser(70,"Bite2");
			Goto See;
		Bite2:
			C1C2 BC 6 A_FaceTarget();
			TNT1 A 0 A_Jump(100,"AvoidBack");
			TNT1 A 0 A_JumpIfCloser(70,"Bite3");
			Goto See;
		Bite3:
			C1C2 DD 3 A_CustomMeleeAttack(10*random(1,6),"Wretched/Bite");
			C1C2 A 10;
			Goto See;
		Missile:
			TNT1 A 0 A_Jump(15,"Roar");
			TNT1 A 0 A_Jump(10,"TriShot");
			TNT1 A 0 A_Jump(60,"StandoffCheck");
			TNT1 A 0 A_Recoil(-0.3);
			C1C2 ABC 5
			{
				A_Chase(Null,Null);
				A_FaceTarget();
			}
			C1C2 D 6 Light("Wretchball2")
			{
				A_Chase(Null,Null);
				A_FaceTarget();
				A_StartSound("Wretched/Spit",CHAN_7);
				A_SpawnProjectile("PM_WretchedBall",25);
			}
			C1C2 CBA 5 Light("Wretchball4")
			{
				A_Chase(Null,Null);
				A_FaceTarget();
			}
			Goto See;
		StandoffCheck:
			TNT1 A 0 A_JumpIfCloser(250,"Missile");
			C1C2 EE 4 A_FaceTarget();
			TNT1 A 0 ThrustThing(randompick(0,128),8, 0, 0);
			C1C2 BBBBB 5 A_FaceTarget();
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			C1C2 EE 4 A_FaceTarget();
		Standoff:
			TNT1 A 0
			{
				bMissileEvenMore = True;
				bFRIGHTENED = False;
			}
			//TNT1 A 0 A_ChangeFlag("MISSILEevenMORE",True);
			TNT1 A 0 A_Jump(7,"See");
			TNT1 A 0 A_SetSpeed(0);
			C1C2 AAAAAA 2
			{
				A_Chase("Melee","StandoffMissile");
				//A_Recoil(-0.012);
				A_FaceTarget();
			}
			CA2B AAAAAA 2 
			{
				A_Chase("Melee","StandoffMissile");
				//A_Recoil(-0.012);
				A_FaceTarget();
			}
			C1C2 AAAAAA 2 
			{
				A_Chase("Melee","StandoffMissile");
				//A_Recoil(-0.012);
				A_FaceTarget();
			}
			CA2C AAAAAA 2 
			{
				A_Chase("Melee","StandoffMissile");
				//A_Recoil(-0.012);
				A_FaceTarget();
			}
			TNT1 A 0 A_CheckSight("See");
			TNT1 A 0 A_Jump(120,"StandoffRepos");
			Loop;
		StandoffRepos:
			TNT1 A 0 {bFRIGHTENED = True;}
			C1C2 AAA 1
			{
				A_Chase();
				A_FaceTarget();
			}
		StandoffReposLoop:
			TNT1 A 0 A_SetSpeed(5);
			C1C2 AAAA 2 
			{
				A_Wander();
				//A_Recoil(-0.012);
				A_FaceTarget();
			}
			CA2B AAAA 2 
			{
				A_Wander();
				//A_Recoil(-0.012);
				A_FaceTarget();
			}
			C1C2 AAAA 2 
			{
				A_Wander();
				//A_Recoil(-0.012);
				A_FaceTarget();
			}
			CA2C AAAA 2 
			{
				A_Wander();
				//A_Recoil(-0.012);
				A_FaceTarget();
			}
			TNT1 A 0 {bFRIGHTENED = False;}
			TNT1 A 0 A_Jump(100,"Standoff");
			Loop;
		StandoffMissile:
			TNT1 A 0 A_Jump(30,"StandoffRoar");
			C1C2 ABC 5
			{
				A_Chase(Null,Null);
				A_FaceTarget();
			}
			C1C2 D 6 Light("Wretchball2")
			{
				A_Chase(Null,Null);
				A_FaceTarget();
				A_StartSound("Wretched/Spit",CHAN_7);
				A_SpawnProjectile("PM_WretchedBall",25);
			}
			C1C2 CBA 5 Light("Wretchball4")
			{
				A_Chase(Null,Null);
				A_FaceTarget();
			}
			TNT1 A 0 A_JumpIfCloser(350,"AvoidBack");
			Goto Standoff;
		StandoffRoar:
			TNT1 A 0 A_Recoil(-0.6);
			C1C2 AB 3 A_FaceTarget();
			TNT1 A 0 A_StartSound("Wretched/Sight");
			C1C2 CCDCCDCCC 6 A_FaceTarget();
			C1C2 BA 3 A_FaceTarget();
			Goto Standoff;
		Melee:
			TNT1 A 0 A_Jump(150,1);
			Goto Missile;
			TNT1 A 0 A_JumpIfCloser(70,"Bite");
			TNT1 A 0 A_ScaleVelocity(0);
			TNT1 A 0 A_StartSound("");	
			C1C2 AB 4 A_FaceTarget();
			TNT1 A 0 A_StartSound("Wretched/Bite");
			C1C2 DDDDDDDD 1
			{
				A_Recoil(-3);
				A_SpawnProjectile("PM_MeleeHitbox",-8,0,0,2,0);
			}
			C1C2 DD 2 A_SpawnProjectile("PM_MeleeHitbox",-8,0,0,2);
			C1C2 D 3 A_ScaleVelocity(0.5);
			C1C2 C 4 A_ScaleVelocity(0.25);
			C1C2 B 3 A_ScaleVelocity(0);
			TNT1 A 0 A_Jump(200,"AvoidBack");
			Goto See;
		TriShot:
			TNT1 A 0 A_Recoil(-0.08);
			TNT1 A 0 A_StartSound("Wretched/Charge");
			CA22 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 Light("Wretchball2")
			{
				A_FaceTarget();
				A_SpawnParticleEx
				(
				"",
				TexMan.CheckForTexture("DTPRB0"),
				style: STYLE_Add,
				flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
				lifetime: 10,
				size: random(70,130),
				xoff: 20,
				zoff: 18,
				startalphaf: 0.3,
				fadestepf: 0.1,
				sizestep: 0,
				startroll: random(-180,180)
				);
				A_SpawnParticleEx
				(
				"",
				TexMan.CheckForTexture("BLULA0"),
				style: STYLE_Add,
				flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
				lifetime: random(2,10),
				size: random(1,100),
				xoff: 20,
				zoff: 18,
				velx: random(-3,3),
				vely: random(-3,3),
				velz: random(-3,3),
				startalphaf: frandom(0,1),
				startroll: random(-180,180)
				);
			}
			C1C2 ABC 2 A_FaceTarget();
			C1C2 DDDDD 5 Light("Wretchball2")
			{
				A_FaceTarget();
				A_StartSound("Wretched/Spit",CHAN_7);
				A_SpawnProjectile("PM_WretchedBall",25,0,random(-6,6),CMF_OFFSETPITCH,random(-2,2));
			}
			C1C2 DDCBA 7 Bright A_FaceTarget();
			Goto See;
		Rush:
			TNT1 A 0 A_SetSpeed(9);
			TNT1 A 0 A_StartSound("Wretched/Rush",CHAN_6);
			C1C2 BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 2
			{
				A_Recoil(-0.02);
				A_Chase("Melee", null);
				A_FaceTarget();
			}
			Goto See;
		Roar:
			TNT1 A 0 A_Recoil(-0.6);
			C1C2 AB 3 A_FaceTarget();
			TNT1 A 0 PM_AlertSound();
			C1C2 CCDCCDCCC 6 A_FaceTarget();
			C1C2 BA 3 A_FaceTarget();
			Goto See;
		Avoid:
			C1C2 EE 4 A_FaceTarget();
			TNT1 A 0 ThrustThing(random(0,360),8);
			TNT1 A 0 ThrustThingZ(0,8,random(0,1),1);
			C1C2 BBBBB 5 A_FaceTarget();
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			C1C2 EE 4 A_FaceTarget();
			Goto See;
		AvoidLeft:
			C1C2 EE 4 A_FaceTarget();
			TNT1 A 0 ThrustThing(random(0,360),16);
			TNT1 A 0 ThrustThingZ(0,13,random(0,1),1);
			C1C2 BBBBB 5 A_FaceTarget();
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			C1C2 EE 4 A_FaceTarget();
			Goto See;
		AvoidRight:
			C1C2 EE 4 A_FaceTarget();
			TNT1 A 0 ThrustThing(0, 8, 0, 0);
			C1C2 BBBBB 5 A_FaceTarget();
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			C1C2 EE 4 A_FaceTarget();
			Goto See;
		AvoidBack:
			C1C2 EE 4 A_FaceTarget();
			TNT1 A 0 ThrustThing(64, 8, 0, 0);
			C1C2 BBBBB 5 A_FaceTarget();
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			C1C2 EE 4 A_FaceTarget();
			Goto See;
		AvoidForward:
			C1C2 EE 4 A_FaceTarget();
			TNT1 A 0 ThrustThing(192, 8, 0, 0);
			C1C2 BBBBB 5 A_FaceTarget();
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			C1C2 EE 4 A_FaceTarget();
			Goto See;
		Pain:
			C1C2 E 3;
			C1C2 E 3 A_Pain();
			TNT1 A 0 A_Jump(70,"Avoid");
			C1C2 F 6;
			Goto See;
		XDeath:
		Death:
			TNT1 AA 0 A_SpawnItemEx("PM_BlueMeatChunkLauncher",random(-2,2),random(-2,2),random(15,40),0,0,0,0,SXF_NOCHECKPOSITION,60);
			C1C2 G 6
			{
			bFLOATBOB = False;
			A_SpawnItemEx("PM_BlueMeatChunkLauncher",random(-2,2),random(-2,2),random(10,40),0,0,0,0,SXF_NOCHECKPOSITION,60);
			}
			C1C2 H 6 PM_Scream();
			C1C2 IJ 6 A_SpawnItemEx("PM_BlueMeatChunkLauncher",random(-2,2),random(-2,2),random(10,30),0,0,0,0,SXF_NOCHECKPOSITION,60);
			C1C2 K 6 A_NoBlocking();
			TNT1 A 0 A_SpawnItemEx("PM_BlueMeatChunkLauncher",random(-2,2),random(-2,2),random(10,30),0,0,0,0,SXF_NOCHECKPOSITION,60);
			C1C2 L 5 A_SetFloorClip();
			TNT1 A 0 A_SpawnItemEx("PM_BlueMeatChunkLauncher",random(-2,2),random(-2,2),random(10,20),0,0,0,0,SXF_NOCHECKPOSITION,60);
			C1C2 L -1;
			Stop;
		Raise:
			C1C2 L 8 A_UnSetFloorClip();
			C1C2 KJIHG 8;
			Goto Look;
	}
}

Class PM_WretchedBall : PM_CacodemonBallBase
{
    Default
    {
	Radius 5;
	Height 6;
	Speed 14;
	FastSpeed 23;
	Scale 1.22;
	DamageFunction 20;
	Projectile;
	+RANDOMIZE
	RenderStyle "Add";
	DamageType "Electric";
	Alpha 0.99;
	SeeSound "Wretched/Fire";
	DeathSound "imp/shotx";
	+FORCEXYBILLBOARD
    +ROLLSPRITE
    +ROLLCENTER
	Decal "PM_DeathSoulScorchMed";
	MaxTargetRange 2;
    }
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 { A_SpawnParticleEx("",TexMan.CheckForTexture("DTPRB0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 10,size: 145.0,xoff: -10,startalphaf: 0.98,fadestepf: 0.1,sizestep: 0,startroll: random(-180,180)); }
		TNT1 A 0 A_StartSound("Wretched/ShotLoop",CHAN_7,1,1);
	Go:
		DKB1 A 1 Bright Light("Wretchedball") A_SetRoll(roll+15);
		TNT1 A 0  
        {
	        A_SpawnParticleEx("905dfc",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 4,size: 34.0,xoff: -10,startalphaf: 1.0,fadestepf: -2.5,sizestep: -7,startroll: random(-180,180));
            A_SpawnParticleEx("",TexMan.CheckForTexture("DTPRB0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 1,size: 90.0,xoff: -10,startalphaf: 0.2,startroll: random(-180,180));
		    A_SpawnParticleEx("",TexMan.CheckForTexture("BLULA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(2,4),size: random(1,70),velx: frandom(-2,2),vely: frandom(-2,2),velz: frandom(-2,2),startalphaf: frandom(0.0,0.9),startroll: random(-180,180),rollvel: frandom(-30,30));
		}
		DKB1 B 1 Bright Light("Wretchedball") A_SetRoll(roll+15);
        TNT1 A 0  
        {
		    A_SpawnParticleEx("905dfc",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 4,size: 34.0,xoff: -10,startalphaf: 1.0,fadestepf: -2.5,sizestep: -7,startroll: random(-180,180));
            A_SpawnParticleEx("",TexMan.CheckForTexture("DTPRB0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 1,size: 90.0,xoff: -10,startalphaf: 0.2,startroll: random(-180,180));
        }
		loop;
	Death:
        TNT1 A 0 A_SetRoll(0);
		TNT1 A 0 A_StopSound(CHAN_7);
		TNT1 AAAAAAAAAAAAAAAAAA 0 { A_SpawnParticleEx("",TexMan.CheckForTexture("BLULA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(2,10),size: random(60,195),velx: random(-6,6),vely: random(-6,6),velz: random(-6,6),startalphaf: 0.9,startroll: random(-180,180)); }
		TNT1 AAAA 0 A_SpawnProjectile("PM_BlueShock",2,0,random(0,360),CMF_OFFSETPITCH,random(0,360));
		TNT1 A 0 A_SpawnItemEx("PM_WretchedBallFlare",0,0,0);
	    DKB1 CDE 3 Bright Light("WretchedBall2");
		stop;
	}
}

Class PM_WretchedBallFlare : Actor
{
	Default
	{
		+CLIENTSIDEONLY
		+FORCEXYBILLBOARD
		+NOINTERACTION
		RenderStyle "Add";
		+Bright
		Scale 0.5;
		Alpha 0.8;
	}

	States
	{
		Spawn:
			SPKO SSS 1 Bright
			{
				A_FadeOut(0.05);
				A_SetScale(Scale.X+0.05);
			}
			Loop;
    }
}

Class WretchedBallTrail : Actor
{
	Default
	{
		Scale 1.25;
		+NOINTERACTION
		+CLIENTSIDEONLY
		+NOBLOCKMAP
		+THRUACTORS
		+DONTSPLASH
		+NOCLIP
		Translation "0:255=%[0,0,0]:[1.0,0.5,1.9]";
		RenderStyle "Add";
	}

	States
	{
		Spawn:
			TNT1 A 2;
		Flying:
			DKB1 AB 1 BRIGHT
			{
				A_FadeOut(0.23);
				A_SetScale(Scale.X-0.19);
			}
			Goto Flying;
    }
}