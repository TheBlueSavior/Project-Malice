Class PM_GraveGuard : PMDeathMonsterBase
{
    int deathwither;
    Default
	{
	PMMonsterBase.CodexLore
	"Grave Guards are named so due to their tendency to defend their spoils (most commonly being souls or cadavers) from other demons of the same circle.
	 They are usually highly aggressive, but in the field all of their anger is directly focused onto you. Combined with their fast and sudden phasing ability, they are relentless in their assault.
	 
	 Uses a fast, seeking soul as its primary attack. May also breathe out blue flames or screaming souls.
	 Can charge up lightning in its hands and releases them in the form of focused thunder from both palms.";
    Health 500;
    Radius 24;
    Height 64;
    Speed 10;
    PainChance 50;
    Mass 1000;
	Scale 1.15;
    SeeSound "HFurySight";
    ActiveSound "HFuryActive";
    PainSound "HFuryPain";
    DeathSound "HFuryDeath";
	Species "Nobles";
    Obituary "%o was reduced to a soulless ember pile by a Grave Guard.";
	Tag "Grave Guard";
    BloodType "PM_DeathBloodBase";
    BloodColor "Black";
    MONSTER;
	+DONTHARMSPECIES
    +FLOORCLIP
	+MissileMore
	+DONTHARMCLASS
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	}
	
	override void PostBeginPlay()
	{
	 deathwither = random(20,35);
	 tex[0] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("DTPRB0",TexMan.Type_Sprite);
	 tex[3] = TexMan.CheckForTexture("BLULA0",TexMan.Type_Sprite);
	 Super.PostBeginPlay();
	}
	
	TextureID tex[4];

    void PM_HellsFuryWither()
	{
	 for(int i=random(1,3);i>0;i--)
	 A_SpawnParticleEx
	 (
	 "545454",
	 tex[0],
	 style: STYLE_Normal,
	 flags: SPF_RELATIVE|SPF_ROLL,
	 lifetime: random(20,120),
	 size: frandom(1,15),
	 xoff: random(-15,15),
	 yoff: random(-25,25),
	 zoff: 5,
	 velx: frandom(-1,1),
	 vely: frandom(-1,1),
	 velz: frandom(1,4),
	 startalphaf: 1.0,
	 fadestepf: -1,
	 sizestep: -0.4,
	 startroll: random(-180,180),
	 rollvel: random(0,4)
	 );
	 A_SpawnParticleEx
	 (
	 "545454",
	 tex[1],
	 style: STYLE_None,
	 flags: SPF_ROLL|SPF_RELATIVE,
	 lifetime: random(50,140),
	 size: frandom(30,80),
	 xoff: frandom(-40,40),
	 yoff: frandom(-40,40),
	 velx: frandom(-1,1),
	 vely: frandom(-1,1),
	 velz: frandom(0,2),
	 startalphaf: frandom(0.2,0.95),
	 fadestepf: -1,
	 sizestep: frandom(-0.8,0.8),
	 startroll: random(-180,180),
	 rollvel: frandom(-0.3,0.3)
	 );
	}
	
	void PM_GraveGuardLCharge()
	{
		A_SpawnParticleEx
				( "", tex[2], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
				 lifetime: random(50,200), size: frandom(10,300), xoff: frandom(10,15), zoff: frandom(-3.5,3.5) + 37,
				 startalphaf: 0.20, fadestepf: -1, startroll: random(-180,180) );
		A_SpawnParticleEx
				( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
				lifetime: random(1,4), size: random(3,250), xoff: frandom(10,15), zoff: frandom(-3.5,3.5) + 37,
				velx: random(-6,6), vely: random(-6,6), velz: random(-6,6), startalphaf: 0.9, startroll: random(-180,180),
				rollvel: frandom(-30,30) );
	}
  States
  {
  Spawn:
    HFRY AA 10 PM_Look();
    Loop;
  Look:
    HFRY AABB 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Squire/Step");
    TNT1 A 0 A_JumpIfTargetInLOS("Spotted",170,JLOSF_DEADNOJUMP);
	HFRY CCDD 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Squire/Step");
    Loop;
  Spotted:
    TNT1 A 0 PM_AlertSound();
  See:
    TNT1 A 0 A_SetSpeed(10);
    HFRY AABB 3 A_Chase();
	TNT1 A 0 A_SpawnItemEx("PM_GraveGuardWalkFire",6,10,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TNT1 AA 0 A_Recoil(-1);
	TNT1 A 0 A_StartSound("Squire/Step");
	HFRY CCDD 3 A_Chase();
	TNT1 A 0 A_SpawnItemEx("PM_GraveGuardWalkFire",6,10,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TNT1 AA 0 A_Recoil(-1);
	TNT1 A 0 A_StartSound("Squire/Step");
	TNT1 A 0 A_Jump(1,"RushCheck");
    Loop;
  Missile:
		TNT1 A 0 A_JumpIfCloser(270,"FireBreathCheck");
		TNT1 A 0 A_Jump(65,"RushCheck");
		TNT1 A 0 A_Jump(90,"SoulSwarm","LStrike");
  NormalMissile:
		HFRY EEEEE 1 Light("HellsFuryBall1")
		{
		A_FaceTarget();
		A_Spawnitemex("PM_HFuryThrowFire",10,random(40,35),65,0,0,2);
		}
		HFRY EEEE 1 Light("HellsFuryBall1")
		{
		A_FaceTarget();
		A_Spawnitemex("PM_HFuryThrowFire",10,random(40,35),60,0,0,2);
		}
		HFRY FFFF 1 Light("HellsFuryBall1")
		{
		A_FaceTarget();
		A_Spawnitemex("PM_HFuryThrowFire",10,random(40,35),50,0,0,2);
		}
		HFRY G 8 Light("HellsFuryBall1")
		{
		A_FaceTarget();
		A_SpawnProjectile("PM_HellsFuryBall",40);
		}
		Goto See;
   FireBreathCheck:
    TNT1 A 0 A_Jump(70,"FireBreath");
	TNT1 A 0 A_Jump(45,"SoulSwarm","ShotgunSoulSwarm");
	Goto NormalMissile;
   FireBreath:
	TNT1 A 0 A_StartSound("CyberFodder/Flamer",CHAN_5,1,1);
	TNT1 A 0 A_StartSound("HFuryRoar",CHAN_7);
	HFRY PP 5 Light("HellsFuryBall1")
	{
	A_FaceTarget(360,180);
	A_SetAngle(angle+random(-5,5));
	A_SetPitch(pitch+random(-5,5));
	}
	HFRY PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 1 Light("HellsFuryBall1")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_HFuryFireFlare",5,0,65);
	A_SpawnProjectile("PM_HFuryFirebreath",67,0,random(-1,1));
	}
	TNT1 A 0 A_StopSound(CHAN_5);
	Goto See;
  RushCheck:
    TNT1 A 0 A_JumpIfHigherOrLower("See","See");
	Goto Rush;
  Rush:
    TNT1 A 0 A_SetSpeed(70);
    TNT1 A 0 A_StartSound("HFury/Rush",CHAN_5);
    HFRY A 15 A_FaceTarget();
    TNT1 A 0 A_StartSound("HFuryRushAdd",CHAN_6);
   	TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
	HFRY A 8 A_FaceTarget();
	TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    TNT1 AAAAAAA 1 Light("HellsFuryBall1")
    {
	A_Chase(null,null);
    A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    A_SpawnProjectile("PM_BlueSoulFlare",32,0,0);
    }
    TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
    TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    HFRY A 8 A_FaceTarget();
	TNT1 A 0 A_SetSpeed(10);
    TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    TNT1 A 0 A_JumpIfCloser(250,"FireBreathCheck");
    Goto See;
  SoulSwarm:
    TNT1 A 0 A_StartSound("HFury/SoulPrefire");
    TNT1 A 0 A_StartSound("GenericChargeSmall",CHAN_5);
	HFRY PPPPPPPPPPPPPPPPPPPPPPP 1 Light("HellsFuryBall1")
	{
	PM_LeadTarget(15);
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY PPPP 1 Light("HellsFuryBall1")
	{
	PM_LeadTarget(15);
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY PPPP 1 Light("HellsFuryBall1")
	{
	PM_LeadTarget(15);
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY PPPPPPPPPPPPPPPPP 2 Light("HellsFuryBall1")
	{
	A_FaceTarget(0.65,180);
	A_SpawnProjectile("PM_HellsFurySoul",65,0,random(-8,8),CMF_AIMDIRECTION,(pitch*1));
	}
	Goto See;
  LStrike:
    TNT1 A 0 
	{
	pm_attackloop = 2;
	A_StartSound("HFury/SoulPrefire",CHAN_5);
	}
	HFRY IIIIIIIIIIIIIIIIIIIIIIIIIIIII 1 Light("HellsFuryBall1")
	{
	A_FaceTarget();
	A_Spawnitemex("PM_HFuryThrowFire",10,46,65,0,0,2);
	A_Spawnitemex("PM_HFuryThrowFire",10,-46,65,0,0,2);
	}
	HFRY JJJJJJJJJJ 1 Light("HellsFuryBall1") A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/BigElectricCharge",6);
	HFRY KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK 1 Light("HellsFuryBall1")
	{
	A_FaceTarget();
	PM_GraveGuardLCharge();
	}
	TNT1 A 0 A_StartSound("HFury/Rush",7);
	HFRY JJJJJJJJJJ 1 Light("HellsFuryBall1") A_FaceTarget();
  LStrikeLoop:
	HFRY I 18 Light("HellsFuryBall2")
	{
	if(pm_attackloop < 0)
	SetStateLabel("LStrikeEnd");
	A_FaceTarget();
	A_StartSound("ArachChassisExplode",20);
	A_StartSound("Depraved/SuperShock1",21,pitch:frandom(0.55,1.00));
	A_StartSound("PM/ShockWarning",22);
	A_SpawnProjectile("PM_GGuardFocusedLightning",62,-45,frandom(-3,3),CMF_OFFSETPITCH,frandom(-1.5,1.5));
	pm_attackloop--;
	}
	HFRY I 18 Light("HellsFuryBall2")
	{
	if(pm_attackloop < 0)
	SetStateLabel("LStrikeEnd");
	A_FaceTarget();
	A_StartSound("ArachChassisExplode",20);
	A_StartSound("Depraved/SuperShock1",21,pitch:frandom(0.55,1.00));
	A_StartSound("PM/ShockWarning",22);
	A_SpawnProjectile("PM_GGuardFocusedLightning",62,45,frandom(-3,3),CMF_OFFSETPITCH,frandom(-1.5,1.5));
	pm_attackloop--;
	}
	Loop;
   LStrikeEnd:
    HFRY II 5 A_FaceTarget();
	Goto See;
  Pain:
    HFRY H 2;
	TNT1 A 0 A_SetSpeed(10);
	TNT1 A 0 A_StopSound(CHAN_5);
	TNT1 A 0 A_StopSound(CHAN_7);
    HFRY H 4 A_Pain;
	TNT1 A 0 A_Jump(75,"Evade");
    Goto See;
  Evade:
    TNT1 A 0 A_SetSpeed(70);
    HFRY H 5 A_FaceTarget();
    TNT1 A 0 A_StartSound("HFuryRushAdd",CHAN_6);
   	TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
	HFRY A 4 A_FaceTarget();
	TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    TNT1 AAAAAAA 1 Light("HellsFuryBall1")
    {
	A_Wander();
    A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    A_SpawnProjectile("PM_BlueSoulFlare",32,0,0);
    }
    TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
    TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
	TNT1 A 0 A_SetSpeed(10);
    HFRY A 8 A_FaceTarget();
    TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    Goto See;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_StopSound(CHAN_5);
	TNT1 A 0 A_StopSound(CHAN_7);
    HFD_ A 1 PM_Scream();
	HFD_ AAAAAAA 1 A_SpawnItemEx("PM_FlameSmoke",xofs:random(-10,10),yofs:random(-12,12),zofs:random(1,30),zvel:frandom(0.2,2));
    HFD_ A 0 
	{
	 for(int i=random(2,4);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodMulti",zofs:25,zvel:3,flags:SXF_NOCHECKPOSITION);
	}
    HFD_ A 1 A_SpawnItemEx("HellsFurySkull", 0, 0, 65, 5, 0, 0, RandomPick(20, 10, 0, -10, -20), SXF_CLIENTSIDE);
	HFD_ BBBBBBB 1 
	{
	A_SpawnItemEx("PM_FlameSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(10,30),zvel:frandom(0.2,2));
    PM_HellsFuryWither();
	}
    HFD_ B 0 
	{
	 for(int i=random(1,2);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodMulti",zofs:15,zvel:3,flags:SXF_NOCHECKPOSITION);
	 for(int i=random(1,2);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodSoul",xofs:random(-10,10),yofs:random(-15,15),zofs:random(15,20),yvel:random(-3,3),zvel:random(3,4),angle:random(0,360),flags:SXF_NOCHECKPOSITION);
	}
    HFD_ CCCCCC 1 A_SpawnItemEx("PM_DeathBloodSoul",yofs:random(-10,10),zofs:random(15,20),yvel:random(-3,3),zvel:random(3,4),flags:SXF_NOCHECKPOSITION);
    HFD_ C 0 
	{
	 for(int i=random(2,4);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodMulti",zofs:10,zvel:3,flags:SXF_NOCHECKPOSITION);
	 for(int i=random(1,3);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodSoul",xofs:random(-10,10),yofs:random(-15,15),zofs:random(15,20),yvel:random(-3,3),zvel:random(3,4),flags:SXF_NOCHECKPOSITION);
	}
    HFD_ D 3 A_Fall;
	HFD_ DDDEEEEEE 1 
	{
	A_SpawnItemEx("PM_FlameSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(5,15),zvel:frandom(0.2,2));
	PM_HellsFuryWither();
	}
	TNT1 A 0 A_StartSound("Corpse/FallMed",CHAN_6);
    HFD_ FFFFFFFFFFFFFFF 1 
	{
	A_SpawnItemEx("PM_FlameSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(5,15),zvel:frandom(0.2,2));
	PM_HellsFuryWither();
	}
  DeathLoop:
    HFD_ F 1
	{
	if(deathwither < 0)
	SetStateLabel("DeathEnd");
	A_SpawnItemEx("PM_FlameSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(5,15),zvel:frandom(0.2,2));
	PM_HellsFuryWither();
	deathwither--;
	}
    Loop;
  DeathEnd:
    HFD_ FFFFFFFFFFFFFFFF 1
	{
	A_FadeOut(0.05);
	A_SpawnItemEx("PM_FlameSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(5,15),zvel:frandom(0.2,2));
	PM_HellsFuryWither();
	}
	Stop;
   }
}

Class PM_HellsFuryBall : Actor
{
    Default
    {
    Radius 6;
    Height 8;
    Speed 13;
	FastSpeed 23;
    DamageFunction (random(30,40));
    RenderStyle "Add";
    Alpha 1;
	Scale 1;
	SeeSound "HFuryProj";
    DeathSound "HFuryProjExp";
	Decal "PM_DeathSoulScorchBig";
	DamageType "Fire";
	+SEEKERMISSILE
    Projectile;
    }
  States
  {
  Spawn:
    PSMM DDEEFF 1 Bright Light("HellsFuryBall1")
    {
	A_SpawnItemEx("PM_HFuryFlameTrails",0,0,0,0,0,0,0,128,0);
	A_SeekerMissile(1,1);
	A_SpawnParticleEx
	   (
		"",
		TexMan.CheckForTexture("DTPRB0"),
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,2),
		size: frandom(40.0,200.0),
		angle: 0,
		xoff: -8,
		startalphaf: frandom(0.2,0.95),
		startroll: random(-180,180)
	    );
	A_SpawnParticleEx
			(
			"",
			TexMan.CheckForTexture("BLULA0"),
			style: STYLE_Add,
			flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: random(1,5),
			size: random(1,120),
			velx: random(-15,1),
			vely: random(-1,1),
			velz: random(-1,1),
			startalphaf: frandom(0,1),
			startroll: random(-180,180)
			);
	A_SpawnParticleEx
	    (
		"61A9FF",
		TexMan.CheckForTexture("SPKOS0"),
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(10,70),
		size: frandom(0.2,15),
		xoff: random(-7,0),
		yoff: random(-5,5),
		zoff: random(-5,5),
		velx: frandom(-0.6,0.6),
		vely: frandom(-0.6,0.6),
		velz: frandom(-0.6,1),
		startalphaf: 1.0,
		fadestepf: -1,
		sizestep: -0.2,
		startroll: random(-180,180)
		);
	A_SpawnParticleEx
	(
		"",
		TexMan.CheckForTexture("GNSML0"),
		style: STYLE_None,
		flags: SPF_ROLL|SPF_RELATIVE,
		lifetime: 56,
		size: 30.0,
		xoff: -10,
		startalphaf: 0.4,
		fadestepf: -1,
		startroll: random(-180,180)
	);
	}
	//TNT1 A 0 A_CustomRailgun(5,0,"","White",RGF_SILENT|RGF_CENTERZ|RGF_FULLBRIGHT,0,9000,"PM_BlueLongFlare2",180,180,random(5,100),0,3,0,"PM_ConduitArcs");
    Loop;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_StartSound("imp/shotx",CHAN_7);
	TNT1 AAAAA 0 A_SpawnItemEx("PM_BlueShockBig",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_BlueLongFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("NetherShockFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TNT1 AAA 0 A_SpawnItemEx("PM_BlueFireShrapnel",0,0,0,random(-10,10),random(10,10),random(1,10),0,SXF_NOCHECKPOSITION);
	//TNT1 AAA 0 A_CustomRailgun(5,0,"","White",RGF_SILENT|RGF_CENTERZ|RGF_FULLBRIGHT,0,9000,"PM_BlueLongFlare2",180,180,random(50,800),0,3,0,"PM_ConduitArcs");
	//TNT1 A 0 A_CustomRailgun(5,0,"","White",RGF_SILENT|RGF_CENTERZ|RGF_FULLBRIGHT,0,9000,"PM_BlueLongFlare2",180,180,random(50,800),0,3,0,"PM_ConduitArcs");
	//TNT1 A 0 A_CustomRailgun(5,0,"","White",RGF_SILENT|RGF_CENTERZ|RGF_FULLBRIGHT,0,9000,"PM_BlueLongFlare2",180,180,random(50,800),0,3,0,"PM_ConduitArcs");
	TNT1 AAAA 0 A_SpawnItemEx("PM_HFuryBlueBallExplode",0,0,0,random(-6,6),random(-6,6),random(-6,6),0,SXF_NOCHECKPOSITION);
	TNT1 AAAAAAAAAAAA 0 A_SpawnProjectile("PM_BlueShock",2,0,random(0,360),CMF_OFFSETPITCH,random(0,360));
    HFBB CDE 5 Bright Light("HellsFuryBall2");
    Stop;
  }
}

Class PM_HFuryThrowFire : PM_RedFireEffect
{
  Default
  {
  Scale 0.3;
  Translation "0:255=%[0,0,0]:[0.4,0.7,2.0]";
  }
  States
{
  Spawn:
	FIR1 R 1
	{
	A_FadeOut(0.05);
	A_SetRoll(random(0,360));
	A_SetScale(Scale.X-0.015);
	}
	Loop;
	}
}

Class HellsFurySkull : Actor
{
    int timer;
    Default
	{
	Translation "174:191=193:207", "32:47=240:247";
    Radius 5;
    Height 5;
    Speed 5;
    Mass 1500;
	Scale 1.15;
    +DropOff
    +DoomBounce
    +Missile
	}

	override void PostBeginPlay()
	{
	 tex[0] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	 Super.PostBeginPlay();
	}
	
	TextureID tex[2];
	
  States
  {
  Spawn:
    TNT1 A 0 NoDelay { timer = random(30,50); }
  Go:
    FSKL ABCDEFGHIJK 2
	{
	 if(timer < 0)
	 SetStateLabel("Death");
	 A_SpawnParticleEx
	 (
	 "545454",
	 tex[1],
	 style: STYLE_None,
	 flags: SPF_ROLL|SPF_RELATIVE,
	 lifetime: random(35,70),
	 size: frandom(28,56),
	 xoff: frandom(-10,10),
	 yoff: frandom(-10,10),
	 zoff: 0,
	 velx: frandom(-1,1),
	 vely: frandom(-1,1),
	 velz: frandom(0,2),
	 startalphaf: frandom(0.2,0.95),
	 fadestepf: -1,
	 sizestep: frandom(-0.8,0.8),
	 startroll: random(-180,180),
	 rollvel: frandom(-0.3,0.3)
	 );
	 A_SpawnParticleEx
	 (
	 "545454",
	 tex[0],
	 style: STYLE_Normal,
	 flags: SPF_RELATIVE|SPF_ROLL,
	 lifetime: random(20,120),
	 size: frandom(1,15),
	 xoff: random(-10,10),
	 yoff: random(-15,15),
	 zoff: 5,
	 velx: frandom(-1,1),
	 vely: frandom(-1,1),
	 velz: frandom(1,4),
	 startalphaf: 1.0,
	 fadestepf: -1,
	 sizestep: -0.4,
	 startroll: random(-180,180),
	 rollvel: random(0,4)
	 );
	 timer--;
	}
    Loop;
  Death:
    FSKL L 1
	{
	 A_FadeOut(0.015);
	 A_SpawnParticleEx
	 (
	 "545454",
	 tex[1],
	 style: STYLE_None,
	 flags: SPF_ROLL|SPF_RELATIVE,
	 lifetime: random(35,70),
	 size: frandom(28,56),
	 xoff: frandom(-10,10),
	 yoff: frandom(-10,10),
	 zoff: 0,
	 velx: frandom(-1,1),
	 vely: frandom(-1,1),
	 velz: frandom(0,2),
	 startalphaf: frandom(0.2,0.95),
	 fadestepf: -1,
	 sizestep: frandom(-0.8,0.8),
	 startroll: random(-180,180),
	 rollvel: frandom(-0.3,0.3)
	 );
	 A_SpawnParticleEx
	 (
	 "545454",
	 tex[0],
	 style: STYLE_Normal,
	 flags: SPF_RELATIVE|SPF_ROLL,
	 lifetime: random(20,120),
	 size: frandom(1,15),
	 xoff: random(-10,10),
	 yoff: random(-15,15),
	 zoff: 5,
	 velx: frandom(-1,1),
	 vely: frandom(-1,1),
	 velz: frandom(1,4),
	 startalphaf: 1.0,
	 fadestepf: -1,
	 sizestep: -0.4,
	 startroll: random(-180,180),
	 rollvel: random(0,4)
	 );
	}
    Loop;
  }
}

Class PM_BlueDropFire : ZombieDropFire
{
 Default
 { Translation "0:255=%[0,0,0]:[0.6,0.9,2.0]"; }
}

Class PM_HellsFurySoul : Actor
{
	int soulhspeed;
	int soulvspeed;
	int soulhdist;
	int soulvdist;
   Default
   {
	DamageFunction 8;
	DamageType "Soul";
	Projectile;
	+SEEKERMISSILE
	DeathSound "Abhor/SoulExp";
	Decal " ";
	RenderStyle "Add";
	Alpha 1.0;
	Height 5;
	Radius 5;
	Speed 15;
	FastSpeed 25;
	Scale 0.35;
   }
   
   override void PostBeginPlay()
   {
    tex[0] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
   }
   
   TextureID tex[1];

	states
  	{
  Spawn:
    TNT1 A 0 NoDelay
	{
	 soulhspeed = random(-2,2);
	 soulvspeed = random(-2,2);
	 soulhdist = random(-3,3);
	 soulvdist = random(-3,3);
	}
	TNT1 A 0 A_StartSound("HFury/SoulScreech",pitch:frandom(0.6,1.2));
	TNT1 A 0 
	{
	A_SeekerMissile(1,5);
	A_ScaleVelocity(frandom(0.85,1));
	}
  Go:
    SHBA AAABBB 1 BRIGHT//Light("HellsFuryBall1")
	{
	 if(random(1,20) == 1)
	 SetStateLabel("WeaveReset");
	 A_SpawnParticleEx
	 (
	 "0B3D94",
	 tex[0],
	 style: STYLE_Add,
	 flags: SPF_ROLL|SPF_RELATIVE,
	 lifetime: random(15,20),
	 size: 20.0,
	 xoff: -5,
	 startalphaf: frandom(0.25,0.65),
	 fadestepf: -1,
	 startroll: random(-180,180)
	 );
	 A_SpawnParticleEx
	 (
	 "143B76",
	 tex[0],
	 style: STYLE_Add,
	 flags: SPF_ROLL|SPF_RELATIVE,
	 lifetime: random(6,10),
	 size: 35.0,
	 xoff: -5,
	 startalphaf: frandom(0.5,0.8),
	 fadestepf: -1,
	 startroll: random(-180,180)
	 );
	 A_Weave(soulhspeed,soulvspeed,soulhdist,soulvdist);
	}
	loop;
  WeaveReset:
    TNT1 A 0
	{
	 soulhspeed = random(-2,2);
	 soulvspeed = random(-2,2);
	 soulhdist = random(-3,3);
	 soulvdist = random(-3,3);
	}
	Goto Go;
  Death:
    SHBA CDEFGHIKL 2 Bright Light("HellsFurySoul1");
    TNT1 A 5 Bright Light("HellsFurySoul2");
	TNT1 A 3 Bright Light("HellsFurySoul3");
    stop;
  }
}

Class PM_HellsFurySpreadFire : Actor
{
   Default
   {
	DamageFunction 18;
	DamageType "Soul";
	+NOGRAVITY
	-LOWGRAVITY
	+MISSILE
	seesound "Abhor/SoulExp";
	Decal " ";
	RenderStyle "Add";
	Alpha 1.0;
	Speed 15;
	FastSpeed 15;
	Scale 0.50;
   }
	states
  	{
  Spawn:
    PSMM DDEEFF 1
	{
	A_SpawnItemEx("PM_WhiteFlameTrails",0,0,0,0,0,0,0,128,0);
	}
	loop;
  Death:
	TNT1 A 0 A_SpawnItemEx("PM_WhiteFireballExplode");
	TNT1 A 0 A_SpawnItemEx("PM_WhiteFireballExplosionFlames");
    AFX5 D 2 Bright Light("PM_SmallWhiteExpLight");
    AFX5 EFGH 2 Bright Light("PM_SmallWhiteExpLight");
    stop;
  }
}

Class PM_HFuryFirebreath : PM_CyberFodderFire
{
   Default
  {
   Radius 2;
   Height 4;
   Speed 12;
   Damage 1;
   PROJECTILE;
   RENDERSTYLE "Add";
   DamageType "Fire";
   Translation "0:255=%[0,0,0]:[0.6,0.9,2.0]";
   Scale 0.02;
   DeathSound "Fire/Unleash";
   Decal "PM_DeathSoulScorchMed";
   +THRUGHOST
   +ROLLSPRITE
   +FORCEXYBILLBOARD
  }
   States
   {
   Spawn:
	  TNT1 A 0;
      NULL A 1 Bright;
	  TNT1 A 0 A_Jump(255,"Go","Go2");
	  Go:
	  TNT1 A 0 A_SetRoll(random(0,360));
      FLMF ABCDEFEDCBA 2 Bright Light("HellsFuryBall1")
	  {
	  A_SpawnItemEx("PM_FlameSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,248);
	  A_SetScale(Scale.X+0.04);
	  }
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF ABCDEF 2 Bright Light("HellsFuryBall1");
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF EDCBABC 1 Bright Light("HellsFuryBall1")
	  {
	  A_SetScale(Scale.X+0.05);
	  A_SpawnItemEx("PM_FlameSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,240);
	  A_Fadeout(0.1);
	  }
	  Stop;
	  Go2:
	  TNT1 A 0 A_SetRoll(random(0,360));
	  FLMF FEDCBABCDEF 2 Bright
	  {
	  A_SpawnItemEx("PM_FlameSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,248);
	  A_SetScale(Scale.X+0.03);
	  }
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF EDCBA 2 Bright;
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF BCDEFED 1 Bright
	  {
	  A_SetScale(Scale.X+0.05);
	  A_SpawnItemEx("PM_FlameSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,240);
	  A_Fadeout(0.1);
	  }
      stop;
   Death:
      FRFX HIJ 2 Bright;
      FRFX J 0 A_SpawnProjectile ("PM_BlueDropFire",0,0,0,4);
      FRFX KLM 2 Bright;
      FRFX NO 2 Bright;
      stop;
   }
}

Class PM_GGuardFocusedLightning : Actor
{
Default
{
 Projectile;
 +BLOODSPLATTER 
 +THRUGHOST
 +SEEKERMISSILE
 Decal "PM_DeathSoulScorchBig";
 Height 8;
 Radius 8;
 Speed 37;
 DamageFunction 30;
 DamageType "Lightning";
 Scale 1.0;
 RenderStyle "Add";
 Alpha 0.60;
}
 override void PostBeginPlay()
 {
 Super.PostBeginPlay();
 tex[0] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
 tex[1] = TexMan.CheckForTexture("SPKBC0",TexMan.Type_Sprite);
 tex[2] = TexMan.CheckForTexture("SPKBA0",TexMan.Type_Sprite);
 tex[3] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
 tex[4] = TexMan.CheckForTexture("BLULA0",TexMan.Type_Sprite);
 tex[5] = TexMan.CheckForTexture("DTPRB0",TexMan.Type_Sprite);
 }
 
 TextureID tex[6];

States
{
	Spawn:
        TNT1 A 0;
		TNT1 A 0
		{
		A_StartSound("NetherConduit/ThunderOrb",pitch:1.35);
		A_SpawnParticleEx
			(
			"0B3D94",
			tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL,
			lifetime: random(10,90), size: frandom(65,100), xoff: -25,
			velx: frandom(0,2), vely: frandom(-0.4,0.4), velz: frandom(-0.4,0.7),
			startalphaf: frandom(0.45,0.85), fadestepf: -1, sizestep: frandom(0.0,1.4), 
			startroll: random(-180,180), rollvel: frandom(-1,1)
			);
			A_SpawnParticleEx
			(
			"",
			tex[1],
			style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,4),
			size: frandom(200,500), xoff: -34, startalphaf: 1.0,
			fadestepf: 0, startroll: random(-180,180)
			);
			A_SpawnParticleEx
			(
			"",
			tex[1],
			style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,65),
			size: frandom(150,200), xoff: -34, startalphaf: 0.55,
			fadestepf: -1.5, startroll: random(-180,180)
			);
			for(int i=random(5,15);i>0;i--)
			A_SpawnParticleEx
			(
			"",
			tex[2],
			style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
			lifetime: random(1,75), size: frandom(1,35), xoff: -22,
			velx: frandom(5,8), vely: frandom(-4,4), velz: frandom(-5,5),
			startalphaf: 1.0, fadestepf: -1, sizestep: frandom(-1,-0.1), startroll: random(-180,180)
			);
		}
	Go:
		TRCR A 1 BRIGHT 
		{
		A_Quake(1,4,0,100,0);
		A_SpawnParticleEx
			( "", tex[5], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,5),
			size: frandom(100,190), xoff: 44, startalphaf: 0.65,
			fadestepf: -1.5, startroll: random(-180,180) );
		A_SpawnParticleEx
			( "", tex[4], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,3),
			size: frandom(50,140), xoff: 30, startalphaf: 1.0,
			fadestepf: -1.2, startroll: random(-180,180) );
		}
        Loop;
	Death:
	    TNT1 A 0
		{
		 A_RadiusThrust(600,400,RTF_NOIMPACTDAMAGE);
		 A_QuakeEx(2,2,2,10,0,700,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,rollintensity:0.8,rollwave:0.8);
		A_SpawnParticleEx
		 ( "", tex[1], style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, lifetime: random(2,20),
		 size: random(200,500), startalphaf: 0.95, fadestepf: -1, sizestep: -15, startroll: random(-180,180) );
		A_SpawnParticleEx
		 (
		 "",
		 tex[2],
		 style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, lifetime: random(75,120),
		 size: random(200,350), startalphaf: 0.95, fadestepf: -3.5, sizestep: -0.5, startroll: random(-180,180));
	   for(int i=random(5,15);i>0;i--) 
	   A_SpawnParticleEx
		 ( "3d85c6", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		 lifetime: random(10,75), size: frandom(5,15),
		 velx: frandom(-4,4), vely: frandom(-4,4), velz: frandom(1,15),
		 accelz: frandom(-0.1,-0.9), startalphaf: frandom(0.45,0.95), fadestepf: -1,
		 sizestep: -0.5, startroll: random(-180,180));
	   for(int i=12;i>0;i--) 
	   A_SpawnParticleEx
	    ( "727272", tex[3], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,
	    lifetime: random(40,70), size: frandom(2,35), zoff: random(10,40),
	    velx: frandom(-8,8), vely: frandom(-8,8), velz: frandom(-8,12),
	    accelz: frandom(-0.1,-0.5), startalphaf: 1.0, sizestep: -0.2, startroll: random(-180,180), rollvel: random(-4,4));
		}
		TRCR A 0 
		{
		A_StartSound("PM/WrathTele2",20,pitch:frandom(0.75,1.00));
		A_StartSound("NetherConduit/ZapExp",21,pitch:frandom(0.75,1.00));
		}
		Stop;
	}
}